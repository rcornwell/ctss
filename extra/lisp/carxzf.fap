H      HED
*      ERRORSET(E,N,SW,AL)
       REM
ERRSET SXD     HORN,4
       TSX     $SAVE,4
       TXL     $END8,,HORN+9
       SXD     HORN+1,2
       SXA     HORN+1,1
       STI     HORN+4
       PDX     0,1
       LXD     $ARG3,4
       SXA     ERNULL,4
       XCA
       PDX     0,2
       TSX     FIXVAL,4
       STO     HORN+6
       CLA     $CNTR1
       ANA     $AMASK
       ADD     $CNTS
       SUB     HORN+6
       TMI     OBOE
       SSM
       STO     HORN+5
       CLA     HORN+6
       STA     $CNTR1
       ANA     PDTMSK
       STO     $CNTS
       TRA     *+2
 OBOE  STZ     HORN+5
       LDQ     $ARG4
       CLS     $CPPI
       STO     HORN+6
       STL     TCOUNT
       AXT     BSOON,4
       SXA     EREXIT,4
*      ATTEMPT TO PERFORM THE EVALUATION
       PXD     0,1
       TSX     $EVAL,4
*      WE GET HERE IF THE EVALUATION WORKED
       LDQ     =0
       TSX     $CONS,4
*      AN ERROR IS THIS CONS ACTS LIKE AN ERROR IN THE EVALUATION
       TRA     SHAWM
*      WE GET HERE IN CASE OF ERROR
 BSOON LDC     HORN+6,4
       SXD     TUBA,4
       TRA     TUBA-1
HARP   LXD     $CPPI,4
       CAL     -1,4
       ANA     $PMASK
       ERA     $QP5
       TZE     *+3
       TSX     UNSAVE,4
       TRA     *+2
       TSX     C$UNWND,4
       LDC     $CPPI,4
TUBA   TXH     HARP,4,**
       PXD     0,0
*      RESTORE PARAMETERS FOR EITHER KIND OF EXIT
 SHAWM STO     HORN+6
       CLA     $CNTR1
       ANA     $AMASK
       ADD     $CNTS
       SUB     HORN+5
       STA     $CNTR1
       ANA     PDTMSK
       STO     $CNTS
       LXA     HORN+1,1
       LXD     HORN+1,2
       LDI     HORN+4
       CLA     HORN+6
       TSX     UNSAVE,4
       LXD     HORN,4
       TRA     1,4
*      PROTECTED TEMPORARY STORAGE FOR ERRORSET
       HEAD    H
 HORN  MZE     ERSETO
       MZE
ERNULL MZE     *
EREXIT TXL     EVQERR
       MZE
       MZE
NUBPDL MZE
TCOUNT MZE
       HEAD    0
 TERA2 SYN     EREXIT
       REM
       HEAD    H
CAAARX SXA     CAX,4
       PDX     0,4
       CLA     0,4
       PAX     0,4
 AA    CLA     0,4
       PAX     0,4
 A     CLA     0,4
       PAX     0,4
       PXD     0,4
 CAX   AXT     **,4
       TRA     1,4
       REM
CAADRX SXA     CAX,4
       PDX     0,4
       CLA     0,4
 AAX   PDX     0,4
       TRA     AA
       REM
CADARX SXA     CAX,4
       PDX     0,4
       CLA     0,4
       PAX     0,4
 AD    CLA     0,4
       PDX     0,4
       TRA     A
CADDRX SXA     CAX,4
       PDX     0,4
       CLA     0,4
 ADX   PDX     0,4
       TRA     AD
       REM
CAARXX SXA     CAX,4
       TRA     AAX
       REM
CADRXX SXA     CAX,4
       TRA     ADX
       REM
CDAARX SXA     CDX,4
       PDX     0,4
       CLA     0,4
       PAX     0,4
 DA    CLA     0,4
       PAX     0,4
 D     CLA     0,4
       ANA     $DMASK
 CDX   AXT     **,4
       TRA     1,4
CDADRX SXA     CDX,4
       PDX     0,4
       CLA     0,4
 DAX   PDX     0,4
       TRA     DA
       REM
CDDARX SXA     CDX,4
       PDX     0,4
       CLA     0,4
       PAX     0,4
 DD    CLA     0,4
       PDX     0,4
       TRA     D
       REM
CDDDRX SXA     CDX,4
       PDX     0,4
       CLA     0,4
 DDX   PDX     0,4
       TRA     DD
       REM
CDARXX SXA     CDX,4
       TRA     DAX
       REM
CDDRXX SXA     CDX,4
       TRA     DDX
       REM
       HEAD    C
 GET   SXA     GETX,4
       STO     GETL
       CLA FCN31
       STO $ARG3
       CLA GETL
       TSX $PROP,4
       PDX 0,4
       CLA 0,4
       PAX 0,4
       PXD 0,4
 GETX  AXT     **,4
       TRA 1,4
 FCN31 TXL     GETX,,0
 GETL
*
* COMPAT
*
COMPAT SXA     CX,4
       SXA     CY,2
       STO     $ARG1
       STQ     $ARG2
       LDQ     =0
       CLA     1,4
       STD     CA
       PAC     0,2
 CL    TXL     CD,2,0
       CLA     $ARG1-1,2
       TSX     $CONS,4
       XCA
       TXI     CL,2,1
 CD    CLA     CA
       STZ     $ARG3
 CX    AXT     **,4
 CY    AXT     **,2
       TXI     $APPLY,4,-1
 CA
F      HED
*      PACK(CHAR)
*
 PACK  ARS     18
       SUB     HORG
       LGR     6
       CAL     CHARS
       TOV     *+1
       LGL     6
       TOV     B5
       SLW     CHARS
       PXD     ,0
       TRA     1,4
 B5    SXA     B1,4
 BFLOC AXT     20,4
       SLW     BOFFO,4
       TNX     B3,4,1
       CLA     =1
       STO     CHARS
       SXA     BFLOC,4
 B1    AXT     ,4
       PXD     ,0
       TRA     1,4
 B3    TXL     B4,4,0
       SXA     BFLOC,0
       TRA     B6
 B4    TSX     $MKNAM,4
       TSX     INTRN1,4
       ERROR   PKG120
 B6    CLA     SEVENS
       STO     CHARS
       TRA     B1
       REM
*      NUMOB
       REM
 NUMOB SXA     GV1,4
       TOV     *+1
       CAL     CHARS
       LDQ     SEVENS
       LGL     6
       TNO     *-1
       LXA     BFLOC,4
       SLW     BOFFO,4
       CLA     PARAM
       TSX     NUMBR,4
       TZE     GV3
       TPL     GV2
       XCA
       LDQ     FLOS
       TSX     $MKNO,4
       LXA     GV1,4
       TRA     CLEAR
 GV2   PBT
       TRA     GV6
       XCA
       LDQ     $OCTD
       TSX     $MKNO,4
       LXA     GV1,4
       TRA     CLEAR
 GV6   XCA
       TMI     GV4
       CAS     $Q10
       TRA     GV4
       TRA     GV4
       ACL     HORG
       ALS     18
       LXA     GV1,4
       TRA     CLEAR
 GV4   LDQ     FIXS
       TSX     $MKNO,4
 GV1   AXT     ,4
       TRA     CLEAR
GV3    ERROR   NUMOB
       PXD     0,0
       TRA     GV1
       REM
*      MKNAM AND CLEARBUFF
       REM
 MKNAM SXA     BB1,4
       SXA     BBIR2,2
       CAL     CHARS
       LAS     =1
       TRA     BB5
       PXD     ,0
       TRA     BB2
 BB5   TOV     *+1
       LDQ     SEVNS
       LGL     6
       TNO     *-1
       SLW     T1
       CLA     T1
       TSX     $CONSW,4
       LDQ     =0
       TSX     $CONS,4
 BB2   LXA     BFLOC,2
 BB4   TXH     BBIR2,2,19
       SLW     BBPNT
       CLA     BOFFO-1,2
       TSX     $CONSW,4
       LDQ     BBPNT
       TSX     $CONS,4
       TXI     BB4,2,1
 BBIR2 AXT     **,2
       TRA     BB3
 CLEAR SXA     BB1,4
 BB3   LDQ     =1
       STQ     CHARS
       AXT     20,4
       SXA     BFLOC,4
       STZ     BBPNT
 BB1   AXT     ,4
       TRA     1,4