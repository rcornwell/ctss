A      HED
 APPLY SXD ASS1,4
       TZE 1,4
       STO AST1
       PDX 0,4
       SXA     ASS1,4
       CLA 0,4
       PAX 0,4
       TXH ASP1,4,-2
       PXD 0,4
       CAS ASLMD
       TRA *+2
       TRA ASP2
       CAS ASFUN
       TRA *+2
       TRA ASP4
       CAS ASLBL
       TRA *+2
       TRA ASP3
       TSX $SAVE,4
       TXL     $END3,,ASSA+2
       STQ ASSL
       LDQ $ARG3
       STQ ASSA
       CLA AST1
       TSX $EVAL,4
       LDQ ASSA
       STQ $ARG3
       LDQ ASSL
       TSX UNSAVE,4
       LXD ASS1,4
       TRA APPLY
       REM
 ASP1  CLA AST1
       LXD ASS1,4
       TRA $APP2
 ASP2  LXD AST1,4
       CLA $ARG3
       STO AST3
       CLA 0,4
       PDX 0,4
       CLA 0,4
       STO AST4
       PAX 0,4
       PXD 0,4
       TSX $PAIR,4
       LDQ AST3
       TSX     $NCONC,4
       XCA
       LXD AST4,4
       CLA 0,4
       PAX 0,4
       PXD 0,4
       LXD ASS1,4
       TRA $EVAL
       REM
 ASP3  LXD AST1,4
       STQ AST2
       LDQ $ARG3
       STQ AST3
       CLA 0,4
       PDX 0,4
       CLA 0,4
       STO AST4
       PDX 0,4
       CLA 0,4
       PAX 0,4
       PXD 0,4
       STO AST1
       XCA
       LXA AST4,4
       PXD 0,4
       TSX $CONS,4
       LDQ AST3
       TSX $CONS,4
       STO $ARG3
       LDQ AST2
       CLA AST1
       LXD ASS1,4
       TRA APPLY
       REM
 ASP4  LXD AST1,4
       CLA ,4
       PDX ,4
       CLA ,4
       STO AST1
       PDX ,4
       CLA ,4
       PAX ,4
       PXD ,4
       STO $ARG3
       LXA AST1,4
       PXD ,4
       LXD ASS1,4
       TRA $APPLY
 ASLBL SYN LABELD
 ASLMD SYN LAMDAD
 ASFUN SYN     FNARGD
       REM
A      HED
 APP2  SXD     ATS1,4
       PDX     ,4
       CLA     ATS1
       SXA     ATS1,4
       STO     CSV
       PXD     ,4
       LXD     $ARG3,4
       SXD     A,4
       STQ     AL
       STO     F
       STZ     APTRT
 APSES PDX     0,4
       TXL     APSAL,4,0
       CLA     0,4
       PAX     0,4
       TXL     *+2,4,$TRACE-1
       TXL     APTRK,4,$TRACE
       TXL     *+2,4,$SUBR-1
       TXL     R2,4,$SUBR
       TXL     APSES,4,$EXPR-1
       TXH     APSES,4,$EXPR
       REM
       PDX     0,4
       CLA     0,4
       PAX     0,4
       PXD     0,4
       ZET     APTRT
       TRA     APTXP
       LXD     ATS1,4
       TRA     $APPLY
       REM
       PDX     0,4
       CLA     0,4
       PAX     0,4
       CLA     0,4
       STO     CWADR
       CLA     AL
       TSX     SPREAD,4
       ZET     APTRT
       TRA     APTSB
       TSX     $SAVE,4
       TXL     $END2,,$ALIST+2
       LXD     A,4
       SXD     $ALIST,4
       TSX     CWADR,4
       TSX     UNSAVE,4
       LXD     CSV,4
       TRA     1,4
       REM
APSAL  CLA     FAS
       STO     $ARG3
       CLA     F
       LDQ     A
       TSX     SASSOC,4
       PDX     0,4
       CLA     0,4
       PDX     0,4
       PXD     0,4
       LDQ     A
       STQ     $ARG3
       LDQ     AL
       ZET     APTRT
       TRA     APTXP
       LXD     ATS1,4
       TRA     $APPLY
       REM
APTXP  TSX     $SAVE,4
       TXL     $END1,,CSV+2
       TSX     $APPLY,4
       TRA     APEXC
       REM
R33    CLA     F
       ERROR   UNDFFN
       REM
 APTRK STL     APTRT
       STO     APA
       CLA     ASS1
       STO     CSV
****INSERTION ABOVE**********
       LXA     ASS1,4
       PXD     0,4
       TSX     ARGOF,4
       LDQ     AL
       CLA     APA
       TRA     APSES
       REM
APTSB  TSX     $SAVE,4
       TXL     $END2,,$ALIST+2
       LXD     A,4
       SXD     $ALIST,4
       TSX     CWADR,4
APEXC  TSX     UNSAVE,4
       XCA
       LXA     CSV,4
       PXD     0,4
       LXD     CSV,4
       TRA     VALOF
       REM
 APA
 APTRT
 CWADR
 ATS1
 FAS   TXL     R33,,0
 F
 AL
 A
       REM
       REM
A      HED
 EVCON TZE $EVAL
       SXD ECS1,4
       TSX $SAVE,4
       TXL     $END4,,ECS4+2
       STQ ECS2
       PDX 0,4
 E1    CLA 0,4
       STO ECS3
       PAX 0,4
       CLA 0,4
       STO ECS4
       PAX 0,4
       PXD 0,4
       TSX $EVAL,4
       LDQ ECS2
       TZE E2
       LXD ECS4,4
       CLA 0,4
       PAX 0,4
       PXD 0,4
E3     TSX UNSAVE,4
       LXD ECS1,4
       TRA $EVAL
 E2    LXD ECS3,4
       TXH E1,4,0
       ZAC
       TRA     E3
       REM BASIC LISP FUNCTIONS FOR APPLY
       REM
       REM
R      HED
       REM CAR
       REM
CARP   SXA     CARX,4
       PDX ,4
       CLA ,4
       PAX ,4
       PXD ,4
CARX   AXT     **,4
       TRA 1,4
 BFS1
CDRP   SXA     CDRX,4
       PDX ,4
       CLA ,4
       ANA BFDM
CDRX   AXT     **,4
       TRA 1,4
 BFDM  SYN     $DMASK
       REM
       REM
ATOMP  SXA     ATMX,4
       TZE ATP1
       PDX ,4
       CLA ,4
       PAX ,4
       TXL *+3,4,-2
 ATP1  CLA BFQ1
       TRA *+2
       PXD ,0
ATMX   AXT     **,4
       TRA 1,4
 BFQ1  SYN     $QD1
       REM
 NULLP TZE *+3
       PXD ,0
       TRA 1,4
       CLA BFQ1
       TRA 1,4
       REM
       REM
       REM LAMBDA FOR FUNCTIONA ARGUMENTS
       REM
 LAMP  SXD BFS1,4
       STO BFS2
       XCA
       LDQ =0
       TSX $CONS,4
       XCA
       CLA BFS2
       TSX APPEND,4
       XCA
       CLA BFFAG
       LXD BFS1,4
       TRA $CONS
 BFFAG SYN     FNARGD
       REM
       REM LABEL FSUBR
       REM
 LABP  SXD BFS1,4
       STQ BFS3
       PDX ,4
       CLA ,4
       STO BFS2
       PDX ,4
       CLA ,4
       PAX ,4
       PXD ,4
       STO BFS4
       XCA
       LXA BFS2,4
       XCA
       PXD ,4
       TSX $CONS,4
       LDQ BFS3
       TSX $CONS,4
       XCA
       CLA BFS4
       LXD BFS1,4
       TRA $EVAL
       REM
       REM
       REM SETQ
       REM
 SETQP SXD REPS1,4
       TSX $SAVE,4
       TXL     $END2,,REPV+2
       PDX ,4
       CLA ,4
       PAX ,4
       SXD REPV,4
       PDX ,4
       CLA ,4
       PAX ,4
       PXD ,4
       TSX $EVAL,4
       STO REPT1
       CLA REPP1
       STO $ARG3
       LDQ     $ALIST
       CLA REPV
       TSX SASSOC,4
       PDX ,4
       CLA REPT1
       STD     0,4
       TSX UNSAVE,4
       LXD REPS1,4
       TRA 1,4
       REM
 REPP1 TXL *+1,,0
       CLA REPV
       ERROR   SETQNV
       REM
       REM
       REM SET
       REM
 SETP  SXD BFS1,4
       STO BFS5
       STQ BFS2
       LDQ SETP1
       STQ $ARG3
       LDQ PRGVAR
       TSX SASSOC,4
       PDX ,4
       CLA BFS2
       STD 0,4
       LXD BFS1,4
       TRA 1,4
       REM
 SETP1 TXL *+1,,0
       CLA BFS5
       ERROR   SETNVR
BFS5
       REM
       REM
 EVA8  TNZ EVA6
       CLA EVCT
       TRA 1,4
 EVA6  SXD EVA1,4
       TSX $SAVE,4
       TXL     $END3,,EVA9+2
       PDX ,4
 EVA4  CLA ,4
       STO EVA2
       PAX ,4
       PXD ,4
       STQ EVA9
       TSX $EVAL,4
       LDQ EVA9
       TNZ EVA3
 EVA5  TSX UNSAVE,4
       LXD EVA1,4
       TRA 1,4
 EVA3  LXD EVA2,4
       TXH EVA4,4,0
       CLA EVCT
       TRA EVA5
       REM
 EVR8  TNZ EVR6
       ZAC
       TRA 1,4
 EVR6  SXD EVR1,4
       TSX $SAVE,4
       TXL     $END3,,EVR9+2
       PDX ,4
 EVR4  CLA ,4
       STO EVR2
       PAX ,4
       PXD ,4
       STQ EVR9
       TSX $EVAL,4
       LDQ EVR9
       TZE EVR3
       CLA EVCT
 EVR5  TSX UNSAVE,4
       LXD EVR1,4
       TRA 1,4
 EVR3  LXD EVR2,4
       TXH EVR4,4,0
       ZAC
       TRA EVR5
 EVCT  SYN     $QD1
       REM
 EQP   STQ BFS1
       SUB BFS1
       TNZ *+3
       CLA BFQ1
       TRA 1,4
       PXD ,0
       TRA 1,4
       REM
       REM EVAL(E,A)  5/6/59
       REM
A      HED
 EVAL  SXD EVS1,4
       TZE 1,4
       STO EVTE
       PDX ,4
       CLA ,4
       STT     EVLNS
       ZET     EVLNS
       TRA     EV1N
       PAX ,4
       TXH EVP1,4,-2
       SXD EVTAE,4
       SXA     EVS1,4
       STD EVTDE
       CLA ,4
       STT     EVLNS
       ZET     EVLNS
       TRA     EVP26
       SXA     EVTRK,0
       SXD     EVTRK,0
       PAX     0,4
       TXL     EV27,4,-2
       REM
 EVP2  PDX ,4
       TXL EVP25,4,0
       CLA ,4
       PAX ,4
       TXH     *+2,4,$TRACE
       TXH     EVTRT,4,$TRACE-1
       TXH *+2,4,$SUBR
       TXH     EVP27,4,$SUBR-1
       TXH *+2,4,$FSUBR
       TXH EVP22,4,$FSUBR-1
       TXH *+2,4,$EXPR
       TXH EVP23,4,$EXPR-1
       TXH EVP2,4,$FEXPR
       TXH EVP2,4,$FEXPR-1
       STD EVD2
       STQ $ARG3
       CLA $ARG3
       LDQ =0
       TSX $CONS,4
       XCA
       CLA EVTDE
       TSX $CONS,4
       XCA
       LXD EVD2,4
       CLA ,4
       PAX ,4
       PXD ,4
       ZET     EVTRK
       TRA     EVTXP
       LXD EVS1,4
       TRA $APPLY
       REM
 EVTRT STL     EVTRK
       TRA     EVP2
 EV1N  CLA     EVTE
       LXD     EVS1,4
       TRA     1,4
       REM
 EVP1  PDX ,4
       TXL EVP11,4,0
       CLA ,4
       PAX ,4
       TXH EVP1,4,$APVAL
       TXL EVP1,4,$APVAL-1
       PDX ,4
       CLA ,4
       PAX ,4
       CLA ,4
       PAX ,4
       PXD ,4
       LXD EVS1,4
       TRA 1,4
       REM
 EVP11 STQ EVTA
       CLA EVTE
       STD EVI1
       SUB EVQD1
       STD EVI2
       SXD EVD1,2
       LXD EVTA,4
 EVL1  TXL EVP12,4,0
       CLA ,4
       PAX ,2
       PDX ,4
       CLA ,2
       PAX ,2
 EVI1  TXH EVL1,2,**
 EVI2  TXL EVL1,2,**
       PDX ,4
       PXD ,4
       LXD EVD1,2
       LXD EVS1,4
       TRA 1,4
       REM
EVP12  CLA     EVTE
       ERROR   UBNDVR
       REM
 EVP22 PDX ,4
       CLA ,4
       PAX ,4
       CLA ,4
       STO EVT1
       CLA     EVS1
       STO     CSV
       TSX     $SAVE,4
       TXL     $END2,,$ALIST+2
       STQ     $ALIST
       ZET     EVTRK
       TRA     EVTFS
       CLA     EVTDE
       TSX     EVT1,4
       TSX     UNSAVE,4
       LXD     CSV,4
       TRA     1,4
       REM
 EVP23 PDX     0,4
       CLA     0,4
       PAX     0,4
       SXD     EVTAE,4
       LXD     $CPPI,4
       TXI     EVP28,4,-5
       REM
 EVP25 CLA EVTAE
       STD EVI3
       SUB EVQD1
       STD EVI4
       SXD EVT1,2
       STQ EVD1
       LXD EVD1,4
 EVL2  TXL EVP26,4,0
       CLA ,4
       PDX ,4
       PAX ,2
       CLA ,2
       PAX ,2
 EVI3  TXH EVL2,2,**
 EVI4  TXL EVL2,2,**
       LXD     EVT1,2
       STD     EVTAE
EV27   LXD     $CPPI,4
       TXI     *+1,4,-5
 EVP28 XEC     ENDPDL
       SXD     $CPPI,4
       LXD     $CPPI,1
       CLA     EVS1
       STO     -5,4
       CLA     EVSE
       STO     -4,4
       CLA     EVSA
       STO     -3,4
       CLA     EVTRK
       STO     -2,4
       CLA     EVCM
       STO     -1,4
       CLA     EVTAE
       STD     EVSE
       STQ EVSA
       CLA EVTDE
       LDQ     ELP1
       TSX     MAPLIS,4
       STO EVT1
       CLA     EVSA
       STO     $ARG3
       CLA     EVSE
       LXD     $CPPI,4
       LDQ     -5,4
       STQ     EVS1
       LDQ     -4,4
       STQ     EVSE
       LDQ     -3,4
       STQ     EVSA
       LDQ     -2,4
       STQ     EVTRK
       TXI     *+1,4,5
       SXD     $CPPI,4
       LXD     $CPPI,1
       LDQ     EVT1
       ZET     EVTRK
       TRA     EVDCO
EVAPG  LXD     EVS1,4
       TRA     $APPLY
       REM
EVDCO  LXD     EVTRK,4
       TXL     EVTXP,4,0
       LXA     EVTRK,4
       TXH     EVAPG,4,0
       LXD     EVTRK,4
       CLA     0,4
       STO     EVT1
       CLA     EVS1
       STO     CSV
       TSX     $SAVE,4
       TXL     $END2,,$ALIST+2
       CLA     $ARG3
       STO     $ALIST
       XCA
       TSX     $SPREAD,4
       TSX     EVT1,4
       TSX     UNSAVE,4
       LXD     CSV,4
       TRA     1,4
       REM
EVP27  PDX     0,4
       CLA     0,4
       PAX     0,4
       SXD     EVTRK,4
       TRA     EV27
       REM
 ELP1  TXL *+1,,0
       SXA     ELT1,4
       PDX ,4
       CLA ,4
       PAX ,4
       PXD ,4
       LDQ     EVSA
 ELT1  AXT     **,4
       TRA $EVAL
       REM
 EVLIS SXD     EVS1,4
       AXT     EVLISL,4
       SXA     EVS1,4
       TSX     $SAVE,4
       TXL     $END3,,EVSA+2
       STQ     EVSA
       LDQ     ELP1
       TSX     MAPLIS,4
       TSX     UNSAVE,4
       LXD     EVS1,4
       TRA     1,4
       REM
EVP26  LXD     EVT1,2
       CLA EVTE
       ERROR   UNDFCN
       REM
EVTFS  PAX     0,4
       PXD     0,4
       LDQ     EVTDE
       TSX     ARGOF,4
       LDQ     $ALIST
       CLA     EVTDE
       TSX     EVT1,4
       TSX     UNSAVE,4
       XCA
       LXA     CSV,4
       PXD     0,4
       LXD     CSV,4
       TRA     VALOF
       REM
EVTXP  STD     EVTDE
       LXA     EVS1,4
       PXD     0,4
       TSX     ARGOF,4
       TSX     $SAVE,4
       TXL     $END1,,EVS1+2
       LDQ     EVT1
       CLA     EVTDE
       TSX     $APPLY,4
       TSX     UNSAVE,4
       XCA
       LXA     EVS1,4
       PXD     0,4
       LXD     EVS1,4
       TRA     VALOF
       REM
 ARGOF SXA     PRX,4
       SXA     PRY,2
       STO     AGA
       STQ     AGQ
       TSX     TERPRI,4
       AXT     3,2
       CLA     AGM+3,2
       TSX     $PRIN2,4
       TIX     *-2,2,1
       CLA     AGA
       TSX     $PRINT,4
       LXD     AGQ,2
 PLL   TXL     PRY,2,0
       CLA     0,2
       PDX     0,2
       PAX     0,4
       PXD     0,4
       TSX     $PRINT,4
       TRA     PLL
 PRY   AXT     **,2
 PRX   AXT     **,4
       TRA     1,4
       REM
 AGA
 AGQ
 AGM   BCI     1,ARGUME
       OCT     456362607777
 AGO   OCT     462660777777
 VALV  BCI     1,VALUE
       REM
 VALOF SXA     VAX,4
       STO     AGA
       STQ     AGQ
       TSX     TERPRI,4
       CLA     VALV
       TSX     $PRIN2,4
       CLA     AGO
       TSX     $PRIN2,4
       CLA     AGA
       TSX     $PRINT,4
       CLA     AGQ
 VAX   AXT     **,4
       TRA     $PRINT
 EVTE
 EVTAE
 EVTA
 EVT1
 EVD1
 EVLNS
 EVCM  TXL     $END4,,EVTRK+2
 EVQD1 SYN     $QD1
       REM
R      HED
 INTER SXD     INTRX,4
       TSX     $SAVE,4
       TXL     $END5,,INTGS+2
       SXA     INTGL,2
       STQ     INTPL
       STZ     INTGS
       PDX     0,4
       CLA     0,4
       STD     INTB
       STD     INTE
       PAX     0,4
       PXD     0,4
       LDQ     INTFB
       TSX     MAPLIS,4
       LDQ     INTPL
       TSX     $NCONC,4
       STO     INTPL
       LDQ     =0
 INTGM LXD     INTE,4
       TXL     INTAA,4,0
       CLA     0,4
       STD     INTE
       PAX     0,2
       CLA     0,2
       PAX     0,2
       TXL     INTGM,2,-2
       PXD     0,4
       TSX     $CONS,4
       XCA
       TRA     INTGM
 INTAA SLQ     INTGL
 INTGA LXD     INTB,4,0
       TXL     INTRN,4,0
       CLA     0,4
       STD     INTB
       PAX     0,4
       CLA     0,4
       PAX     0,2
       TXH     INTGA,2,-2
       TXL     INTEV,2,$COND-1
       TXH     INTEV,2,$COND
       PDX     0,2
 INTEB TXL     INTGA,2,0
       CLA     0,2
       PDX     0,2
       PAX     0,4
       CLA     0,4
       PDX     0,4
       SXA     INTB,4
       PAX     0,4
       PXD     0,4
       LDQ     INTPL
       TSX     $EVAL,4
       TZE     INTEB
       LXA     INTB,4
       CLA     0,4
       PAX     0,4
 INTEV PXD     0,4
       LDQ     INTPL
       TSX     $EVAL,4
       NZT     INTGS
       TRA     INTGA
       LXA     INTGS,4
       TXH     INTRN,4,-2
       PXD     0,4
       LDQ     INTFC
       STQ     $ARG3
       LDQ     INTGL
       TSX     SASSOC,4
       PDX     0,4
       CLA     0,4
       STD     INTB
       STZ     INTGS
       TRA     INTGA
       REM
 INTFB TXL     *+1,,1
       SXA     INTFX,4
       PDX     0,4
       CLA     0,4
       PAX     0,4
       PXD     0,4
       LDQ     =0
 INTFX AXT     **,4
       TRA     $CONS
       REM
 INTFC TXL     *+1,,1
       LXA     INTGS,2
       PXD     0,2
       LXA     INTGL,2
       ERROR   UNDFGO
       REM
 INTRN LXD     INTGS,4
       PXD     0,4
       STZ     INTGS
       LXA     INTGL,2
       TSX     UNSAVE,4
       LXD     INTRX,4
       TRA     1,4
       REM
 INTE
PRGVAR SYN     INTPL
       REM
RETURN ORA     $AMASK
       STO     INTGS
       CLA     $QD1
       TRA     1,4
       REM
GOGOGO SXD     GOX,4
       PDX     0,4
       CLA     0,4
       STA     INTGS
       PAX     0,4
       CLA     0,4
       PAX     0,4
       TXH     GOT,4,-2
       LXA     INTGS,4
       PXD     0,4
       TSX     $SAVE,4
       TXL     $END1,,GOX+2
       TSX     $EVAL,4
       TSX     UNSAVE,4
       PDX     0,4
       SXA     INTGS,4
 GOT   CLA     $QD1
       LXD     GOX,4
       TRA     1,4
       REM
       HEAD    C
LINK   STO     LNKA
       STQ     LNKB
       SXA     LER,4
       LAC     0,4
       TXI     *+1,4,1
       CLA     0,4
       STO     LNKD
       ANA     TAGMSK
       ERA     TAGMSK
       TNZ     LER
       SXD     LNKC,4
       CLA     B$ZERO
       STO     0
       STZ     LNTRS
       LXA     LNKD,4
       CLA     0,4
       PAX     ,4
       TXL     LNNF,4,-2
LNLP   PDX     0,4
       TXL     LNNF,4,0
       CLA     0,4
       PAX     0,4
       TXL     *+2,4,$SUBR-1
       TXL     LNSBR,4,$SUBR
       TXL     *+2,4,$FSUBR-1
       TXL     LNSBR,4,$FSUBR
       TXL     *+2,4,$TRACE-1
       TXL     LNTR,4,$TRACE
       TXL     *+2,4,$EXPR-1
       TXL     LNEXP,4,$EXPR
       TXL     LNLP,4,$FEXPR-1
       TXH     LNLP,4,$FEXPR
LNEXP  PDX     0,4
       CLA     0,4
       PAX     0,4
LNGN   SXD     LNFN,4
       ZET     LNTRS
       TRA     LNTEX
       TSX     LNARS,4
       XCA
       CLA     $ALIST
       STO     $ARG3
       CLA     LNFN
       LXD     LNKC,4
       TRA     $APPLY
       REM
LNNF   LXA     LNKD,4
       TRA     LNGN
       REM
LNTR   STL     LNTRS
       STO     LNAC
       TSX     LNARS,4
       STO     LNRGL
       XCA
       LXA     LNKD,4
       SXA     LNKC,4
       PXD     0,4
       TSX     $SAVE,4
       TXL     $END1,,LNKC+2
       TSX     A$ARGOF,4
       CLA     LNAC
       TRA     LNLP
       REM
LNTEX  CLA     LNFN
       LDQ     $ALIST
       STQ     $ARG3
       LDQ     LNRGL
       TSX     $APPLY,4
LNTEN  TSX     UNSAVE,4
       XCA
       LXA     LNKC,4
       PXD     0,4
       LXD     LNKC,4
       TRA     A$VALOF
       REM
LNTSB  STA     LNDIS
       CLA     LNKA
       LDQ     LNKB
LNDIS  TSX     **,4
       TRA     LNTEN
       REM
LNSBR  PDX     0,4
       CLA     0,4
       PAX     0,4
       CLA     0,4
       ZET     LNTRS
       TRA     LNTSB
       STA     LNTSX
       CLA     LNTSX
       LXD     LNKC,4
       STO     0,4
       CLA     LNKA
       LDQ     LNKB
       TRA     0,4
       REM
LNARS  SXA     LNLX,4
       LXD     LNKD,4
       TXL     LNN,4,0
       SXD     LNKP,4
       TSX     LST,4
LNKP   TXH     LNKA,0,**
       PAX     LNKB,0
       PAX     $ARG3,0
       PAX     $ARG4,0
       PAX     $ARG5,0
       PAX     $ARG6,0
       PAX     $ARG7,0
       PAX     $ARG8,0
       PAX     $ARG9,0
       PAX     $ARG10,0
       PAX     $ARG11,0
       PAX     $ARG12,0
       PAX     $ARG13,0
       PAX     $ARG14,0
       PAX     $ARG15,0
       PAX     $ARG16,0
       PAX     $ARG17,0
       PAX     $ARG18,0
       PAX     $ARG19,0
       PAX     $ARG20,0
LNLX   AXT     **,4
       TRA     1,4
LNN    PXD     0,0
       TRA     LNLX
       REM
LER    AXT     **,4
       CLA     LNKA
       ERROR   STRTRP
       REM
LNTRS
LNFN
LNAC
LNRGL
LNKC
LNKD
LNTSX  TSX     **,4
       REM
LST    SXA     LX2,2
       CLA     1,4
       STD     LSN
       STD     LSC
LSN    TNX     *+1,4,**
       PDX     0,2
       PXD     0,4
       PDC     0,4
       TXI     *+1,4,1
       SXA     LSP,4
       SXA     LSE,4
       LXD     $FREE,4
       TXH     *+2,4,0
       TSX     $FROUT,4
       SXD     LAN,4
       LXA     $CNTR1,4
LSC    TIX     *+3,4,**
       TSX     ARREST,4
       AXT     -1,4
       SXA     $CNTR1,4
       LXD     LAN,4
LSP    CLA*    **,2
       ARS     18
       STA     0,4
       CLA     0,4
       SXA     LFX,4
LSR    PDX     0,4
       TXL     LFIX,4,0
       TIX     LSP,2,1
       STD     $FREE
       PXD     0,0
LFX    AXT     **,4
       STD     0,4
LX2    AXT     **,2
       CLA     LAN
LSE    TRA     **
LFIX   CLA     LAN
       TSX     RECLAM,4
       CLA     $FREE
       XEC     LFX
       STD     0,4
       TRA     LSR
LAN    PZE
       REM
UNWND  SXA     UNX,4
       STQ     UNSQ
       LXD     $CPPI,1
       PXD     0,1
       ADM     -1,1
       SUB     $QD1
       STD     UNTL
UNLP   TXI     *+1,1,1
UNTL   TXH     UNF,1,**
       CLA     0,1
       PAX     0,4
       TXL     UNLP,4,0
       PXD     0,4
       XCA
       CLA     0,4
       PAX     0,4
       TXH     UNLP,4,-2
       CLA     PROS
       ZET     BPLACE
       CLA     PROSCM
       TSX     GET,4
       TZE     UNLP
       CLA     0,1
       PAX     0,4
       STD     0,4
       TRA     UNLP
UNF    SXD     $CPPI,1
UNX    AXT     **,4
       LDQ     UNSQ
       TRA     1,4
UNSQ
       REM
MOVE   SXA     MOVY,1
       LXD     $CPPI,1
       STO     1,1
       STQ     2,1
       SXD     TXLW,4
MOVY   AXT     **,4
       CLA     1,4
       STA     TXLW
       STD     STRW
       CLA     TXLW
       STO     0,1
       CLA     0,4
       ANA     CNTMSK
       TZE     MOVD
       PDX     0,4
       TRA     MOVD-1,4
       REM
MOVEMC MACRO   NUMBER
       IRP     NUMBER
       CLA     $ARG'NUMBER
       STO     NUMBER,1
       IRP
MOVEMC END
       REM
       REM
       LXA     MOVY,4
MOVD   XEC     1,4
       SXD     $CPPI,1
       CLA     STRW
       STO     -1,1
ENPDL1 TXL     $NOPDL,1,**
       TRA     2,4
STRW   STR     **
TXLW   TXL     **,,**
       REM
RESTOR SXD     $CPPI,1
       XCA
       CLA     0,1
       PDX     0,4
       XCA
       TRA     1,4
       REM
PROBE  SYN     *
LIST
QTLST
QTLSTC
TAB
LCOM
PROS
PROSCM
PROEN  SYN     *-1
MOV            MOVE
LSTR           LST
RTRN           RESTOR
 ALST          $ALIST
LITRLS EQU     *
       REM
       REM
       ORG     26800
LOWERP BSS     1
       REM
0      HED
       REM
       REM
BUCK5  MACRO
               ,,-*+1
               ,,-*+1
               ,,-*+1
               ,,-*+1
               ,,-*+1
BUCK5  END
       REM
BUCK25 MACRO
       BUCK5
       BUCK5
       BUCK5
       BUCK5
       BUCK5
BUCK25 END
       REM
       PZE     ,,
       BUCK25
       BUCK25
       BUCK25
       BUCK25
       BUCK25
BUCKET         ,,-*+1
OBLIST SYN     BUCKET