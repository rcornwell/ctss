       REM
*      ADVANCE, STARTREAD, AND ENDREAD PROGRAMS
       REM
ADVANC SXD PORK,4
       LXD CHPOS,4
       TIX CHOPS,4,6
       LXD WDNUM,4
       TIX LAMB,4,1
       NZT EORTS
       TRA JOYCE
PORK   TXI SUZIE,,0
STREAD SXD PORK,4
JOYCE  TSSX    RDFLXA,4
               BUFF-12,,12
       STL EORTS
       STZ $CHACT
       AXT 12,4
LAMB   SXD WDNUM,4
       CLA BUFF,4
       STO PWORD
       AXT 36,4
CHOPS  SXD CHPOS,4
       PXD ,0
       LDQ PWORD
       LGL 6
       STQ PWORD
       PAX 0,4
       TXH     SHANK,4,45
       TXL     SHANK,4,44
       TRA     ENDRED+1
 SHANK TXI *+1,4,$H00
 BACON CLA $CHACT
       ADD =1
       STO $CHACT
       PXD ,4
       SLW $CURC
       SXA $CURC1,4
       LXD PORK,4
       TRA 1,4
ENDRED SXD PORK,4
       SXD CHPOS,0
       SXD WDNUM,0
 SUZIE LXA EOR,4
       STZ EORTS
       TRA BACON
****
*
 SEVNS SYN     SEVENS
CHPOS  PZE     ,,**
WDNUM  PZE     ,,**
       REM
       REM
       REM
       REM ALPHABETIC FUNCTIONS
       REM
       REM DIGIT(CHAR)
DIGIT  CAS HOL9
       TRA AL5
       NOP
       CLA $QD1
       TRA 1,4
AL5    PXD ,0
       TRA 1,4
       REM
       REM
       REM UNPACK(NAME)
       REM
UNPACK SXA     UPI4,4
       SXA     UPI2,2
       PDX     ,4
       LDQ     0,4
       AXT     6,2
 UP2   PXD     ,0
       LGL     6
       CAS     =O77
       TXI     UP1,2,1
       TXI     UP1,2,1
       STO     T1+6,2
       TIX     UP2,2,1
       REM
 UP1   STZ     UPLST
 UP4   TXH     UP3,2,6
       CLA     T1+6,2
       ADD     HORG
       ALS     18
       LDQ     UPLST
       TSX     $CONS,4
       STO     UPLST
       TXI     UP4,2,1
 UP3   CLA     UPLST
       STZ     UPLST
 UPI4  AXT     **,4
 UPI2  AXT     **,2
       TRA     1,4
       REM
*      THIS ROUTINE USES $CONS
       REM
       REM
 HORG  SYN $H00A
 EOR   SYN $H72A
 HOL9  SYN $H11D
 HOL40 SYN $H40D
 BUFF  BES     12
       BES     3
PWORD  BSS 1
 PARAM PZE     BOFFO-20,,1
 FLOS  SYN     FLOATD
 T1    BSS     7
 FIXS  SYN     $FIXD
 CHARS BSS     1
 EORTS BSS 1
 BOFFO BES     20
       BSS     1
 UPLST SYN     BBPNT
       BSS     1
CURC1  PZE
CURC   PZE
 CHACT PZE
*
*   MKNO   A FUNCTION OF TWO ARGUMENTS
*
 MKNO  SXA     MKIR,4
       STQ     MKT1
       TSX     $CONSW,4
       XCA
       CLA     $DMASK
       TSX     $CONS,4
       LXD     MKT1,4
       STD     MKT1
       CLA     $QT5
       TXL     *+3,4,$FIX-1
       TXH     *+2,4,$FIX
       CLA     $QT1
       TXL     *+3,4,$FLOAT-1
       TXH     *+2,4,$FLOAT
       CLA     $QT2
       LXD     MKT1,4
       ORS     0,4
       PXD     0,4
 MKIR  AXT     **,4
       TRA     1,4
       REM
       REM
H      HED
*      LOGOR, LOGAND, AND LOGXOR
       REM
 LOGOR TZE     1,4
       SXD     T1,4
       AXT     $PJ36,4
       SXA     T1,4
       TSX     $SAVE,4
       TXL     $END1,,T1+2
       TSX     $EVLIS,4
       TSX     UNSAVE,4
       LDQ     =0
       STQ     T1+1
       LDQ     ORS
       TRA     LOG2
       REM
*
LOGAND TZE     1,4
       SXD     T1,4
       AXT     $PJ37,4
       SXA     T1,4
       TSX     $SAVE,4
       TXL     $END1,,T1+2
       TSX     $EVLIS,4
       TSX     UNSAVE,4
       LDQ     SEVENS
       STQ     T1+1
       LDQ     ANS
       TRA     LOG2
       REM
*
LOGXOR TZE     1,4
       SXD     T1,4
       AXT     $PJ38,4
       SXA     T1,4
       TSX     $SAVE,4
       TXL     $END1,,T1+2
       TSX     $EVLIS,4
       TSX     UNSAVE,4
       LDQ     =0
       STQ     T1+1
       LDQ     ERS
*      COMMON PART OF LOGAND, LOGOR, AND LOGXOR
 LOG2  STQ     LOG5
       SXA     LOG4,2
       PDX     ,2
*
 LOG1  CLA     0,2
       PDX     0,2
       PAX     0,4
       PXD     0,4
       TSX     NUMVAL,4
       PDX     0,4
       CAL     0,4
 LOG5          **
       TXH     LOG1,2,0
*
 LOG6  CAL     T1+1
       LDQ     $OCTD
       TSX     $MKNO,4
       LXD     T1,4
 LOG4  AXT     **,2
       TRA     1,4
*      INSTRUCTIONS TO BE INSERTED IN INNER LOOP
 ORS   ORS     T1+1
 ANS   ANS     T1+1
 ERS   TRA     *+1
       ERA     T1+1
       SLW     T1+1
       TXH     LOG1,2,0
       TRA     LOG6
*
 T1    OCT     -0,-0
*
*      LEFTSHIFT(X,N)
*
LSHIFT SXA     LSH1,4
       SXA     LSH4,2
       STO     T2
       XCA
       PDX     0,2
       TSX     FIXVAL,4
       AXT     7*4096+7*512+1*64,4
       TMI     LSH2
       AXT     7*4096+6*512+7*64,4
 LSH2  SXD     LSH3,4
       STA     LSH3
       CLA     T2
       TSX     NUMVAL,4
       PDX     0,4
       CAL     0,4
 LSH3  ALS     **
       LDQ     $OCTD
       TSX     $MKNO,4
 LSH4  AXT     **,2
 LSH1  AXT     **,4
       TRA     1,4
 T2    SYN     T1