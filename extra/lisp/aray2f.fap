*
* ARYMAK       THE FUNCTION THAT MAKES ARRAYS
*              THE ARGUMENT IS A SINGLE LIST WHOSE SUB-LISTS HAVE THE
*              FORM  (NAME,(DIMENSION1,DIMENSION2,DIMENSION3),TYPE)
       REM
ARYMAK LDQ     AMFAG
       TRA     MAPLIS
*
 AMFAG TXL     *+1,,1
       SXA     AFRX,4
       SXA     AFRY,2
       PDX     0,4
       CLA     0,4
       PAX     0,4
       CAL     0,4
       PAX     0,4
       SXD     AFAT,4
       PDX     0,4
       CLA     0,4
       PAX     0,2
       STZ     ATYP
       TXL     ADA,4,$LIST-1
       TXH     ADA,4,$LIST
       SXD     ATYP,4
 ADA   CLA     0,2
       PAX     0,2
       STD     ATMP
       TSX     FIXVAL,4
       STO     ADO
       LXD     ATMP,4
       TXL     AOD,4,0
       CLA     0,4
       STD     ATMP
       PAX     0,2
       TSX     FIXVAL,4
       STO     ADT
       LXD     ATMP,4
       TXL     ATD,4,0
       CLA     0,4
       PAX     0,2
       TSX     FIXVAL,4
       STO     ADTH
       AXT     3,2
       TRA     AGA
 AOD   CLA     ADO
       STO     ADTH
       CLA     =1
       STO     ADT
       STO     ADO
       AXT     1,2
       TRA     AGA
ATD    STO     ADTH
       CLA     ADO
       STO     ADT
       CLA     =1
       STO     ADO
       AXT     2,2
 AGA   LDQ     ADO
       PXD     0,0
       MPY     ADT
       STQ     ADOT
       MPY     ADTH
       ZET     ATYP
       STQ     ATYP
       XCA
       ADD     ADOT
       ADD     ADO
       ADD     =5
       STA     APWT
       STA     ATMQ
       LXA     ATYP,4
       SXD     APWT,4
       TSX     BLOCKR,4
       TZE     ARYTL
       STA     ATMP
       ADD     =1
       STA     APWO
       SXD     ATBZ,2
       SXD     APWO,2
       SXD     ASBR,2
       SUB     ATMQ
       STA     ASBR
       PAC     0,4
       TXI     *+1,4,-2
       SXD     AARY,4
       PAC     0,4
       ADD     =4
       ADD     ADO
       STA     ATBZ
       AXT     5,2
 ACLA  CLA     ADOT,2
       STO     0,4
       TXI     *+1,4,-1
       TIX     ACLA,2,1
       ANA     $AMASK
       ORA     ACLAS
       LXA     ADO,2
 AADD  ADD     ADT
       STO     0,4
       TXI     *+1,4,-1
       TIX     AADD,2,1
       LXA     ADOT,2
       ANA     $AMASK
       ORA     ARSTO
 AAA   ADD     ADTH
       STO     0,4
       TXI     *+1,4,-1
       TIX     AAA,2,1
       REM     TABLE CONSTRUCTION ALL DONE.
       REM     THE FOLLOWING ADDS PROPERTYS TO THE ARYATOM
       CLA     AARY
       LDQ     =0
       TSX     $CONS,4
       LDQ     =0
       TSX     $CONS,4
       XCA
       CLA     ARY
       TSX     $CONS,4
       STO     ATMP
       CLA     ASBR
       TSX     $CONSW,4
       LDQ     ATMP
       TSX     $CONS,4
       XCA
       CLA     ASB
       TSX     $CONS,4
       XCA
       LXD     AFAT,4
       CLA     0,4
       PDX     0,4
       PXD     0,4
       XCA
       TSX     $NCONC,4
       LXD     AFAT,4
       STD     0,4
       PXD     0,4
       LDQ     ARYLIS
       TSX     $CONS,4
       STD     ARYLIS
       CLA     AFAT
 AFRX  AXT     **,4
 AFRY  AXT     **,2
       TRA     1,4
       REM
ARYTL  LXA     AFRY,2
       CLA     AFAT
       ERROR   NARYRM
       REM
       REM     CONSTANTS AND STORAGE
       SXA     ARYGTX,4
       TSX     ARYGET,4
 APWO
 APWT
 ATBZ
 ADOT
 ATYP
 ATMQ
 ADO
 ADT
 ADTH
 ASBR  TXL     **,,**
 AARY
 ACLAS CLA*    **,2
 ARSTO STO     **,1
 ARY           ,,$ARRAY
 ASB   SYN     $SUBRD