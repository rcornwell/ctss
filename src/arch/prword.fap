*       BUFFER CONTROL ROUTINE FOR TYPEWRITER OUTPUT.
*
*      J. H. SALTZER, JANUARY, 1965.
*
       ENTRY   PRWORD        ADD A WORD TO PRINT BUFFER.
       ENTRY   PRBUFF        PRINT OUT PARTIAL BUFFER.
       ENTRY   TYWORD        GET NEXT TYPED WORD.
       ENTRY   TYLINE        RESET TO START READING NEXT LINE.
*
       EXTERN RDFLXA,WRFLXA
*
BUFLEN EQU     28            TYPEWRITER BUFFER SIZE.
*
*      ADD WORD TO PRINT BUFFER.
*      PRWORD.(WORD)
*
PRWORD SXA     PRX4,4        SAVE LINKAGE.
       CAL*    1,4           GET WORD BEING ADDED.
       PDX     0,4           CHECK FOR DISK CONTROL WORD.
       TXH     PRX4,4,-2     DON'T STORE CONTROL WORD.
 PWCNT AXT     BUFLEN,4      SET WORD COUNTER (INITIALLY BUFLEN.)
       SLW     BUFF+BUFLEN,4 STORE WORD IN BUFFER.
       TIX     PSAVE,4,1     COUNT, CHECK FOR OVERFLOW.
       TSX     WRFLXA,4      BUFFER FULL, PRINT W/O C.R.
               BUFF,,BUFLEN  ..
PFRESH AXT     BUFLEN,4      RESET WORD COUNTER.
 PSAVE SXA     PWCNT,4       SAVE WORD COUNTER.
 PRX4  AXT     --,4          RESTORE LINKAGE.
       TRA     1,4           RETURN.
*
*      PRINT OUT REMAINING BUFFER, RESET COUNTER.
*
PRBUFF SXA     PRX4,4        SAVE LINKAGE.
       LAC     PWCNT,4       GET WORD COUNT.
       TXI     *+1,4,BUFLEN  FORM COMPLEMENT MOD BUFLEN.
       SXD     PRCL,4        SAVE COUNT IN CALLER.
       TSX     WRFLXA,4      PRINT REMAINDER.
 PRCL          BUFF,,--      ..
       TRA     PFRESH        GO REFRESH COUNTER, AND RETURN.
*
*      GET A WORD FROM INPUT BUFFER.
*         WORD = TYWORD.(0)
*
TYWORD SXA     TYX4,4        SAVE LINKAGE.
TYCNT  AXT     --,5          GET WORD COUNTER.
TYPOS  AXT     --,4          GET BUFFER POINTER.
       TIX     TYGET,5,6     CHECK FOR WORDS LEFT.
       TSX     RDFLXA,4      NO, GET SOME.
TYCALL         BUFF,,BUFLEN  ..
       PAX     0,5           INSERT 6-BIT CHARACTER COUNT INTO COUNTER.
       TIX     *+1,5,16384   REMOVE BIT 21 IF PRESENT.
       AXT     0,4           RESET INPUT BUFFER POINTER.
TYGET  CLA     BUFF,4        GET A WORD.
       TXI     *+1,4,-1      MOVE POINTER TO NEXT WORD.
       SXA     TYPOS,4       SAVE POINTER.
       SXA     TYCNT,5       SAVE CHARACTER COUNTER.
TYX4   AXT     --,4          RESTORE LINKAGE.
       TRA     1,4           RETURN.
*
TYLINE ZSA     TYCNT         RESET CHARACTER COUNT TO MAKE LINE EMPTY.
       TRA     1,4           RETURN
*
BUFF   BSS     BUFLEN            OUTPUT BUFFER.
*
       END
