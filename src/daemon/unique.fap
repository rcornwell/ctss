*
       TTL UNIQUE NAME GENERATOR                                    
       LINK    ON
       LBL     UNIQUE,X
       ENTRY   UNIQUE   
       EXTERN  GETIME   
       REM
       REM     THIS PROGRAM GENERATES A UNIQUE PAIR OF NAMES
       REM
       REM     TSX           $UNIQUE,4    
       REM     PTH           NAME1  
       REM     PTH           NAME2  
       REM
       REM     THIS PROGRAM IS USED TO GENERATE UNIQUE
       REM     NAMES FOR INTERMEDIATE FILES     
       REM
       REM                   D.R. WIDRIG  DEC. 1965   
       REM
UNIQUE SXA     X4,4          SAVE LINKAGE 
       SXA     X1,1          COUNTER
       TSX     GETIME,4      GET DATE AND TIME  
       XCL                   SWITCH AC AND MQ   
X4     AXT     **,4          RESTORE LINKAGE    
       ANA     =O777777770000 MASK OUT YY PORTION OF DATE   
       SLW*    1,4           TEMPORARY SAVE     
       ZAC                   CLEAR AC     
       RQL     12            THROW OUT HIGH-ORDER ZEROES    
       LGL     3             CONVERT TO OCTAL   
       ALS     3             ..     
       LGL     3             ..     
       ORS*    1,4           'OR' WITH MMDD     
       AXT     6,1           SET LOOP COUNT     
       ZAC                   CLEAR AC     
       ALS     3             CONVERT TO OCTAL   
       LGL     3             ..     
       TIX     *-2,1,1       LOOP   
       SLW*    2,4           SAVE NAME2   
X1     AXT     **,1          RESTORE XR1  
       TRA     3,4           RETURN 
       END  
