*
       TTL     FILE NAME -- GETMD FAP
       REM     ROUTINE TO CHANGE NMEMONICS FOR MODE TO
       REM     OCTAL EQUIVALENTS.
       REM     G. SCHROEDER            JUNE,1965
       ENTRY   GETMOD
SAVXR  MACRO
       SXA     X1,1
       SXA     X2,2
       SXA     X4,4
SAVXR  END
RSTXR  MACRO
       LXA     X1,1
       LXA     X2,2
       LXA     X4,4
RSTXR  END
GETMOD SAVXR
       AXT     6,2
       STZ     MODE
       LDQ*    1,4
NEXT   ZAC
       LGL     6
       TZE     DECR
       LAS     BLANK
       TRA     *+2
       TRA     DECR
       AXT     5,1
CCR    LAS     TABLE+5,1
       TRA     *+2
       TRA     GCHAR
       TIX     CCR,1,1
       RSTXR
       TRA     3,4
GCHAR  CAL     CHAR+5,1
       ORS     MODE
DECR   TIX     NEXT,2,1
       RSTXR
       CLA     MODE
       STO*    1,4
       TRA*    2,4
MODE   PZE     0
BLANK  OCT     60
CHAR   OCT     1
       OCT     4
       OCT     10
       OCT     20
       OCT     100
TABLE  BCI     1,00000T      TEMPORARY
       BCI     1,00000R      READ-ONLY
       BCI     1,00000W      WRITE-ONLY
       BCI     1,00000V      PRIVATE
       BCI     1,00000P      PROTECTED
X1     PZE     0
X2     PZE     0
X4     PZE     0
       END
