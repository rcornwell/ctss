* NEWMOD  -  03/25/68   D.A. ANDERSON  COMPUTE NEW MODE FOR FILE
       ENTRY   NEWMOD
       REM
NEWMOD SXA     XR1,1
       SXA     XR2,2
       SXA     XR4,4
       CAL*    1,4
       SLW     OLDMOD
       LDQ*    2,4
       AXT     6,1
       STZ     MODE1
       STZ     MODE2
CYCLE  TSX     GENMOD,4
               MODE1
       TXL     NOREM,2,REMOVE
       TSX     GENMOD,4
               MODE2
       TXH     CYCLE,2,REMOVE
NOREM  CAL     MODE2
       COM
       ANA     MODE1
       ANA     =O377
XR4    AXT     **,4
XR2    AXT     **,2
XR1    AXT     **,1
       TRA     4,4
       REM
GENMOD STZ     OCTBUF
       STZ     HAVSUM
       AXT     0,2
LOOP   ZAC
       LGL     6
       LAS     =7
       TRA     NOTOCT
       NOP
       LGR     3
       CAL     OCTBUF
       LGL     3
       SLW     OCTBUF
       TRA     SETHAV
NOTOCT AXT     NMOD,2
       LAS     MODS+NMOD,2
       TRA     *+2
       TRA     GOTMOD
       TIX     *-3,2,2
BAD    LXA     XR1,1
       LXA     XR2,2
       LXA     XR4,4
       TRA*    3,4
       EJECT
GOTMOD TXH     OUT,2,REMOVE
       CLA     MODS+NMOD+1,2
       ORA     OCTBUF
       STZ     OCTBUF
       ORS*    1,4
       TMI     NDLOP
SETHAV STL     HAVSUM
NDLOP  TIX     LOOP,1,1
EXIT   CAL     OCTBUF
       NZT     HAVSUM
       ORA     OLDMOD
       ORS*    1,4
       TRA     2,4
       REM
OUT    TNX     BAD,1,1
       TRA     EXIT
       REM
MODS   BCI     1,00000/
       PZE
.RMV.  BCI     1,00000*
OLDMOD PZE
       BCI     1,00000M
       OCT     200
       BCI     1,00000P
       OCT     100
       BCI     1,00000L
       OCT     40
       BCI     1,00000V
       OCT     20
       BCI     1,00000W
       OCT     10
       BCI     1,00000R
       OCT     4
       BCI     1,00000S
       OCT     2
       BCI     1,00000T
       OCT     1
       BCI     1,00000
       OCT     -0
REMOVE EQU     *-.RMV.
NMOD   EQU     *-MODS
       REM
MODE1
MODE2
HAVSUM
OCTBUF
       REM
       END
