* NAMASK        
*   RECONSTRUCTED FROM OBJECT DECK BY ROGER ROACH  7-13-67  
*     
*  PROGRAM TO CONVERT AN ARGUMENT INTO A MASK TO BE USED TO TEST  
*  WORDS TO SEE IF THEY AGREE TO THE ARGUMENT ACCORDING TO THE LISTF    
*  STAR CONVENTION.  IT CHANGES '*'S TO ZEROS, ANYTHING ELSE
*  TO 77 (OCTAL). 
*     
*   USAGE-  
*         MASK = NAMASK.(ARG) 
*         W'R ((WORD.XOR.ARG).A.MASK).E.0, WORD CORRESPONDS TO ARG
*     
       LINK    OFF
       ENTRY   NAMASK   
NAMASK CAL*    1,4
       ERA     =H     *  RETURN ZERO IF ONE STAR
       TZE     2,4
       STI     INDICS        SAVE THOSE INDICATORS    
       LDQ*    1,4
       CAL     =2            FOR LATER OVERFLOW 
       TOV     *+1           TURN OFF OVERFLOW INDICATOR    
LOOP   LGL     6  
       PAI  
       IIR     54            IS CHARACTER A STAR
       RFT     77            IF SO, BITS WOULD BE ZERO
       SIR     77            IF NOT, INSERT 77 (OCTAL) INTO MASK  
       PIA                   PUT MASK INTO AC   
       TNO     LOOP          HAVE WE REACHED THE 2    
       LFT     400000        YES, WAS AC NEGATIVE     
       SSM                   YES, RESET IT
       LDI     INDICS        RESET THOSE INDICATORS   
       TRA     2,4
INDICS PZE     **            RESERVED     
       END  
