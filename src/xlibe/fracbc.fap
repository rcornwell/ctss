*      FRACBC - FRACTION TO BCD CONVERSION.      
       REM
       REM     7/19/66 - NOEL I. MORRIS   
       REM
       LINK    OFF
       ENTRY   FRACBC   
       REM
       REM
FRACBC SXA     SX4,4         SAVE XR4     
       STZ     FRACT         INITIALIZE RESULT  
       LDQ*    1,4           PICK UP NUMBER     
       REM
       AXT     0,4           CONVERSION AND SHIFT COUNTER   
FLOOP  MPY     =10           DIGIT IN AC  
       ALS     30,4          SHIFT TO POSITION  
       ORS     FRACT         AND OR INTO RESULT 
       TXI     *+1,4,6       ..     
       TXL     FLOOP,4,30    ..     
       REM
       CLA     FRACT         PICK UP RESULT     
SX4    AXT     **,4          RESTORE XR4  
       TRA     2,4           AND RETURN   
       REM
       REM
FRACT  PZE  
       REM
       REM
       END  
