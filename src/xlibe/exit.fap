*  EXIT - DUMMY EXIT, EXITM AND ENDJOB ROUTINES (INCL .SETUP) - R. ROACH 
*     
       LINK    OFF
       ENTRY   EXIT     
       ENTRY   EXITM    
       ENTRY   ENDJOB   
       ENTRY   .SETUP   
EXIT   STL     FLAG          INDICATE EXIT CALLED     
       TSX     WRFLX,4       WRITE 'EXIT CALLED.' ONLINE.   
       PZE     MESS,,2       ..     
EXITM  TSX     $BFCLOS,4     CLOSE FILES IN BF-PACKAGE
       PTH     =HALL         ..     
       PTH     =-0           ..     
       PTH     *             ..     
       TSX     $CLOSE,4      CLOSE OTHER FILES  
       PTH     =HALL         ..     
       ZET     FLAG          TEST FOR EXIT CALLED.    
       TSX     DORMNT,4      IF SO GO DORMNT    
       TSX     WRFLX,4       IF EXITM OR RETURN FROM DORMNT 
       PZE     =O545757575757,,1 PRINT '*'
       TSX     $CHNCOM,4     AND CALL CHNCOM    
FLAG   PZE     0             WITH NO CORE IMAGE UNLESS EXIT CALLED
ENDJOB SYN     EXITM    
.SETUP EAXM                  JUST LEAVE MULTIPLE TAG MODE   
       XIT     1,4           AND RETURN   
MESS   BCI     2,EXIT CALLED. 
WRFLX  TIA     =HWRFLX  
DORMNT TIA     =HDORMNT 
       END  
