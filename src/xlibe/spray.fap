* XLIBE ROUTINE SPRAY - RECONSTRUCTED BY ROGER ROACH  2/22/68  
*     
* SPRAY FILLS AN ARRAY WITH A CERTAIN WORD
* ZERO FILLS THAT ARRAY WITH ZEROS  
*     
* CALLING SEQUENCES ARE 
*                SPRAY.(WORD,LOC1,LOC2...LOC2(N),LOC3,...)  
*                ZERO.(LOC1,LOC2...LOC2(N),LOC3,....) 
*     
       LINK    OFF
       ENTRY   SPRAY    
       ENTRY   ZERO     
ZERO   LDQ     =0 
       STQ     WORD     
       SXA     XR1,1         SAVE IX 1    
LOOP   CLA     1,4           GET THE ARGUMENT FROM THE LIST 
       PAX     ,1            PLACE INTO XR1     
       STD     TNX           SAVE COUNT(IF ANY) IN TNX INSTR.     
       SXA     DST,1         PLACE ADDRESS IN DOUBLE STORE INSTR. 
       ANA     SEVEN         TEST THE PREFIX    
       CAS     TWO           COMPARE WITH A PTW 
       TRA     OVER          NOPE, A PTH (NO COUNT)   
       TRA     TNX           A COUNT, WE HAVE A BLOCK TO FILL     
XR1    AXT     **,1          EXIT   
       TRA     1,4
       REM
       REM
SPRAY  LDQ*    1,4
       TXI     ZERO+1,4,-1  MAKE THEM THINK IT WASN'T THERE 
       REM
TNX    TNX     OVER,1,** IF WE HAVE ONE LOC, SKIP     
       CLA     WORD     
       EVEN 
DST    DST     **,1          STORE BOTH AT ONCE 
       TIX     DST,1,2       LOOP FOR ENTIRE BLOCK    
       TNX     *+3,1,1       SKIP IF NO MORE TO STORE 
OVER   AXT     0,1
       STQ*    DST           STORE THAT LAST ONE
       TXI     LOOP,4,-1     GO BACK FOR NEXT ARG     
       REM
TWO    TWO  
SEVEN  SEVEN
WORD   PZE  
       END  
