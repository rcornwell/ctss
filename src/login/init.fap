* M1416 3854  PETER R. BOS  ..... INITIALIZATION COMMAND FOR CTSS ......
       PCC     ON
       ENTRY   0             MAIN PROGRAM ENTRY
       ENTRY   TEST00
       LBL     INIT
       PCC     OFF
       NOLNK
       REM
       EXTERN  SETERR,PRINT,PTERR,WHOAMI,ENTLIN,ATTACH,GETMEM
       EXTERN  SETMEM,BFOPEN,BFREAD,BFCLOS,IOERR,BCDEC,GETA
       EXTERN  CLOSE,RESETF,DEAD,PRDIAG
       REM
       UNLIST
       INSERT  EQU
       INSERT  BEQU
       INSERT  COMMON
       INSERT  MACRO
       DETAIL
       LIST
TEST00 SYN     *
INIT   LMTM                  NEED ALL 7 FOR GETA
       CALL    SETERR(ERROR,LOCK) SET UP FILE ERROR PROCEDURE
       CAL     PRINT         PRDIAG TO PRINT ONLINE
       STA*    PTERR         ..
       CALL    WHOAMI((CARD,,5)) GET LOGIN COMMAND NAME
       CAL     CARD+4        ..
       SLW     LGNDMP        SET INTO LOGIN COMMAND FOR DAEMON
       SLW     LGNFIB        .. AND FIB
       SWT     1             IS SWITCH SET TO INHIBIT FIB LOGIN
       TRA     *+2           .. NO
       TRA     ENTDMP        .. YES
       TSX     ENTLIN,4      SWITCH UP, ENTER LINE TO LOGIN FIB
       PAR     LGNFIB,,6     ..
       PAR     =1            .. USER NO. 1
       HTR     *             ..
ENTDMP SWT     2             IS SWITCH SET TO INHIBIT DAEMON LOGIN
       TRA     *+2           .. NO
       TRA     INIT1         .. YES
       TSX     ENTLIN,4      ENTER LINE TO LOGIN INCREMENTAL DUMPER
       PAR     LGNDMP,,5     ..
       PAR     =2            .. USER NO. 2
       HTR     *             .. UNLIKELY FULL RETURN
       REM
INIT1  CALL    ATTACH(SYSFIL,SYSFIL+1) ATTACH TO TSS FILES
       TSX     GETMEM,4      GET OLD MEMORY BOUND
       STA     OPN1+4        ASSIGN DISK BUFFERS
       STA     OPN2+4        ..
       ADM     =432          ..
       STA     OPN1+5        ..
       STA     OPN2+5        ..
       ADM     =432          ..
       TSX     SETMEM,4      SET NEW MEMORY SIZE
       REM
OPN1   CALL    BFOPEN(=HR,PRTYGP,TIMACC,*-*,*-*,=-0,IOERR)
       CALL    BFREAD(PRTYGP,TIMACC,(CARD,,14),EOFPTY,TMP.,IOERR)
       CALL    BCDEC(CARD)   CONVERT MXUSRS TO BINARY
       PAX     ,4            ..
       TXH     PRTYER,4,N    CHECK IF TOO BIG
       XEC.A   STO(MXUSRS)   SET VALUE IN SUPERVISOR
       XEC.A   STO(MXUSRS+Q.MIN) LOAD-LEVELER PLATEAU ALSO
PRTYLP CALL    BFREAD(PRTYGP,TIMACC,(CARD,,14),EOFPTY,TMP.,IOERR)
       CALL    BCDEC(CARD)   GROUP NO.
       PAC     ,2
       TXL     PRTYER,2,-GROUPS-1 INSURE CORRECT VALUE
       CALL    BCDEC(CARD+1) NO. PRIME LINES ASSIGNED TO THIS GROUP
       PAX     ,4            ..
       TXH     PRTYER,4,N    NOT DAMN LIKELY
       XEC.A   STA(GRPTBL,2) SET PARTY GROUP ALLOCATION
       TRA     PRTYLP        CONTINUE TILL DONE
       REM
PRTYER TSX     PRINT,4       HERE FOR FORMAT ERROR IN PRTYGP TIMACC
       PZE     FMTERR,,8     PRINT MESSAGE TO OPERATOR
       AXT     GROUPS+1,2    AND RESET GROUP TABLE ARRAY
ZGRP   XEC.A   STZ(GRPTBL+GROUPS+1,2) ..
       TIX     ZGRP,2,1      ..
       REM
EOFPTY CALL    BFCLOS(PRTYGP,TIMACC,IOERR) CLOSE PRTYGP FILE
       REM
OPN2   CALL    BFOPEN(=HR,TSSFL,TIMACC,*-*,*-*,=-0,IOERR)
       AXC     0,2           INITIALIZE TSSFIL TABLE
TSSLP  CALL    BFREAD(TSSFL,TIMACC,(CARD,,14),EOFTSS,TMP.,IOERR)
       CAL     CARD          PROBN OF TSSFIL
       XEC.A   SLW(TSSTBL,2) SET INTO SUPERVISOR
       CAL     CARD+1        PROGN
       XEC.A   SLW(TSSTBL+1,2) ..
       TXI     *+1,2,-2      INCREMENT POINTER
       TXH     TSSLP,2,-TSSMAX CONTINUE IF STILL ROOM
       REM
EOFTSS CALL    BFCLOS(TSSFL,TIMACC,IOERR) CLOSE TSSFIL FILE
       REM
EXIT   CALL    ATTACH(=0,=1,*+1) DETACH BACKGROUND FROM ANY UFD
       TSX     DEAD,4        INITIALIZATION COMPLETE, EXIT
       EJECT
ERROR  TSX     PRDIAG,4      HERE FOR DISK ERROR IN INIT.
       TRA     ERRXIT        CLOSE FILES AND EXIT
       REM
LOCK   STO     LCKD+4        HERE FOR FILE LOCK
       STQ     LCKD+6        FILE NAME INTO MESSAGE
       TSX     PRINT,4       PRINT MESSAGE ONLINE
       PZE     LCKD,,9       ..
       REM
ERRXIT CALL    CLOSE(=HALL,=-0,*+1) PANIC CLOSEOUT
       CALL    RESETF(*+1)   RESET ANY THAT GOT AWAY
       TRA     EXIT
       REM
LCKD   BCI     / TIME ACCOUNTING FILE /
       PZE
       BCI     / /
       PZE
       BCI     / IS LOCKED./
       REM
FMTERR BCI     / TIME ACCOUNTING FILE PRTYGP TIMACC HAS ILLEGAL FORMAT./
       REM
LGNFIB BCI     6, LOGIN C0056 FIBMON 99995 099999
LGNDMP BCI     5, LOGIN C0056 DAEMON 99999
       REM
SYSFIL CTSSID                TSS FILE DIRECTORY
       REM
PRTYGP BCI     1,PRTYGP
TSSFL  BCI     1,TSSFIL
TIMACC BCI     1,TIMACC
       REM
CARD   BSS     14            CARD IMAGE BUFFER
TMP.   PZE                   TEMPORARY
       REM
       RMT     *
       DETAIL
       REF     1
       END
