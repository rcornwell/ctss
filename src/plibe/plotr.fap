* 
       LINK    ON
       ENTRY   ENDUP    
       ENTRY   STRTUP   
       ENTRY   FACTOR   
       ENTRY   WHERE    
       ENTRY   PLOT1    
       EXTERN  WRFILE   
FACTOR SXD     FACTOR-2,4     
       CLA     X  
       STO     TX 
       CLA     Y  
       STO     TY 
       CLA     P  
       STO     TP 
       CLA*    1,4
       STO     X  
       STZ     Y  
       STZ     P  
       TSX     WRB,4    
       CLA     TX 
       STO     X  
       CLA     TY 
       STO     Y  
       CLA     TP 
       STO     P  
       LXD     FACTOR-2,4     
       TRA     2,4
       REM
       REM
WHERE  CLA     X  
       STO*    1,4
       CLA     Y  
       STO*    2,4
       TRA     3,4
       REM
       REM
PLOT1  TRA     NSTRT    
       CLA*    1,4
       STO     X  
       CLA*    2,4
       STO     Y  
       CLA*    3,4
       STO     P  
       TSX     WRB,4    
       LXD     FACTOR-2,4     
       TRA     4,4
       REM
       REM
WRB    SXD     XR4,4    
       SXD     XR1,1    
       LXD     USED,4   
       LXD     TAGCNT,1 
       CLA     X  
STOX   STO     ABF+4,4  
       CLA     Y  
STOY   STO     ABF+5,4  
       CLA     P  
       LDQ     TAGS     
       LGR     3  
       LRS     0  
       XCL  
       ARS     3  
       TIX     NOP,1,1  
STOP   STO     ABF-7,4  
       AXT     6,1
       SXD     TAGCNT,1 
       STZ     TAGS     
       TXI     TEST,4,-3
NOP    STO     TAGS     
       SXD     TAGCNT,1 
       TXI     TEST,4,-2
TEST   LXD     XR1,1    
       TXL     WRD,4,-428     
       SXD     USED,4   
       LXD     XR4,4    
       TRA     1,4
       REM
       REM
WRD    CLA     BUFNO    
       TNZ     USEA     
USEB   CLA     =1 
       STO     BUFNO    
       STZ     USED     
       CLA     BBUFF    
       ADD     =4 
       STA     STOX     
       ADD     =1 
       STA     STOY     
       SUB     =12
       STA     STOP     
       CLA     ABUFF    
       STA     WR1
       TRA     WRITE    
USEA   STZ     BUFNO    
       STZ     USED     
       CLA     ABUFF    
       ADD     =4 
       STA     STOX     
       ADD     =1 
       STA     STOY     
       SUB     =12
       STA     STOP     
       CLA     BBUFF    
       STA     WR1
WRITE  TSX     WRFILE,4 
       TXH     NAM1     
       TXH     NAM2     
       TXH     ZERO     
WR1    TXH     ABF,,432 
       TXH     ZERO     
       TXH     ZERO     
DONE   LXD     XR4,4    
       TRA     1,4
       REM
       REM
ENDUP  SXD     XR4,4    
       SXD     XR1,1    
       CAL     STOPD    
       SLW     PLOT1    
       LXD     USED,4   
       PCA     ,4 
       TZE     DONE     
       XCA  
       ZAC  
       DVP     =13
       TZE     NOTAG    
       MPY     =13
       XCA  
       ADD     =10
       PAC     ,4 
       LXD     TAGCNT,1 
       CLA     TAGS     
       LGR     6  
       TIX     *-1,1,1  
       STO*    STOP     
       LXD     USED,4   
       TRA     *+2
NOTAG  TXI     *+1,4,1  
       PCA     ,4 
       ADD     =4 
       ALS     18 
       STD     WR1
       CLA     BUFNO    
       TNZ     TAGA     
       LXD     WR1,4    
       SXA     ATAG,4   
       LXD     XR1,1    
       TRA     USEB     
TAGA   LXD     WR1,4    
       SXA     BTAG,4   
       TRA     USEA     
       REM
       REM
STRTUP CLA*    1,4
       STO NAM1   
       CLA*    2,4
       STO     NAM2     
       CLA     ABUFF    
       ADD     =4 
       STA     STOX     
       ADD     =1 
       STA     STOY     
       SUB     =12
       STA     STOP     
       CLA     WR1SET   
       STO     WR1
       STZ     BUFNO    
       STZ     USED     
       STZ     TAGS     
       CLA     CNTSET   
       STO     TAGCNT   
       CAL     STRTD    
       SLW     PLOT1    
       TRA     3,4
       REM
       REM
NSTRT  TSX     $ERNOTE,4
       TXH     =1 
       TXH     =0 
       REM
       REM
NRSTRT TSX     $ERNOTE,4
       TXH     =7 
       TXH     =0 
       REM
       REM
X      PZE  
Y      PZE  
P      PZE  
BUFNO  PZE  
ABUFF  PZE     ABF
BBUFF  PZE     BBF
ABF    BCI     1, (PLOT 
       BCI     1, TAPE) 
ATAG   PZE  
       BSS     429
BBF    BCI     1, (PLOT 
       BCI     1, TAPE) 
BTAG   PZE  
       BSS     429
NAM1   PZE     ** 
NAM2   PZE     ** 
TX     PZE  
TY     PZE  
TP     PZE  
USED   PZE  
ZERO   PZE  
TAGS   PZE  
TAGCNT PZE     ,,6
XR4    PZE  
XR1    PZE  
CNTSET PZE     ,,6
WR1SET TXH     ABF,,432 
STRTD  SXD     FACTOR-2,4     
STOPD  TRA     NSTRT    
       END  
