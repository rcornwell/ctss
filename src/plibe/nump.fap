* 
       LINK    ON
       LBL     NUMP,X
       REM
       REM        SUBROUTINE TO COUNT ARGUEMENTS
       ENTRY   NUMP     
       ENTRY   PCTEXT   
       EXTERN  PICTUR   
NUMP   SXD     NUMP-2,4 
       SXA     IR2,2    
       SXA     REST,1   
       CLA*    1,4
       STO     SOURCE   
GET    CLA*    3,4
       PAX     ,1 
       AXT     0,2
TTST   CAL     1,1
       ANA     =O777777700000  CHECK DECREMENT AND PREFIX   
       TZE     OK              PZE NO DECREMENT,  F A P CALL
       SUB     =O007400000000 
       TZE     OK              TAGLESS TSX, F II CALL 
       CAL     1,1
       ANA     =O700000000000    KEEP ONLY PREFIX FOR MAD CHECK   
       SUB     =O300000000000  IS IT TXH, NO DECREMENT, M A D CALL
       TNZ     TOT
OK     TXI     *+1,1,-1      DECREASE X1 TO GET NEXT INSTRUCTION  
       TXI     *+1,2,1  INCREASE COUNT    
       TRA     TTST        TEST NEXT INST.
TOT    ZET     SOURCE          WAS IT A CALL FROM PICTUR    
       TRA     REST        NOT PICTUR     
       TXI     *+1,1,-1 
       SXA     PCTEXT,1 
REST   AXT     **,1     
       LXD     NUMP-2,4 
       PXA     ,2 
       STO*    2,4        IN N
IR2    AXT     **,2     
       TRA     4,4
EP     PZE     ** 
SOURCE PZE     ** 
       REM         RETURN FROM PICTUR TO CALLING PROGRAM - ALLOWING FOR 
       REM            VARIABLE NUMBER OF ARGUMENTS.   
PCTEXT AXT     **,4     
       TRA     0,4
       END  
