* WRBSS - WRITE A BSS FILE.
       LINK    OFF
       REM     NOEL I. MORRIS - 6/22/65
       REM
       UNLIST
       INSERT  MACS
       LIST
       ENTRY   WRBSS
       REM
WRBSS  SXA     XR4,4         SAVE XRS
       STI     SVSI          AND SI
       REM
       LDI     BUFF          FIRST WORD IN SI
       PIA                   LOOK AT FIRST WORD
       ANA     =O77000000    MASK OFF WORD COUNT
       PDX     ,4            WORD COUNT IN XR4
       TXL     XR4,4,0       DON'T WRITE ANYTHING IF WORD COUNT ZERO
       REM
       NZT     FNAM-1        CHECK FOR SQZBSS FILE
       TRA     FIX           ..
       REM
       CAL     =HSQZBSS      SECONDARY NAME IS 'SQZBSS'
       LNT     200000        CHECK FOR INSTRUCTION CARD
       TXI     SLW,4,2       MUST BE PROGRAM CARD
       TXI     SLW,4,4       INSTRUCTION CARD
       REM
FIX    CAL     =H   BSS      SECONDARY NAME IS 'BSS'
       AXT     28,4          RECORD LENGTH IS 28
       REM
SLW    SLW     SNAM          STORE SECONDARY NAME
       SXD     WRITE+3,4     STORE WORD COUNT
       REM
WRITE  CALL    $BFWRIT(FNAM,SNAM,(BUFF,,**),$DSKER)
       REM
XR4    AXT     **,4          RESTORE XRS
       LDI     SVSI          AND SI
       TRA     1,4           RETURN
       REM
SVSI   PZE
SNAM   PZE
       REM
       COMMON  27
BUFF   COMMON  3
FNAM   COMMON  2
       RMT     *             REMOTE SEQUENCES
       REM
       END
       REM
