*               MAD III FOR CTSS ... R M GRAHAM ... MAR 1964
       TITLE
       ABS
GETBRK BOOL    31
       ORG     GETBRK
       TIA     K10
SETBRK TIA     K11
ILC    SXD     K12,4
       SLW     K15
       STQ     K16
       ARS     35
       STO     K17
       TSX     GETBRK,4
       STA     K12
       CLA     K13
       STO     K52
       TRA     K52-1
K51    TSX     RDFLX,4
       PZE     0
       CLA     0
       SUB     K14
       TZE     K50
       LDQ     1
       AXT     6,4
       RQL     3
       LGL     3
       TIX     *-2,4,1
       LDQ     0
       AXT     6,4
       RQL     3
       LGL     3
       TIX     *-2,4,1
       SLW     *+2
       TSX     SDUMP,4
K52    PZE     **,,**
       TRA     K51
K50    TSX     SETBRK,4
       PZE     ILC
       CLA     K17
       ALS     35
       LDQ     K16
       ORA     K15
       LXD     K12,4
       TRA*    K12
K10    BCI     1,GETBRK
K11    BCI     1,SETBRK
K14    BCI     1,RETURN
K13    PZE     K12,,K17
K12    PZE
K15    PZE
K16    PZE
K17    PZE
       REM
IOBT   OPSYN   IORT
PXD    OPD     475401120000
TXH    OPD     300011120000
TXL    OPD     700011120000
MADCOM BOOL    144
       ORG     MADCOM
PASS1  BSS     1
PASS2  BSS     1
PASS3  TRA     INIT
       TRA     SDUMP
       BSS     1             SEARCH
INSERT BSS     1
IDNOW  BSS     1
PGCERS BSS 1                  PZE PGMCOM,,ERAS
SIMPIO PZE
EQ.CT  PZE
NORMD  PZE
CONF   PZE
CON1   PZE
COMLOC PZE
TVSTRT PZE
UPBRK  PZE
PRSTRT PZE
       TRA     SUCCES        SUCCESSFUL EXIT
       TRA     UNSCCS        UNSUCCESSFUL EXIT
HICOM. BSS     1
PRGTVS PZE
SBTEST PZE
SPRINT TRA     PRLINE
P.FNAM BSS     1
LIST.F BSS     1
SYM.FL BSS     1             FOR SYMTAB
SYMFSW BSS     1
RESETF TIA     H1
       TRA     CHNCOM
DELFIL TIA     H2
       TRA     WRFLX
RDFLX  TIA     H3
       TRA     CHNXIT
SETMEM TIA     H4
(BFOPN TTR     BFOPEN
(BFRD  TTR     BFREAD
(BFWR  TTR     BFWRIT
(BFCL  TTR     BFCLOS
(BFCOD TTR     BFCODE
(EOFCT PZE     0
GETCOM TIA     H11
TTYOUT TIA     H12
*
*      MAD III INTERNAL PARAMETERS (PP)
*
PPLC   EQU     4096          DIFFERENCE FACTOR FOR DEFINED LITERALS
PPCDIM EQU     400           CDIM TABLE SIZE
PPOPMD EQU     200           OP-MODE TABLE SIZE (SEARCH STARTS AT 128)
PPBCD  EQU 20                 LINE BUFFER SIZE
PPBIN  EQU 25                 SMALL BINARY BUFFER SIZE
PPFLAD EQU 1000               FLOATING ADDRESS TABLE SIZE
PPLINK EQU     500       LINK TABLE SIZE
PPELNK EQU     500     EXTERNAL LINK TABLE SIZE
PPADTT EQU 20                 ADDITIONAL TRANSFER TABLE SIZE
PPPETY EQU 30                 PROGRAM ENTRY TABLE SIZE
PPAABC EQU 30                 ADDITIONAL ABCON TABLE SIZE
PPSD   EQU     400           STATEMENT DICT BUFFER SIZE
PPTEM  EQU     100           TEMPORARY TABLES' SIZE
PPREBN EQU     27
PPCODE EQU     12*950
PPINST EQU     7200
PPREL  EQU 20                 COMMON-TV SEPARATION
PPMRK1 EQU     600           MAX STATEMENT MTX
PPMRK2 EQU     1200          MAX CODE OUT
PPSEQ  EQU     100           MAX SEQUENCE LENGTH
SCRA   EQU     2
SCRB   EQU     4
PRINT  EQU     6
PUNCH  EQU     7
INT    EQU     5
*      COMMON STORAGE
WB1    SYN     -432
WB2    SYN     WB1-432
B1     SYN     WB2-432
B2     SYN     B1-432
RB1    SYN     B2-432
RB2    SYN     RB1-432
CMST   SYN     RB2-1
CTT    SYN     CMST-800-1
CABC   SYN     CTT-256-1
PL     SYN     CABC-650-1
CDIM   SYN     PL-200-1
CDIM.  SYN     CDIM-PPCDIM-1-PPCDIM
INST   SYN     CDIM.-1 HIGH END OF MTX OF PART 1
TTB    SYN     INST-PPINST-1-1   CTTBUF OF PART II
*              PART III STORAGE
CODE   SYN     TTB-256-1
LINK   SYN     CODE-PPCODE-1
BCD    SYN     LINK-PPLINK-2
FLAD   SYN     BCD-PPBCD-1
ELINK  SYN     FLAD-PPFLAD-1
ADTT   SYN     ELINK-PPELNK-1
PETY   SYN     ADTT-PPADTT-1
AABC   SYN     PETY-PPPETY-1
SD     SYN     AABC-PPAABC-1
PTEM   SYN     SD-PPSD-1
FTEM   SYN     PTEM-PPTEM-1
BIN    SYN     PTEM
REBN   SYN     FTEM
S1     SYN     FTEM-PPTEM-1-432
S2     SYN     S1-432
SD10   SYN     S2
TMTX   SYN     SD-10    HIGH END OF OPMD EXPANSION
       REM                   COMMON SUBROUTINES
       REM
PRLINE NZT     LIST.F
       TRA     2,4
       SXA     SPRXIT,4
       CAL     1,4
       STA     LIST.C
       STD     LIST.C
       ARS     18
       STT     BLSW.         SAVE PZE OR MZE IN CALLING SEQUENCE
       ANA     MQA
       AXT     1,4
       NZT     BLSW.         IF NON ZERO, INSERT BLANK WORD
       TRA     *+3
       AXT     2,4           ACCOUNT FOR EXTRA WORD
       ACL     AQ1           **
       SXD     PRL.,4
       STA     L.MARK
       TSX     BFWRIT,4
       TXH     P.FNAM
       TXH     P.FNAM+1
PRL.   TXH     L.MARK,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       TSX     BFWRIT,4
       TXH     P.FNAM
       TXH     P.FNAM+1
LIST.C TXH     **,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
SPRXIT AXT     **,4
       TRA     2,4
L.MARK OCT     777777000000,606060606060
BLSW.  PZE     0,,**         NON ZERO IF BLANK WORD SHOULD BE INSERTED
CHNCOM ZAC
       TIA     H13
WRFLX  SXA     TTYXIT,4
       CLA     1,4
       STO     *+2
       TSX     TTYOUT,4
       PZE     **,,**
TTYXIT AXT     **,4
       TRA     PRLINE
CHNXIT SYN     *
       TSX     P$CLOSE,4
       TXH     ALL
       TXH     PQ4
       TXH     *+1
       CAL     PRNTER
       LDQ     AQ0
       TSX     NEXCOM,4
*
ALL    BCI     1,ALL
PRNTER BCI     1,PRNTER
NEXCOM TIA     H17
UNSCCS TSX     WRFLX,4
       PZE     TRNER.,,4
       TRA     SUCCXX
SUCCES TSX     BFCLOS,4
       TXH     B.S.S.
       TXH     B.S.S.+1
       TXH     *+1
       NZT     SYMFSW
       TRA     SUCCXX
       TSX     BFCLOS,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     *+1
SUCCXX TSX     BFCLOS,4
       TXH     P.FNAM
       TXH     P.FNAM+1
       TXH     *+1
       TSX     RESETF,4
       TRA     CHNCOM
TAB1   BCI     1,TABLE
       PZE
       BCI     1,MAD
TAB2   BCI     1,ENTRY
       PZE
TAB3   BCI     1,SYMBOL
       PZE
TAB4   BCI     1,SEQNOS
       PZE
TAB5   BCI     1,TV
       PZE
TAB6   BCI     1,DIM
       PZE
TAB7.0 BCI     1,MAP
               TAB7.9-TAB7.1+1+1
       BCI     1,LENGTH
TAB7.1 PZE     0,,**
       BCI     1,TV
TAB7.2 PZE     0,,**
       BCI     1,VAR
TAB7.3 PZE     **,,**
       BCI     1,LIT
TAB7.4 PZE     **,,**
       BCI     1,PROG
TAB7.5 PZE     **,,**
       BCI     1,TEMP
TAB7.6 PZE     **,,**
       BCI     1,COMMON
TAB7.7 PZE     **,,**
       BCI     1,SYMTAB
TAB7.8 PZE     **,,**
       BCI     1,PROLOG
TAB7.9 PZE     **,,**
       REM
TRNER. BCI     4,      TRANSLATION ERROR
BC13   PZE
       EJECT
SDUMP  SXA     SD1,4           DUMP 8 LOCATIONS PER LINE IN OCTAL
       SXA     SD11,2          ALWAYS DUMP A MULTIPLE OF 8 LOCATIONS
       SXA     SD12,1          ADR OF FIRST LOC ON LINE ENDS IN ZERO
       CAL     1,4             FIRSTLOC,,LASTLOC
       PDX     ,4              GET LAST LOC
       TXI     *+1,4,1         CALCULATE FIRST LOC NOT NEEDED
       SCD     SD2,4           SAVE FOR END TEST
       ANA     MQ100           CALCULATE FIRST LOC TO DUMP
       PAC     ,1              POINTER TO NEXT LOC TO DUMP
       TOV     *+1             OV IS USED TO COUNT BCD CHARS
SD7    PCA     ,1              GET FIRST LOC FOR CURRENT LINE
       LGR     18              IN MQ TO BE EXPANDED
       AXT     6,2             6 CHARACTERS IN THE MQ
       AXT     10,4
       CAL     AQ1             SET UP FOR OVERFLOW WHEN WORD IS FULL
SD4    ALS     3               FORM FIRST HALF OF BCD CHARACTER
       LGL     3               FORM LAST HALF
       TOV     SD3             CHECK FOR FULL AC
SD8    TIX     SD4,2,1         BACK FOR NEXT CHARACTER IN MQ IF ANY
       TXL     SD5,4,1         CHECK FOR END OF CURRENT LINE
       LDQ     COM1            PUT A BLANK BETWEEN NUMBERS IN LINE
       LGL     6               FOR PROPER SPACING
       TOV     SD6             AND CHECK IF AC IS FULL
SD9    LDQ     0,1             GET NEXT LOC TO CONVERT
       TXI     *+1,1,-1        MOVE LOC POINTER UP
       AXT     12,2            12 CHARACTERS IN THE MQ
       TRA     SD4             EXPAND THEM
SD3    SLW     SD10,4          STORE FULL AC IN OUTPUT LINE
       CAL     AQ1             SET AC TO START NEW WORD
       TXI     SD8,4,-1        AND CONTINUE
SD6    SLW     SD10,4          DITTO
       CAL     AQ1             ..
       TXI     SD9,4,-1        ..
SD5    LDQ     COM1            FILL OUT LAST WORD WITH BLANKS
       LGL     12
       SLW     SD10,4          AND STORE IT
       TOV     *+1             TURN OFF THE OVERFLOW
       TSX     WRFLX,4
       PZE     SD10-10,,10
SD2    TXH     SD7,1,**        GO FOR NEW LINE IF NEEDED
SD1    AXT     **,4            OTHERWISE RETURN
SD11   AXT     **,2            ..
SD12   AXT     **,1            ..
       TRA     2,4             ..
       EJECT
       REM REGION INITIALIZATION CONSTANTS
       REM
PBCD   PZE PPBCD
PBIN   PZE 20,,20
PFLAD  PZE PPFLAD
PLINK  PZE PPLINK
PELINK PZE     PPELNK
PADTT  PZE ADTT
PCODE  PZE PPCODE,,PPCODE
PREBN  PZE PPREBN,,PPREBN
PAABC  PZE PPAABC
PLC    PZE     PPLC
       REM
       REM PROGRAM VARIABLES
       REM
LOC    PZE
TVSIZE PZE
SATEST PZE
CURPL  PZE     PL
OP     PZE
AOPND  PZE
AA     PZE
MA     PZE
BOPND  PZE
AB     PZE
MB     PZE
TOPND  PTH
MT     PZE
I      PZE
FNSW   PZE
CON    PZE 16
MSTDEC PZE
DICLOC PZE
DICETY PZE
FNR    PZE
FNFLAD OCT     0,0           TEMPS FOR FUNCTION FLADS
FNFNRS PZE     0 NON-ZERO TO USE TEMP (FLAD+1) FOR FN FLAD
RELBIT PZE
RELCON PZE PPREL
COMBRK PZE
TVS    PZE
COMREL PZE
LDRREL PZE
SAVLOC PZE
IR1    PZE
IR2    PZE
IR1A   PZE
IR2A   PZE
ABASE  PZE
BBASE  PZE
       REM
       REM CONSTANTS
       REM
H1     BCI     1,RESETF
H2     BCI     1,DELFIL
H3     BCI     1,RDFLXA
H4     BCI     1,SETMEM
H5     BCI     1,OPEN
H6     BCI     1,RDFILE
H7     BCI     1,WRFILE
H8     BCI     1,RDWAIT
H9     BCI     1,WRWAIT
H10    BCI     1,CLOSE
H11    BCI     1,GETCOM
H12    BCI     1,WRFLX
H13    BCI     1,CHNCOM
H14    BCI     1,(MAIN)
H15    BCI     1,FWAIT
H16    BCI     1,BUFFER
H17    BCI     1,NEXCOM
(W     BCI     1,W
(R     BCI     1,R
PQ5    MON
PQ4    FOR
PQ6    SIX
AQ1    PZE 1
AQ0    PZE
AQ16   PZE 16
AQ17   PZE 17
AQ18   PZE 18
AQ19   PZE 19
AQ26   PZE 26
AQ22   PZE 22
AQ51   PZE     51
AQ2    PZE 2
PQ3    PTH
PQ2    PTW
PQ1    PON
AQ4    PZE 4
AQ8    PZE 8
AQ9    PZE     9
AQ3    PZE 3
AQ330  OCT     330
AQ20   PZE 20
AQ384  PZE 384
AQBLPL OCT 000000140400
AQBLMI OCT 000000141000
(15K10 OCT     150000000000
(4K5   OCT     400000
AQ48   PZE 48
TQ1    PZE     0,1
DQ22   PZE 0,,22
DQ1    PZE     0,,1
DQ2    PZE     0,,2
DQ8    PZE     0,,8
DQ13   PZE     0,,13
EQ146  DEC 146B8
EQ128  DEC 2B2
AQ512  OCT 1000
AQEOF  OCT 606060606061
       REM LOGICAL
MQ77   OCT     77
MQDT   OCT 077777700000
MQPD   OCT 777777000000
MQPDD  OCT     777717000000
MQMPD  OCT     777770000000
MQMDT  OCT     000007700000
MQMTA  OCT     000000770000
MQMDP  OCT     770000000000
MQMD   OCT     000007000000
MQTA   OCT 000000777777
MQPTA  OCT 700000777777
MQPDA  OCT     777777077777
MQA    OCT 000000077777
MQT    OCT 000000700000
MQCT6  OCT     777777477777
MQD    OCT 077777000000
MQPDT  OCT 777777700000
MQDA   OCT 077777077777
MQ34   OCT     003400000000
MQ7    OCT     070000000000
MQ6    OCT     060000000000
MQ2CI  OCT 777777770000
MQCON  OCT     000000001777
MQPSE  OCT     777760000000
MQIND  OCT     000060000000
MQINDS OCT     000300000000
WQTXL  OCT 606367436040
WQ479  OCT 400500000000
WQ279  OCT 200500000000
MQ100  OCT     77774
       REM INSTRUCTIONS
IQSTO  STO     0
IQSTA  STA     0
IQTRA2 TRA 0,2
IQSXA4 SXA     0,4
IQSXD1 SXD     0,1
IQAXT4 AXT     0,4
IQTXH  TXH     0,0,0
IQSUB  SUB     0
IQCLA4 CLA 0,4
IQCAL5 CAL PQ5
IQALS  ALS 18
IQSTD  STD     0
IQTXL  TXL     0,0,0
IQTTR  TTR     0
IQPSE  PSE     0
IQLXA1 LXA     0,1
IQLXA2 LXA     0,2
IQLXD1 LXD     0,1
IQTSX  TSX     0,0
IQTRA4 TRA     0,4
IQTRAD TRA DEFINE
IQLAS  LAS     AQDEF
AQSUB  PZE SUB
AQTAG  PZE TAG
AQPAR  PZE PAR
AQFCN  PZE FCN
AQEFTY PZE EFETY
AQCON  PZE CONST
AQTAGA PZE TAGA
AQFLD  PZE FLD
AQBCD  PZE     BCDOP
AQDEF  PZE DEF
       REM
       REM COMMENTS
       REM
COM0   BCD 10
COM1   BCD 1
COM2   BCD 31MAD PROGRAM,TYPE
       BCI     2,NOV 1963
       BCD 5  (ALL NUMBERS ARE OCTAL)
COM3   BCI     8,0TOTAL LOCATIONS         TRANSFER VECTOR LENGTH
       BCI     5,       PROGRAM BEGINS
       BCI     6, FIRST UNUSED COMMON/ERASABLE
COM3A  BCI     5,LENGTH000000.  TV SIZE000000.
       BCI     2, ENTRY000000
       BCI     5,.  FIRST UNUSED COMMON000000.
COM4   BCD 60 PROGRAM IS AN EXTERNAL FUNCTION.
       BCD 5 THE FOLLOWING ARE ENTRIES
COM5   BCD  0VARIABLE STORAGE (A=ARRAY,C=COMMON,E=ERASABLE,DIGIT=MODE)
COM6   BCD 40FUNCTION DICTIONARY
COM7   BCD 40ABSOLUTE CONSTANTS
COM8   BCD 90ARRAY VARIABLE            HAS NOT BEEN DIMENSIONED.
COM9   BCD 40STATEMENT DICTIONARY
COM10  BCD 20PROGRAM
COM11  BCI     2,0ERROR
       BCI     2, PART III...
       BCI     2,000000000000
       BCI     9, TRANSLATION HALTED WITH STATEMENT ENDING
COM12  BCD 9SEQUENCE FOR OP-MODE ARGUMENT        INCORRECT OR TOO LONG
COM13  BCD  0A VECTOR VALUES STATEMENT ATTEMPTS TO PRESET ERASABLE
COM14  BCI ,0A VECTOR VALUES STATEMENT ATTEMPTS TO PRESET COMMON WIT
       BCI ,LOCATABLE INFORMATION OR STATEMENT LABELS ASSIGNED TO COMMON
COM15  BCD 6MAIN STORAGE AND ERASABLE OVERLAP
COM16  BCD  TEMPORARY          SHOULD HAVE BEEN RESERVED BY OPERATOR
       BCD 5       IN STATEMENT ENDING
COM17  BCD 30MAD ERROR
COM18  BCD 50IDENTICAL STATEMENT LABELS
COM19  BCD   (THE NUMERIC FORM OF THE OPERATOR-MODE ARGUMENT IS
       BCD 1 )
COM20  BCD  TEMPORARY          RESERVED BUT NOT USED AS OPERAND IN STA
       BCD 3MENT PRECEDING
COM22  BCD  INVALID OPERATOR-MODE COMBINATION          (OOO/M/M) IN TH
       BCD 3STATEMENT ENDING
COM23  BCD 50PARAMETER USE TABLE EXCEEDED
COM24  BCD 60ADDITIONAL TRANSFER TABLE EXCEEDED
COM25  BCD 60FLOATING ADDRESS TABLE EXCEEDED
COM26  BCD 60ADDITIONAL ABCON TABLE EXCEEDED
COM27  BCD 50PROGRAM ENTRY TABLE EXCEEDED
COM28  BCD 50STORAGE DICTIONARY EXCEEDED
COM29  BCD  ILLEGAL OPERATION OR ADDRESS MNEMONIC IN DEFINING SEQUENCE
COM30  BCD  OPERATION-MODE OR TRANSLATION MATRIX TABLE EXCEEDED
COM31  BCD 7OPERATOR-MODE NOT IN TABLE
COM32  BCD 4TEMPORARY TABLE EXCEEDED
COM33  BCD 1 A00
COM34  BCD 1  00
COM35  BCD 100C000
COM36  BCD 100E000
COM37  BCD 100 000
COM39  BCD 1000*00
       REM
       REM CONVERT ADDRESS TO OCTAL SUBROUTINE (CA)
       REM    ARG IN AC ADDRESS-RESULT INAC
CADDR  LRS 15
       CLA AQ384
CA4    LLS 3
       ALS 3
CA2    LLS 3
CA3    ALS 3
CA5    LLS 3
       ALS 3
CA1    LLS 3
       ALS 3
       LLS 3
       TRA 1,4
       REM     FIND HIGH AND LOW ADDRESS IN A TWO ENTRY
       REM     TABLE, WHERE THE BASE ADDRESS CONTAINS THE
       REM     LENGTH.
       REM     TSX           ROGHL,4
       REM                   BA        BASE ADDRESS
       REM
       REM     RETURN WITH ADDRESS IN 'HIGH.' AND 'LOW.'
       REM
ROGHL  SXA     ROGHLX,4
       CLA     1,4
       STA     ROGHL1
       ADM     AQ1
       STA     ROGHL3
ROGHL1 LXA     **,4
       TXL     ROGHLX,4,0    EMPTY
ROGHL2 CLA*    ROGHL3
       STA     HIGH.
       STA     LOW.
       TRA     ROGHL4
       REM
ROGHL3 CLA     **,4
       ANA     MQA
       CAS     HIGH.
       STO     HIGH.
       NOP
       NOP
       CAS     LOW.
       TRA     ROGHL4
       TRA     ROGHL4
       STO     LOW.
ROGHL4 TIX     ROGHL3,4,2
ROGHLX AXT     **,4
       TRA     2,4
       EJECT
HIGH.  OCT     0,0
LOW.   SYN     HIGH.+1
       REM
       REM     RELOCATE ADDRESS AFTER PASS 2 OF MAD 3
       REM     THIS MEANS THAT RELCON, UPBRK AND COMBRK
       REM     HAVE BEEN SET.
ROREL  ANA     MQA
       SUB     UPBRK
       LRS     0
       ADD     UPBRK
       TQP     1,4
       SUB     COMBRK
       LRS     0
       ADD     COMBRK
       SUB     RELCON
       TQP     1,4
       ADD     RELCON
       TRA     1,4
       REM
       REM
       REM
       REM WRITE 4WD BCD GROUPS SUBROUTINE (WB)
       REM FORM 2WD INPUT BUFFER GROUPS
WRBCD  CLA     1,4
       STA     WB14
       STA     WB15
       STA     WB16
       STA     WB17
       STA     WB18
       STA     WB20
       ADD     AQ1
       STA     WB24
       STA     WB19
       SXA     WB22,4
WB14   LXA     **,1
       TXL     WB31,1,0
WB21   LXA     AQ20,2
       CLA COM1
       STO BCD,2
       TIX *-1,2,1
       LXA AQ20,2
WB3    LDQ     COM33
WB24   CAL     **,1
       STT     WBT1
       PBT
       LDQ     COM34
       STQ     WBT2
       LDQ     COM36         LOAD .E. CHECK VS. LOWEST HIGH MEMORY
       ANA     MQA
       SUB     UPBRK
       LRS     0
       ADD     UPBRK
       TQP     WB2.
       SUB     COMBRK
       LRS     0
       ADD     COMBRK
       SUB     RELCON
       TQP     WB23
       ADD     RELCON
WB12   STQ     WBT3
       TSX     CADDR,4
WB11   TXH WB10               FORMAT CHANGE SWITCH 3
WB4    TXH WB5                FORMAT CHANGE SWITCH 1
       SLW BCD+2,2
WB15   CLA     **,1
       STO BCD+1,2
       CAL     WBT1
       ARS     3
       ACL     WBT2
       ADM     WBT3
       SLW     BCD+3,2
WB6    TIX WB8,2,4
WB7    TSX     PRLINE,4
       PZE     BCD-20,,20
       TIX     WB21,1,2
WB22   AXT     **,4
WB31   TRA     2,4
WB2.   LAS     HICOM.        LOAD .E. CHECK BS LOWEST HIGH MEMORY
       TRA     WB12
       NOP
       LDQ     COM35
       TRA     WB12
WB23   LDQ     COM37
       TRA     WB12
WB8    TIX WB3,1,2
       TRA WB7
WB5    TXH WB9                FORMAT CHANGE SWITCH 2
       SLW BCD,2
WB16   LDQ     **,1
       CLA AQBLPL
       TQP *+2
       CLA AQBLMI
       LLS 2
       TSX CA3,4
       SLW BCD+1,2
       PXD
       LLS 3
       ALS 3
       TSX CA4,4
       SLW BCD+2,2
       PXD
       TSX CA1,4
       LDQ COM1
       LGL 24
       SLW BCD+3,2
       TRA WB6
WB9    SLW BCD,2
WB17   CLA     **,1
       ADD RELCON
       LRS 18
       ADD RELCON
       LRS 17
WB18   STQ     **,1
       CLA WQTXL
       STO BCD+1,2
       PXD
       LLS 2
       ALS 3
       TSX CA4,4
       SLW BCD+2,2
       PXD
       LLS 3
       TSX CA4,4
       SLW BCD+3,2
       CAL COM1
       TRA WB13
WB10   SLW BCD,2
WB19   LDQ     **,1
       CLA AQ48
       LGL 18
       ALS 6
       ORA AQ48
       ALS 6
WB20   LDQ     **,1
       ORA AQ16
       TQP *+2
       ADD AQ16
       SLW BCD+1,2
       PXD
       LLS 2
       TNZ     WB25
       STQ     WBT1
       XCA
       ANA     MQINDS
       SUB     MQINDS
       TNZ     WB26
       CLA     BCD+1,2
       STO     BCD+1,2
WB26   LDQ     WBT1
       PXD
WB25   ALS     9
       ORA AQ384
       TSX CA2,4
       SLW BCD+2,2
       PXD
       LLS 3
       ALS 9
       ORA AQ384
       LLS 3
       ALS 9
       ORA AQ384
       TSX CA1,4
       SLW BCD+3,2
       PXD
       TSX CA5,4
       LDQ COM1
       LGL 18
WB13   SLW BCD+4,2
       TIX WB8,2,5
       TRA WB7
WBT1   PZE
WBT2   PZE
WBT3   PZE
       REM
       REM WRITE OUTPUT TAPE FROM 2 WD INPUT
       REM BUFFER ENTRIES SUBROUTINE (WT)
B.S.S. PZE
       BCI     1,   BSS
WTBIN  CLA     1,4
       STA     WT22
       STA     WT25
       ADD     AQ1
       STA     WT23
       STA     WT3
       STA     WT24
WT22   LXA     **,1
       TXL WT1,1,0
       SXD WTT1,4
       LXD BIN+1,4
       LXD BIN,2
WT2    TXL WT3,2,19
       AXT 25,4
       STZ BIN,4
       TIX *-1,4,1
WT23   CLA     **,1
       ANA MQA
       STO BIN-24
       LXA AQ0,4
WT3    CLA     **,1
       ANA MQA
       SUB BIN-25
       SUB BIN-24
       TNZ WT7
WT24   CLA     **,1
       ANA MQT
WT6    PXD                    PXD OR CAL P=S OR ALS 18
       STP WTT2
       LDQ AQ0
       LRS 0,4
       ORS BIN-22
       STQ WTT3
       CAL WTT3
       ALS 1
       ORS BIN-21
       CLA WTT2
       TMI WT10
       TNZ WT11
       TXI WT12,4,-2
WT10   TNZ WT13
WT11   TXI WT12,4,-3
WT13   TXI WT12,4,-4
WT12   TXL WT7,4,-73
WT25   CAL     **,1
       SLW BIN,2
       ACL BIN-23
       SLW BIN-23
       CLA BIN-25
       ADD AQ1
       STO BIN-25
       TIX WT9,2,1
WT5    CLA BIN-24
       CAS     HICOM.
       TRA     WT15
       NOP
       CAS     UPBRK
       TRA     WT14
       NOP
       CLA     BIN-25
       ALS 18
       ADD BIN-24
       SUB RELCON
WT17   ORA     WQ279
       SLW     BIN-24
       ACL BIN-22
       ACL BIN-21
WT17.  ACL     BIN-23
       SLW BIN-23
WT8    AXT     24,4
       CLA BIN,4
       STO REBN-3,4
       TIX     *-2,4,1
       AXT     4,2
       TRA     WT18
WT26   CLA     ID1
       STO REBN-3
       CLA ID2
       STO REBN-2
       CLA ID3
       STO REBN-1
       TSX     BFWRIT,4
       TXH     B.S.S.
       TXH     B.S.S.+1
       TXH     REBN-PPREBN,,PPREBN+1
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
WT21   TXL     MP33    SWITCHED AFTER PGM CARD
       TRA     WT29
WT18   CLA WTT6+1,2
       ARS 1
       TZE WT19
       STO WTT6+1,2
       TRA WT20
WT19   CLA AQ512
       STO WTT6+1,2
       TIX WT18,2,1
WT20   CLA     WTT7
       ALS     24
       ALS 12
       STO ID3
       CLA     WTT6
       ALS     12
       ORA     WTT5
       ALS     12
       ORA     WTT4
       SLW     ID2
       TRA     WT26
WT29   LXA     AQ20,2
WT9    TIX WT2,1,2
       SXD BIN,2
       SXD BIN+1,4
       LXD WTT1,4
WT1    TRA     2,4
WT7    TXI WT5,1,2
WT14   CLA     BIN-22
       ADD     BIN-21
       TNZ     WT16
       LXA     AQ20,2
       CLA     BIN,2
       STO     BIN-2,2
       TIX     *-2,2,1
       STZ     BIN-1
       STZ     BIN-2
       CLA     BIN-25
       ALS     18
       ADD     BIN-24
       SUB     WQ479
       SLW     BIN-24
       TRA     WT17.
WT16   TSX     ERROR,4
       PZE     COM14,,20
WT15   TSX ERROR,4            LOADING ERASABLE
       PZE COM13,,10
WTT1   PZE     0,,-MP2+2
WTT2   PZE
WTT3   PZE
WTT7   OCT     1000
WTT4   OCT 1000
WTT5   OCT 1000
WTT6   OCT 1000
ID1    PZE
ID2    PZE
ID3    OCT     100010001000
       REM
       REM    BCD WRITE ROUTINE  'BW)
       REM
BCDWR  LGL     4
       TNZ     BW21
       CLA     BW11
       STO     BW2
BW21   ALS     18
       SUB     DQ13
       STD     BW23
       SXA     BWT2,2
       SXA     BWT5,1
       CLA     BWT5
       SUB     BWT2
       TZE     BW2
       CLA     BWT1
       SUB     BWT2
       STA     BW6
       STA     BW7
       CLA     IRT3
       SUB     BWT2
       STO     CODE,2
       TSX     WRBCD,4
BW6    NOP     **
       TSX     WTBIN,4
BW7    NOP     **
BW2    LXA     BWT5,1
       CLA     CODE,1
       LRS     12
       ALS     6
       ORA     AQ48
       LLS     12
       SLW     BCD-2
       CLA     CODE+1,1
       STO     BCD-1
       AXT     13,4
       TXI     *+1,1,-2
BW23   TXL     BW22,4,**
       CLA     CODE,1
       STO     BCD-2,4
       TXI     *+1,1,-1
       TXI     BW23,4,-1
BW22   CLA     COM1
       STO     BCD-2,4
       TIX     BW22,4,1
       ZET     SYMFSW
       STL     SEQNSW
BW4    TSX     PRLINE,4
       MZE     BCD-15,,15
BW5    TXH     BW18,1,PPCODE
BW9    CLA     BW10
       STD     BW5
       TMI     BW12
       STA     BW13
       STP     BW14
       ZET     BW14
       AXT     PPCODE/2,1
       CLA     SATEST
       TZE     BW19
       SUB     AQ1
       STO     SATEST
       SUB AQ1
       TNZ     *+4
       CLA BWT3
       STD BW20
       STD     BW3
       TXH     BW17,,0
       CLS     *-1
       STO     *-2
       TSX     BFOPEN,4
       TXH     (R
       TXH     C.O.DE
       TXH     C.O.DE+1
       TXH     B1
       TXH     B2
       TXH     PQ4
       TXH     PQ4
       TXH     MADERR
BW17   TSX     BFREAD,4
       TXH     C.O.DE
       TXH     C.O.DE+1
       TXH     BW24,,1
       TXH     MADERR
       TXH     (EOFCT
       TXH     MADERR
       CLA     BW24
       STD     BW13
       TSX     BFREAD,4
       TXH     C.O.DE
       TXH     C.O.DE+1
BW13   TXH     **,,**
       TXH     MADERR
       TXH     (EOFCT
       TXH     MADERR
BW19   CAL     BW9
       ACL     AQ1
BW12   STA     BW9
BW18   SXD     CODE,1
       REM     HERE TO WRITE OUT SEQUENCE NUMBERS
       REM
       NZT     SEQNSW        IS THERE A WAITING SEQUENCE NUMBER
       TRA     BW.X
BW.1   TRA     *+1
       CLA     TAB7.5        FIRST TIME THRU, SET UP FIRST
       STA     FLOC
       ARS     18            AND LAST LOCATION
       ADM     FLOC          TO TEST BOUNDS
       SUB     AQ1
       STA     LLOC
       STL     BW.1
BW.1B  SXA     IRT3,1
       LXA     IRT3,2
       LDQ     CODE,1
       ZAC
       LGL     2
       SUB     AQ3
       TZE     BCDWR
       XCL
       ARS     2
       TSX     ROREL,4
       STO     BW.T
       CAS     FLOC          COMPARE ADDRESS TO FIRST LOCATION OF PGM
       TRA     BW.2          .G. FIRST LOCATION, CHECK HIGH END
       TRA     BW.3          .E. FIRST, SURELY O K
       TRA     BW.X          .L. FIRST, NO GOOD
BW.2   CAS     LLOC          COMPARE TO LAST LOCATION
       TRA     BW.X          TO HIGH
       NOP
BW.3   CAL     BCD-1
       AXT     0,4
BW.3D  SLW     BW.T1
       ANA     MQ77
       LAS     AQ9
       TRA     BW.3C
       NOP
       LGR     6
       TXI     *+1,4,6
       TXH     BW.3C,4,30
       CAL     BW.T1
       ARS     6
       TRA     BW.3D
BW.3C  ZAC
       LGR     36,4
       AXT     6,4
       STZ     BW.T1
BW.3A  ZAC
       LGL     3
       ADM     BW.T1
       LGL     3
       ADM     BW.T1
       ADM     BW.T1
       STO     BW.T1
       TIX     BW.3A,4,1
BW.1A  AXT     0,4
       TXI     *+1,4,-1
       SXA     BW.1A,4
       TXH     BW.3B,4,-PPCDIM
       TXI     *+1,4,1
       SXA     BW.1A,4
       STL     BW.1
       TRA     BW.X
       REM
SEQNSW PZE     0
FLOC   OCT     0,0
LLOC   SYN     FLOC+1
BW.T   PZE
BW.T1  PZE
       REM
BW.3B  CLA     BW.T
       ALS     18
       ORA     BW.T1
       STO     CDIM.-1,4
BW.X   STZ     SEQNSW
       TRA     INREW
       REM
BW14   PZE
BW24   PZE
BW10   PZE     CODE-PPCODE/2,,PPCODE/2+PPMRK2
       PZE     CODE-PPCODE/4,,PPCODE/4+PPMRK2
BW3    MZE     *+1,,PPCODE/4
       PZE     CODE-PPCODE/2,,PPMRK2
       PON     CODE-PPCODE/4,,PPCODE/4+PPMRK2
BW20   MZE *-2,,PPCODE/4
BW11   TRA     FINIS
BWT1   PZE CODE
BWT2   PZE
BWT3   PZE
BWT5   PZE
       REM
       REM     ERROR SUBROUTINE
       REM
ERROR  LDQ     1,4
       STQ     ER5
MADERR SXD     ERT1,4
       LDC     ERT1,4
       STO     ERT1
       PXA     0,4
       TSX CADDR,4
       SLW COM11+1
       LDQ ERT1
       AXT     6,4
       PXD
ER1    ALS 3
       LGL 3
       TIX ER1,4,1
       SLW     COM11+4
       LDQ ERT1
       LGL 18
       AXT     6,4
       PXD
ER2    ALS 3
       LGL 3
       TIX ER2,4,1
       SLW     COM11+5
       TSX     WRFLX,4
       PZE     COM11,,13
       TSX     WRFLX,4
       PZE     BCD-15,,15
       TSX     WRFLX,4
ER5    PZE     COM17,,3
ER3    TRA     UNSCCS
ERT1   PZE
       REM
       REM     SUCCESSFUL COMPILATION (SC)
       REM
       REM     FINISH SEQNO TABLE, CDIM AND CMST
FINIS  NZT     SYMFSW
       TRA     QQ5
       LAC     BW.1A,4       SET UP COUNT OF SEQNOS
       SXD     BW.1D,4
       SXA     TAB4+1,4
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB4,,2
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
BW.1D  TXH     CDIM.,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       REM     NOW WRITE OUT DIMVEC AND CMST
       LXA     CDIM,2
       PXA     0,2
       TXL     QQ3,2,0
       STO     TAB6+1
       SXD     QQ4,2
       SSM
       ADD     QQ4
       STA     QQ4
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB6,,2
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
QQ1    LXA     CMST,1
       CAL     CDIM,2
       LAS     CMST,1
       TRA     *+2
       TRA     QQ2
       TIX     *-3,1,2
       TSX     MADERR,4      MUST BE IN MST
QQ2    CAL     CMST+1,1      GET ADDRESS OF SYMBOL
       TSX     ROREL,4
       LGR     18
       CAL     CDIM+1,2      GET DIMENSION OF VARIABLE
       LGL     18
       SLW     CDIM+1,2
       TIX     QQ1,2,2
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
QQ4    TXH     CDIM.,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       REM
QQ3    LXA     CMST,2        HERE TO WRITE OUT CMST
       TXL     QQ5,2,0
       PXA     0,2
       STO     TAB3+1
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB3,,2
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
QQ8    AXT     0,1
       CLA     CMST,2
       STO     CDIM.,1
       CAL     CMST+1,2      GET CODE WORD FROM TABLE
       SLW     CDIM.+1,1     SAVE PREFIX AND TAG
       ARS     18
       ANA     MQA           LOOK AT DIMVEC LOCATION
       TZE     *+3
       TSX     ROREL,4       RELOCATE IT
       ALS     18
       STD     CDIM.+1,1
       CLA     CMST+1,2      GET LOCATION OF VARIABLE
       TSX     ROREL,4
       STA     CDIM.+1,1
       TXI     *+1,1,-2
       TNX     QQ6,2,2
       TXH     QQ8+1,1,-PPCDIM
       AXC     PPCDIM,1
QQ6    SCD     QQ7,1
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
QQ7    TXH     CDIM.,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       TXH     QQ8,2,2
QQ5    SYN     *             END OF CDIM AND CMST WRITING
       REM
       CLA     SIMPIO
       RFT     300000
       TRA     SC2A
       TRA     SC2B          TAG=4, NO SYMBOL TABLE WANTED
SC2A   STZ     PARTSW
       RNT     600000
       STL     PARTSW        TAG=5, WANT PARTIAL SYMBOL TABLE
       LXA     CMST,1
       PXA     0,1
       NZT     PARTSW
       ADD AQ4
       STA     CMST
       ADD AQ1
       ALS     1
       PAX     0,2
       STO     IR2BEG
       ZET     PARTSW
       TRA     SC2
       CLA     SC9
       STO     CMST-2,1
       STO     CMST-4,1
       CLS COMLOC
       STO     CMST-1,1
       CLS     UPBRK
       STO     CMST-3,1
       TXI     *+1,1,4
SC2    LDI     CMST+1,1
       ZET     PARTSW
       LFT     100000
       TRA     *+3
       TIX     SC2,1,2
       TRA     SC7A
       RIL     100000
       STI     CMST+2,2
       CLA     CMST,1
       STO     CMST,2
       CLA     SIMPIO
       STO     CMST+1,2
       ADD     AQ1
       STO     SIMPIO
       STZ     RELBIT
       PIA
       ARS     18
       ANA     MQA
       TZE SC4
       SUB     COMBRK
       TMI SC4
       ADD     COMBRK
       SUB     UPBRK
       TPL SC3
       ADD     UPBRK
       SUB     RELCON
       ALS     18
       STD     CMST+2,2
SC3    CLA     AQ8
       STO     RELBIT
SC4    CLA     CMST+1,1
       ANA     MQA
       SUB     COMBRK
       TMI SC7
       ADD     COMBRK
       SUB     UPBRK
       TPL SC5
       ADD     UPBRK
       SUB     RELCON
       STA     CMST+2,2
SC5    CLA     AQ2
       ORS     RELBIT
SC7    CLA     RELBIT
       LRS     3
       LBT
       LLS     1
       LLS     2
       ALS     15
       ADD     SIMPIO
       STO     CMST+3,2
       ADD     AQ1
       STA     SIMPIO
       TXI     *+1,2,-4
       TIX     SC2,1,2
SC7A   ZET     PARTSW
       TRA     SC1B
       CLA     CMST
       STO     CMST-2
       ADD AQ1
       ALS     1
       STA     CMST
       CLA     SIMPIO
       STO     CMST-1
SC1A   TSX     WTBIN,4
       NOP CMST
SC1    TSX WTBIN,4
       NOP SC6
       TRA     SUCCES
       PZE
SC6    PZE     2
SC9    BCI     1, ...
*
SC1B   PXA     0,2
       SUB     IR2BEG
       SSP
       ARS     1
       STZ     CMST,2
       STA     CMST,2        LENGTH = (IR2BEG-IR2END)/2
       ADD     AQ1
       ALS     1
       STO     CMST+2,2
       CLA     SIMPIO
       STO     CMST+1,2
       PXA     0,2
       SUB     CILOC
       STA     *+2
       TSX     WTBIN,4
       NOP     **
       TRA     SC1
*
SC2B   STO     CMST-1
       CLA     AQ2
       STO     CMST
       STZ     CMST-2
       TRA     SC1A
*
PARTSW PZE
IR2BEG PZE
CILOC  VFD     36/CMST+2
       REM
       REM INSTRUCTION REWRITE ROUTINE (IR)
       REM
INREW  LXD     CODE,3
       SXA     IRT3,1
IR24   PXD     0,0
       LDQ     CODE,1
       LGL     2
       SUB     AQ3
       TZE     BCDWR
       TXI     *+1,1,-2
       STZ RELBIT
       STZ IRT1
       CAL     CODE,1
       TZE IR5
       ARS 15
       PDX 0,4
       TRA     *+5,4
       TRA     IR22          INTERNAL PARAMETER
       TRA IR3                TEMP FLAD
       TRA     IR25          FLAD
       TRA     IR22          EXTERNAL PARAMETER
IR6    CAL     CODE-1,1
       ANA MQPDT
       ORA     CODE,1
       SLW     CODE-1,1
       ANA MQA
       SUB COMBRK
       TMI IR5
       ADD COMBRK
       SUB UPBRK
       TPL IR20
       ADD UPBRK
       SUB RELCON
       STA     CODE-1,1
IR20   CLA AQ2
       STO RELBIT
IR5    CAL     CODE-2,1
       PBT
       TRA *+2
       TRA IR23
       CLA     CODE-1,1
       ARS 15
       PDX 0,4
       TXL IRB,4,0
       TPL IR11
       TXI IR11,4,4
IR22   CLA     CODE,1
       ANA     MQTA
       STO     CODE,1
       TRA     IR6
IRB    CAL     CODE-1,1
       ANA     MQPDD
       SLW IRT1
       CLA IQPSE
       SBM IRT1
       TNZ IR7
       CLA     CODE-1,1
       ANA MQA
       ALS 18
       ORS IRT1
IR7    CLA IRT1
       SXD IRT2,2
       AXT     14,2
       AXT     64,4
IR10   TXI *+1,2,-2
       TXH     IR26,4,OPTAB-OPBGN
       LDQ OPTAB,4
       SLQ IRT1
       CAS IRT1               BINARY SEARCH FOR OP
IR26   TRA     IR8+1,2
       TRA IR9
       TRA     IR8,2
       TXI IR10,4,32
       TXI IR10,4,-32
       TXI IR10,4,16
       TXI IR10,4,-16
       TXI IR10,4,8
       TXI IR10,4,-8
       TXI IR10,4,4
       TXI IR10,4,-4
       TXI IR10,4,2
       TXI IR10,4,-2
       TXI IR10,4,1
       TXI IR10,4,-1
IR8    TXI IR10,4,1
       TXI IR10,4,-1
       NOP
       TSX     MADERR,4
IR23   CLA COM0
       TRA IR13
IR9    LXD IRT2,2
       CLA OPTAB,4
IR13   ANA MQTA
       ALS 18
       SLW     IRT4
       CLA RELBIT
       LRS 3                  TEST FOR 00,010,100,1010
       LBT                    CONFIGURATIONS
       LLS 1
       LLS 2
       ALS 15
       ORS     IRT4
       CLA     CODE-2,1
       STA     IRT4
       CLA     CODE-1,1
       STO     CODE,2
       CLA     IRT4
       STO     CODE+1,2
       TXI     *+1,1,-1
       TXI     IR24,2,-2
IR11   CLA CODE-2,1
       ANA MQD
       TZE IR12
       STD     CODE-1,1
       ARS 18
       SUB COMBRK
       TMI IR12
       ADD COMBRK
       SUB UPBRK
       TPL IR21
       ADD UPBRK
       SUB RELCON
       ALS 18
       STD     CODE-1,1
IR21   CLA AQ8
       ORS RELBIT
IR12   CLA OPTXI+1,4
       TRA IR13
*
SAVIND PZE
*
IR25   CLA     CODE,1        FLAD BINARY SEARCH
       STI     SAVIND
       PAI
       RNT     700000        TEST FOR TAG = 7
       TRA     *+2           NO
       TRA     IR25.         YES, LEAVE IT UNTOUCHED
       RFT     400000
       RIR     300000
       PIA
IR25.  LDI     SAVIND
IR14   SXD IRT2,2
       LXA FLAD,2             2X TABLE SIZE
       LXA AQ2,4
IR15   TXI *+1,2,-2
IR16   TXH IR17,4,**
       CAS FLAD,4
IR17   TRA IR18+1,2
       TRA IR19
       TRA IR18,2
       TXI IR15,4,1024
       TXI IR15,4,-1024
       TXI IR15,4,512
       TXI IR15,4,-512
       TXI IR15,4,256
       TXI IR15,4,-256
       TXI IR15,4,128
       TXI IR15,4,-128
       TXI IR15,4,64
       TXI IR15,4,-64
       TXI IR15,4,32
       TXI IR15,4,-32
       TXI IR15,4,16
       TXI IR15,4,-16
       TXI IR15,4,8
       TXI IR15,4,-8
       TXI IR15,4,4
       TXI IR15,4,-4
       TXI IR15,4,2
       TXI IR15,4,-2
IR18   TXI IR15,4,2
       TXI IR15,4,-2
       NOP
       TSX     MADERR,4
IR19   CLA FLAD+1,4
       ANA MQA
       ADD IRT1
       STO CODE,1
       LXD IRT2,2
       TRA IR6
IR3    CLA CODE,1            TEMP FLAD
       STA IRT1
       SUB IRT1
       TRA IR14
IRT1   PZE
IRT2   PZE
IRT3   PZE
IRT4   PZE
       REM
       REM OPERATION TABLE
       REM
OPBGN  OCT -300000636743
       OCT -200000634567
       OCT -100000626351
       OCT     -077400216723 AXC
       OCT     -077300515043 RQL
       OCT     -076500432751 LGR
       OCT     -076300432743 LGL
       OCT -076003626244      SSM
       OCT -076001472263      PBT
       OCT     -076000446225 MSE
       OCT     -075400476724 PXD
       OCT     -073700472423 PDC
       OCT     -073400472467 PDX
       OCT     -063400626724 SXD
       OCT     -062500626343 STL
       OCT     -062000624350 SLQ
       OCT     -060200465162 ORS
       OCT     -060000626350 STQ
       OCT     -053500432423 LDC
       OCT     -053400436724 LXD
       OCT     -052000457163 NZT
       OCT     -050100465121 ORA
       OCT     -050000232143 CAL
       OCT     -040000622244 SBM
       OCT     -034000432162 LAS
       OCT     -032000214521 ANA
       OCT     -030600646244 USM
       OCT     -030400642144 UAM
       OCT     -030200642662 UFS
       OCT     -030000642621 UFA
       OCT     -026000642644 UFM
       OCT     -020000444751 MPR
       OCT     -014000634546 TNO
       OCT     -013000672343 XCL
       OCT     -012000634431 TMI
       OCT     -010000634571 TNZ
       OCT     -004600473121 PIA
       OCT     -004200513121 RIA
       OCT     -000000447125 MZE
       OCT     000000477125 PZE
       OCT     002000635121  TRA
       OCT     002100636351  TTR
       OCT     004000634350  TLQ
       OCT     004100313121 IIA
       OCT     004200633146 TIO
       OCT     004300462131 OAI
       OCT     004400472131 PAI
       OCT     004600633126 TIF
       OCT     007400636267  TSX
       OCT     010000637125  TZE
       OCT     012000634743  TPL
       OCT     013100672321  XCA
       OCT     014000634665  TOV
       OCT     016100635046  TQO
       OCT     016200635047  TQP
       OCT     020000444770  MPY
       OCT     022100246547  DVP
       OCT     024100262447  FDP
       OCT     026000264447  FMP
       OCT     030000262124  FAD
       OCT     030200266222  FSB
       OCT     030400262144  FAM
       OCT     030600266244  FSM
       OCT     032000214562  ANS
       OCT     032200255121  ERA
       OCT     034000232162  CAS
       OCT     036100212343  ACL
       OCT     040000212424  ADD
       OCT     040100212444  ADM
       OCT     040200626422  SUB
       OCT     042000304751  HPR
       OCT     044000313162 IIS
       OCT     044100432431 LDI
       OCT     044200466231 OSI
       OCT     044400462663 OFT
       OCT     044500513162 RIS
       OCT     044600464563 ONT
       OCT     050000234321  CLA
       OCT     050200234362  CLS
       OCT     052000712563  ZET
       OCT     052200672523  XEC
       OCT     053400436721  LXA
       OCT     053500432123  LAC
       OCT     056000432450  LDQ
       OCT     060000626371  STZ
       OCT     060100626346  STO
       OCT     060200624366  SLW
       OCT     060400626331 STI
       OCT     062100626321  STA
       OCT     062200626324  STD
       OCT     062500626363  STT
       OCT     063000626347  STP
       OCT     063400626721  SXA
       OCT     073400472167  PAX
       OCT     073700472123  PAC
       OCT     075400476721  PXA
       OCT     076000234344  CLM
       OCT 076001432263       LBT
       OCT 076002233062       CHS
       OCT 076003626247       SSP
       OCT 076006234644       COM
       OCT 076010514524       RND
       OCT     076011265145  FRN
       OCT 076012242363       DCT
       OCT     076100454647  NOP
       OCT     076300434362  LLS
       OCT     076500435162  LRS
       OCT     076700214362  ALS
       OCT     077100215162  ARS
       OCT     077400216763  AXT
       OCT 100000636731
       OCT 200000633167
       OCT 300000636730
OPTAB  PZE OPTAB-OPBGN
OPTXL  OCT     -300000636743 TXL
OPTNX  OCT     -200000634567 TNX
OPSTR  OCT     -100000626351 STR
       OCT     0             PZE
OPTXH  OCT     300000636730  TXH
OPTIX  OCT     200000633167  TIX
OPTXI  OCT     100000636731  TXI
       REM
       REM                    MAIN PROGRAM (MP)
       REM
MAD3B  TSX     PRLINE,4
       PZE COM2,,10
       LXA     SATEST,4
       TXL     *+3,4,2
       TSX     BFCLOS,4
       TXH     C.O.DE
       TXH     C.O.DE+1
       TXH     *+1
       CLA     PBIN
       STO     BIN
       CLA     PREBN
       STO     REBN
       CLA     P.FNAM
       STO     B.S.S.
       TSX     DELFIL,4
       TXH     B.S.S.
       TXH     B.S.S.+1
       TXH     *+1
       TSX     BFOPEN,4
       TXH     (W
       TXH     B.S.S.
       TXH     B.S.S.+1
       TXH     RB1
       TXH     RB2
       TXH     PQ4
       TXH     AQ0
       TXH     CHNXIT
       LDQ     P.FNAM
       ZAC
MP50   LGL     6
       SUB     AQ48
       TNZ     MP51
       XCL
       ORA     AQ48
       XCL
       TRA     MP50
MP51   ADD     AQ48
       LGR     6
       STQ     TAB1+1
       TSX     DELFIL,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     *+1
       NZT     SYMFSW
       TRA     MP40
       TSX     BFOPEN,4
       TXH     (W
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     S1
       TXH     S2
       TXH     PQ4
       TXH     AQ0
       TXH     CHNXIT
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB1,,3
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
MP40   CLA     COMLOC
       SUB     TVSTRT
       STO TVSIZE
       CLA TVSTRT
       SUB RELCON
       STO COMBRK
       LDQ TVSTRT
       CAL FNSW
       TZE MP36
       STQ RELCON
MP36   LDQ COMREL
       CLA TVSTRT
       SUB RELCON
       TZE MP29
       STQ RELCON
       SUB LDRREL
MP29   STO TVSTRT
       CLA ADTT
       ARS 19
       SSM
       ADD RELCON
       STO RELCON
       CLA LOC
       SUB RELCON
       ADD AQ1
       STO LOC
       TSX CADDR,4
       SLW COM3+3
       SLW     COM3A+1
       CLA ADTT
       ARS 19
       ADD PRGTVS
       STO TVS
       TSX CADDR,4
       SLW     COM3+8
       LGR     24
       ORS     COM3A+3
       XCL
       ANA     MQ2CI
       ORS     COM3A+4
       CLA PRSTRT
       SUB RELCON
       TSX CADDR,4
       SLW     COM3+12
       SLW     COM3A+6
       CLA UPBRK
       ANA MQA
       STO UPBRK
       TSX CADDR,4
       SLW     COM3+18
       LGR     24
       ORS     COM3A+10
       XCL
       ANA     MQ2CI
       ORS     COM3A+11
       CLA     UPBRK
       SUB     HICOM.
       TNZ     *+5
       AXT     7,1
       SXD     MP..,1
       AXT     13,4
       SXD     *+2,4
       TSX     PRLINE,4
       PZE     COM3,,19
       ZET     SYMFSW        DON'T PRINT STAT LINE IF
       TRA     *+3           SYMBOL TABEL WANTED
       TSX     TTYOUT,4
MP..           COM3A,,12
       LXA     PETY,1
       TXL MP22,1,0
       TSX     PRLINE,4
       PZE COM4,,11
       TSX WRBCD,4
MP22X  NOP     PETY
MP22   LXA     CMST,4
       TXL     MP22.,4,0
       TSX     PRLINE,4
       PZE COM5,,10
       TSX WRBCD,4
       NOP     CMST
MP22.  TSX     PRLINE,4
       PZE COM6,,4
       LXD     ADTT,1        GET ADDITIONAL T.V. SIZE IF ANY
       TXL     MP25,1,0      SKIP IF NONE
       SXD     *+2,1         SAVE TO INCREMENT XR2 LATER
       LXA     CTT,2         GET THE COUNT
       TXI     *+1,2,**
       TXL     *+4,2,128     IF LESS THAN 128, OK
       PXA     0,2           OTHERWISE AN ERROR
       TSX     ERROR,4
       PZE     COM24+2,,4
       LAC     CTT,4
       TXI     *+1,4,CTT
       SXA     *+3,4
       SXA     CTT,2
       CLA     ADTT,1
       STO     **,1
       TIX     *-2,1,1
MP25   TSX WRBCD,4
       NOP     CTT
       REM     HERE TO PUT OUT 'MAP' PORTION
       REM     OF SYMBOL TABLE FILE
       REM
       NZT     SYMFSW
       TRA     MAP.X
       CLA     LOC
       ALS     18
       STD     TAB7.1
       CLA     TVS
       STA     TAB7.3
       ALS     18
       STD     TAB7.2
       TSX     ROGHL,4
       NOP     CABC
       CLA     LOW.
       TSX     ROREL,4
       STA     TAB7.4
       SUB     TAB7.3
       ALS     18
       STD     TAB7.3
       LXA     CMST,4
       TXH     *+2,4,0
       STZ     TAB7.3
       CLA     HIGH.
       SUB     LOW.
       ADD     AQ1
       ALS     18
       STD     TAB7.4
       CLA     PRSTRT
       TSX     ROREL,4
       STA     TAB7.5
       CLA     TAB7.6
       TSX     ROREL,4
       ADD     AQ1
       STA     TAB7.6
       SUB     TAB7.5        TEMP START- PROG START
       ALS     18            GIVES NO. LOCATIONS
       STD     TAB7.5        OF PROGRAM INSTRUCTIONS
       CLA     UPBRK
       STA     TAB7.7
       SSM
       ADD     HICOM.
       ALS     18
       STD     TAB7.7
       CAL     TAB7.8        ADJUST SYMTAB ADDRESS
       TSX     ROREL,4
       STA     TAB7.8
       CAL     TAB7.9        ADJUST PROLOG ADDRESS
       TSX     ROREL,4
       STA     TAB7.9
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB7.0,,TAB7.9+1-TAB7.0
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
MAP.X  SYN     *             END OF WRITING MAP
       REM
       REM
       REM WRITE PROGRAM CARD
       REM
       LXA     PETY,1
       TXH MP3,1,0
       CLA     H14
       STO     PETY-2
       CLA PRSTRT
       STO PETY-1
       TXI *+1,1,2
MP3    SXA     PETY,1
       SXD     MP22Y,1
MP37   CLA PETY+1,1
       SUB RELCON
       STO PETY+1,1
       TIX MP37,1,2
       NZT     SYMFSW
       TRA     MP22Y.
       CLS     PETY
       SLW     TAB2+1
       ADD     MP22X
       STA     MP22Y
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB2,,2
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
MP22Y  TXH     **,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
MP22Y. CLA     PETY-2
       SUB     H14
       TNZ     *+2
       STZ     PETY-2
       LXA     PETY,1
       CLA TVS
       ALS 18
       ADD LOC
       STO PETY-2,1
       CLA TVSTRT
       ALS 18
       ADD UPBRK
       STO PETY-1,1
       TXI *+1,1,2
MP4    LXD DQ22,2
       STZ BIN,2
       TIX *-1,2,1
       LXD DQ22,2
       PXD 0,1
       SUB DQ22
       TMI *+2
       PXD
       ADD DQ22
       ORA     WQ479
       SLW BIN-24
       SLW BIN-23
MP7    CAL PETY,1
       SLW BIN,2
       ACL BIN-23
       SLW BIN-23
       TIX *+2,2,1
       TRA *+2
       TIX MP7,1,1
       TRA     WT8
MP33   TIX MP4,1,1
       CLA     WT21     SWITCH WTBN
       SLW     WT21
       LXA     AQ0,5          INCREMENT ID
       TRA     WT21
       REM
       REM WRITE BINARY TRANSFER VECTOR
       REM
MP2    LXA     CTT,4
       TXL     MP2Z.,4,0
       TSX     WTBIN,4
MP2Z   NOP     CTT
       NZT     SYMFSW
       TRA     MP2Z.
       LXA     CTT,4         RESTORE CTT COUNT
       AXT     1,2           COUNTER FOR NEW CTT
       AXT     2,1           COUNTER FOR OLD
       CLA     CTT,1         GET OLD LOCATION ( NAME)
       STO     CTT,2         AND STORE MOVED DOWN
       TNX     *+3,4,2       COUNT ENTRIES
       TXI     *+1,2,1
       TXI     *-4,1,2
       SXD     MP2Y,2        STORE NEW COUNT
       PXA     0,2
       STO     TAB5+1
       SSM
       ADD     MP2Z
       STA     MP2Y
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
       TXH     TAB5,,2
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
       TSX     BFWRIT,4
       TXH     SYM.FL
       TXH     SYM.FL+1
MP2Y   TXH     **,,**
       TXH     EOF
       TXH     (EOFCT
       TXH     CHNXIT
MP2Z.  TSX     PRLINE,4
       PZE COM7,,4
       CLS WB4                SET SWITCH IN
       STO WB4                BCD OUTPUT ROUTINE
       TSX WRBCD,4
       NOP     CABC
       TSX WTBIN,4
       NOP     CABC
       REM
       REM LOAD AND WRITE STATEMENT DICTIONARY
       REM
       LXA     SD,3
       TXL     MP39,1,0
       CLS WB5                SET BCD FORMAT SWITCH 2
       STO WB5
       CLA IQCAL5
       STO WT6
MP17   CLA     SD+1,1
MP18   TNX     MP31,2,2
MP30   CAS     SD+1,2
       TRA     MP32
       TRA     MP34
       TRA     MP18
MP34   TSX     ERROR,4
       PZE     COM18,,5
MP32   LDQ     SD+1,2
       STO     SD+1,2
       STQ     SD+1,1
       CLA     SD,1
       LDQ     SD,2
       STQ     SD,1
       STO     SD,2
       CLA     SD+1,1
       TIX     MP30,2,2
MP31   AXT     0,2
       TIX     MP17,3,2
       TSX     PRLINE,4
       PZE COM9,,4
       TSX WRBCD,4
       NOP     SD
       TSX WTBIN,4
       NOP     SD
       REM
       REM LOAD AND WRITE PROGRAM
       REM
MP39   CLS WB11
       STO WB11
       CLA IQALS
       STO WT6                SET BIN FORMAT SWITCH 2
       CLA FLAD
       STD IR16               COMPUTE FLAD TABLE SIZE
       ANA MQD                AND SET FLAD SEARCH
       TZE MP28
       ARS 9                  ROUTINE
       ORA EQ146
       FAD EQ146
       SUB EQ128
       ARS 27
       ALS 1
MP28   STA FLAD
       TSX     PRLINE,4
       PZE COM10,,2
MP42   LXA     CODE,3
       TRA     INREW+1
       REM
       REM                MAD III INITIALIZATION
       REM
INIT   AXT     LINK-BOT,1
       STZ     LINK,1
       TIX     *-1,1,1
INIT1  CLA     PBCD
       STO BCD
       CLA PFLAD
       STO FLAD
       CLA PLINK
IN1    STO     LINK
       CLA PELINK
IN2    STO     ELINK
       CLA PADTT
       STO ADTT
       STZ     PETY
       CLA     TEM
       STO PTEM
       STO FTEM
       CLA PCODE
       STO CODE
       CLA PAABC
       STO AABC
       STZ     SD
       CLA     CURPL
       SUB     PL
       STO     CURPL
       ZET     SBTEST
       TRA     *+3
       PXD
       STD     BC5
       CLA     PGCERS
       ANA MQA
       STO LDRREL
       ADD RELCON
       STO COMREL
       REM
       REM STATEMENT PROCESSING (ST)
       REM
       CLA PRSTRT
       STO LOC
       AXT     PPINST,1
       TRA     MAD3A1
MAD3A  LXD INST,1
       TXI     *+1,1,-3      BIT 35 = RESULT IN MQ
MAD3A1 SXD     INST,1        BIT 34 = RESULT IN AC
       CLA INST,1             BIT 33 = A(I) =T(I-1)
       STO AOPND                   OR, IF OP(I-1)=TAG
       CLA INST+2,1                A(I)=T(I-2)
       STO BOPND              BIT 32=B(I)=T(I-1)
       CAL     INST+1,1
       LAS     AQBCD
       TRA     *+2
       TXI     BCDS,1,-3
ST12   LAS     AQDEF
       TRA     *+2
       TRA     INDEF
       CLA     OP
       CAS     AQTAG         OR, IF OP(I-1)=TAG
       TRA     *+2           B(I)=T(I-2)
       TRA     ST1           BIT 31 = 1
       CAS     AQFLD         BIT 30 = RESULT IN LAC
       TRA     *+2
       TRA     ST1
       CLA     I
       STA     TOPND
ST1    CLA     I
       ADD AQ1
       STO I
       STZ MSTDEC
       STZ     BBASE
       CLA INST+1,1
       STA     OP
       CAS     AQCON
       TRA     *+2
       TRA     ST15
       CAS     AQEFTY
       TRA     *+2
       TRA     EFETYS
       CLA     AQ51
       ANS CON                0=FLOATING
       CLA TOPND              1=INTEGER(ALPHA)
       SUB AOPND                3=FUNCTION NAME
       TNZ ST2                  4=STATEMENT
       CLA AQ8                2 = BOOLEAN
       ORS CON
ST2    CLA TOPND
       SUB BOPND
       TNZ ST3
       CLA AQ4
       ORS CON
ST3    CAL AOPND
       TSX MADET,4            ENTER MODE-ADDRESS
       STO AA
       STQ MA
       CLA     BBASE
       STO ABASE
       CAL BOPND
ST5    TSX MADET,4
       STO AB
       STQ MB
       CLA OP
       SUB AQPAR
       TZE ST4
ST14   LXD     TEM,1
       CLA*    TEM
       TPL     *+2
       STZ*    TEM
       TIX     *-3,1,1
       CAL     OP
       ALS     3             - OPMD ENTRY -
       ORA     MA       OP+MODEA+MODEB+NEWMB+NEWMA+ENTRY
       ALS     3                  - OR -
       ORA     MB       OP+MODEA+MODEB+0+MODERES+ENTRY
       TSX     OPMTL,4       -OR-
       TXH     ST23,1,0      OP+MODEA+MODEB+7+0+EXIT
       ARS     21
       TSX     CADDR,4
       SLW     COM22+6
       TSX     WRFLX,4
       MZE     COM22,,13
       TSX     WRFLX,4
       PZE     BCD-15,,15
       CLA     ER3
       STO     PRENDS
       LXD     INST,1
ST13   CLA     INST+1,1
       CAS     AQBCD
       TRA     *+2
       TRA     MAD3A1
       TIX     ST13,1,3
       TSX     MADERR,4
ST23   SXD OPMD,1
       STA     ST39
       CLA     OPMD,1
       STA     ST30
       STA     ST7
       ANA     MQMDT
       SUB     MQMD
       TNZ     ST37
ST30   TRA     **
ST37   LXA     ST7,4
       TXL     ST38,4,OPMD
ST36   TXH     ST38,4,TMTXF
ST39   AXT     **,4
       TXH     ST38,4,PPSEQ
       TXI     *+1,4,1
       SXA     ST39,4
ST7    CLA **
       STO STT1
       LXD CODE,2
       STZ CODE,2
       LRS 33
       ADD AQ1
       TNZ     ST17
       CLA     STT1
       ANA     MQT
       SUB     MQT
       TNZ     ST8
       CAL     MQPDA
       ANS     STT1
       PXD
       TRA     ST9
ST38   CLA     DET4
       ARS     21
       TSX     CADDR,4
       STO     COM12+5
       CLA     ST39
       ANA     MQA
       TSX     ERROR,4
       MZE     COM12,,10
ST17   SUB     AQ1
       TNZ ST9                TRA TO DECREMENT TYPE
ST10   CLA STT1
       STO CODE+1,2
       LDQ AQ0
       TSX ADEC,4             ENTER ADDRESS DECODING
       STA CODE+1,2
       STO CODE+2,2           SUBROUTINE
       CLA     CODE+1,2
       ARS     33
       TZE     ST22          NOT DECREMENT TYPE
       SUB     AQ1
       TZE     ST24
       SUB     AQ1
       TZE     ST24
       ADD     AQ4
       TZE     ST24
       TRA     ST16          DECREMENT TYPE BUT
ST22   CLA     CODE+1,2      DOES NOT CHANGE IR
       ANA     MQ34
       SUB     MQ34
       TNZ     ST25          NOT IR CHANGE TYPE
       CLA     CODE+1,2
       ANA     MQPDT
       SUB     IQLXA1
       TZE     ST19
       SUB     TQ1
       TZE     ST20
       CLA     CODE+1,2
       ANA     MQPD
       SUB     IQTSX
       TNZ     ST24
       STZ     IR1
       STZ     IR1A
       TRA     ST27
ST24   CLA     CODE+1,2      ZERO APPROPRIATE IR
       ANA     MQT
       SUB     TQ1
       TNZ     ST21
       STZ     IR1
       STZ     IR1A
       TRA     ST16
ST21   SUB     TQ1
       TNZ     ST16
       TRA     ST27
ST19   CLA     CODE+2,2
       SUB     IR1
       TZE     ST34
       CLA     CODE+2,2
       STO     IR1
       CLA     ADT2
       STO     IR1A
       TRA     ST16
ST34   CLA     ADT2
       TPL     ST18
       CLA*    ADT2
       SUB     DQ2
       STO*    ADT2
       TRA     ST18
ST20   CLA     CODE+2,2
       SUB     IR2
       TZE     ST34
       CLA     CODE+2,2
       STO     IR2
       CLA     ADT2
       STO     IR2A
       TRA     ST16
ST25   CLA     CODE+1,2
       ANA     MQ7
       SUB     MQ6
       TNZ     ST16          NOT A STORE OP
       CLA     ADT2
       TMI     ST28
       CLA CODE+2,2
       ANA     MQT
       TZE     ST28
       CLA     IR1A
       SUB     ADT2
       TZE     ST29
ST32   CLA     IR2A
       SUB     ADT2
       TZE     ST31
       TRA     ST16
ST28   CLA     CODE+2,2
       SUB     IR1
       TNZ     ST26
       STZ     IR1
       STZ     IR1A
       TRA     ST26
ST29   STZ     IR1A
       STZ     IR1
       TRA     ST32
ST31   STZ     IR2A
       STZ     IR2
       TRA ST16
ST26   CLA     CODE+2,2
       SUB     IR2
       TNZ     ST16
ST27   STZ     IR2A
       STZ     IR2
ST16   CLA     LOC
       STA CODE,2
       ADD AQ1                INCREASE LOC COUNTER
       STO LOC
       TXI     *+1,2,-3
       SXD     CODE,2
ST18   CAL     ST7
       ACL AQ1                INCREASE ADDRESS
       SLW ST7
       TRA     ST37          INTERPRET NEXT INSTRUCTION
ST4    CLA MSTDEC
       LDQ AQ0
       ARS 18
       STO AA
       STQ MA
       TRA ST14
ST8    LLS 15                 TEST PSEUDO-OP SEQUENCE
       STA ST7
       PXD
       LLS 3
       TZE ST11                    -TESTS-
       PAX 0,1                -1 + NEXT(F)+B+NEXT(T)
       CLA CON
       ARS     6,1
       LBT                    WHERE B IS A CODE TO
       TRA     ST37          INDICATE BINARY DECISION FUNCTIONS
       LLS 15                0  EXIT
       STA ST7               1  RESULT IN LAC
       TRA     ST37          2  TRUE
ST9    LLS 15                3  A(I) = T(I-1)
       LDQ PQ2
       TSX ADEC,4            4  B(I)=T(I-1)
       STD STT1
       ALS 18                5  RESULT IN AC
       STD CODE,2            6  RESULT IN MQ
       TRA ST10
ST11   LLS 15
       STA *+1
       TRA **
ST15   LXD CODE,2
       CLA LOC
       ORA PQ4
       SLW CODE,2
       ADD AQ1
       STA LOC
       CLA BOPND
       STO CODE+1,2
       STZ CODE+2,2
       TXI *+1,2,-3
       SXD     CODE,2
       TRA MAD3A
STT1   PZE
       REM
       REM   BCD SEQUENCE (BC)
       REM
BCDS   LXD     CODE,2
       CLA     AOPND
       ARS     12
       STD     BC29
       CLA     AOPND
       LRS     12
       ALS     6
       ORA     AQ48
       LLS     12
       SLW     BCD-2
       CLA     COM1
       STO     BCD-3
       CLA     AOPND
       ORA     PQ6
       SLW     CODE,2
       CLA     BOPND
       STO     BCD-1
       STO     CODE+1,2
       TXI     *+1,2,-2
       AXT     12,4
BC29   TXL     BC30,4,**
       CLA     COM1
       STO     BCD-3,4
       TXI     BC29,4,-1
BC30   CLA     INST,1
       STO     BCD-3,4
       STO     CODE,2
       TXI     *+1,1,-1
       TXI     *+1,2,-1
       TIX     BC30,4,1
BC34   TXH     0,,0          SWITCH TO LXD CODE, 2 IN DEFINE
       STZ     I
       CLA     INST+1,1
       SUB AQBCD
       TNZ     BC2           TRA IF STATEMENT END
BC5    TXH     BC3,1,PPINST
BC4    CLA     BC7
       STD     BC5
       TMI     BC8
       STA     BC9
       STP     BC23
       ZET     BC23
       AXT     PPINST/2,1
       CLA     SBTEST
       TZE     BC20
       SUB     AQ1
       STO     SBTEST
BC6    TSX     BFREAD,4
       TXH     M.T.X
       TXH     M.T.X+1
       TXH     R.CNT,,1
       TXH     EOF.
       TXH     (EOFCT
       TXH     CHNXIT
       CLA     R.CNT
       STD     BC9
       TSX     BFREAD,4
       TXH     M.T.X
       TXH     M.T.X+1
BC9    TXH     **,,**
       TXH     EOF.
       TXH     (EOFCT
       TXH     CHNXIT
BC20   CAL     BC4
       ACL     AQ1
BC8    STA     BC4
       TRA     BC3
EOF.   TSX     MADERR,4
R.CNT  PZE
M.T.X  BCI     2,M.T.X.O.V.F.
C.O.DE BCI     2,C.O.D.E.O.V.
BC7    PZE     INST-PPINST/2,,PPINST/2+PPMRK1
       PZE     INST-PPINST/4,,PPINST/4+PPMRK1
       MZE     *+1,,PPINST/4
       PZE     INST-PPINST/2,,PPMRK1-1
       PON     INST-PPINST/4,,PPINST/4+PPMRK1
       MZE     *-2,,PPINST/4
BC11   TSX     MADERR,4
BC3    TXH BC26,2,PPCODE/4
BC15   CLA     BC14
       STD     BC3
       TMI     BC16
       STA     BC18
       STP     BC23
       PXD     0,2
       SSM
       NZT BC23
       PXD
BC27   ADD BC24
       STD     BC18
       ZET     BC23
       AXT     PPCODE/2,2
       CLA     SATEST
       ADD     AQ1
       STO     SATEST
       TXH     BC12,,0
       CLS     *-1
       STO     *-2
       TSX     BFOPEN,4
       TXH     (W
       TXH     C.O.DE
       TXH     C.O.DE+1
       TXH     B1
       TXH     B2
       TXH     PQ4
       TXH     AQ1
       TXH     CHNXIT
BC12   LXD     BC18,4
       PXD     ,4
       STO     BC13
       TSX     BFWRIT,4
       TXH     C.O.DE
       TXH     C.O.DE+1
       TXH     BC13,,1
       TXH     EOF.
       TXH     (EOFCT
       TXH     CHNXIT
       TSX     BFWRIT,4
       TXH     C.O.DE
       TXH     C.O.DE+1
BC18   TXH     **,,**
       TXH     EOF.
       TXH     (EOFCT
       TXH     CHNXIT
       CLA     BC15
       ACL     AQ1
BC16   STA     BC15
BC26   SXD     CODE,2
BC28   TRA     MAD3A1
       TRA PR2
BC23   PZE
BC24   PZE     0,,PPCODE/4
BC14   PZE     CODE-PPCODE/2,,PPMRK2
       PON     CODE-PPCODE/4,,PPCODE/4+PPMRK2
       MZE     *-2,,PPCODE/4
BC21   TSX     MADERR,4
       REM
       REM STATEMENT END SEQUENCE
       REM
BC2    CLA UPBRK
       ANA MQA
       SUB LOC
       TPL BC17
       TSX ERROR,4            STORAGE OVERLAP
       MZE     COM15,,6
BC17   CLA AQ16
       STO CON
       CLA DICLOC
       TZE BC19
       SUB COMLOC
       TZE BC19
       LXA     SD,4
       TXI     *+1,4,2
       TXH     BC22,4,PPSD
       SXA     SD,4
       CLA     DICLOC
       STO SD+1,4
       CLA LOC
       ALS 18
       ORA IQTXL
       ORA DICETY
       SLW SD,4
BC19   STZ DICETY
       STZ DICLOC
       SXA     BC1,1
       LXD     TEM,1
       TXL     BC1,1,0
BC32   NZT*    TEM
       TRA     BC31
       CLA*    TEM
       STZ*    TEM
       TMI     BC31
       TSX     CADDR,4
       SLW     COM20+2
       TSX     WRFLX,4
       MZE     COM20,,13
       TSX     WRFLX,4
       PZE     BCD-15,,15
BC31   TIX     BC32,1,1
BC1    AXT     **,1
       TRA     BC5
BC22   TSX     ERROR,4
       PZE     COM28,,5
       REM
       REM MODE AND ADDRESS DETERMINATION SUBROUTINE (MA)
       REM      (ARGUMENT IN AC)
       REM   (RESULTS-ADDRESS IN AC, MODE IN MQ)
MADET  SLW MAT1
       ARS 15                      -ADDRESS CODES-
       PDX 0,1                PREFIX     TYPE
       TRA *+8,1                0     NULL OR CONSTANT
       TRA MAFCN              1EXTERNAL PARAMETER
       TRA MAPAR                2     FLOATING
       TRA MAABC                3     TEMPORARY
       TRA MAVAR                4     VARIABLE
       TRA MATEM                5     ABCON
       TRA MAFLT                6     PARAMETER
       TRA MAEPAR             7FUNCTION
MANULL CAL MAT1
       SSM
       LDQ AQ0
       TRA 1,4                  -ADDRESS OUTPUT-
MAFLT  CLA MAT1                 0+0+TAG+ADDRESS
       LDQ AQ4                OR
       TRA 1,4
MATEM  CLA MAT1                 2+0+0+TEMPNO.
       ANA MQA                     OR
       STO     MAT2
       TZE MANULL
       CLA I
       ANA MQA
       SUB MAT2
       STO MAT2
       ALS 2
       SUB MAT2
       ALS 18
       ACL     INST
       PDX     0,1
       LDQ INST+1,1
       LRS     18
       CLA MAT1               RESULT
       TRA 1,4
MAVAR  LXA     MAT1,1
       CLA     CMST,1
       STD     MSTDEC        ARRAY SN+DIM ADDRESS+MODE+ADDRESS
       STA MAT3
       STA     BBASE
       STT     MAT4
       CLA MAT1
       ANA MQDT
       TZE MA7
       CLA     CMST,1
       TMI MA8
       TRA     MA13
MA7    TXL MA8,4,-ST5
       CLA OP
       SUB AQSUB
       TZE MA9
       CLA OP
       SUB AQTAGA
       TNZ MA8
MA9    CLA     CMST,1
       TMI MA8
MA13   SSM
       STO     CMST,1
       CLA     ER3
       STO     PRENDS
       CLA     CMST-1,1
       STO     COM8+3
       SXA     MA14,4
       TSX     WRFLX,4
       PZE     COM8,,9
MA14   AXT     **,4
MA8    LDQ     MAT4
       RQL     21
       CLA     MAT1
       STT     MAT4
       ARS 18
       ADD MAT3               ADDRESS-ADD CONST
       ORA MAT4               ADD TAG
       TRA 1,4
MAABC  LXA     MAT1,1
       CLA MAT1                    -ABCB ENTRY-
       ARS 18                   0+0+0+ADDRESS
       ANA MQA
       STO MAT2
       LDQ MAT2
       CLA     CABC,1
       ANA MQA
       TRA 1,4
MAEPAR LXA MAT1,1
MA11   CLA **,1               PRESET TO EPL
       TRA MA10
MAPAR  LXA MAT1,1
MA12   CLA **,1              -2+ADD CONST+TAG+PL INDEX
MA10   ANA MQT
       ARS 15                 -PL ENTRY-
       STO MAT2               MODE+0+MODE+0
       CLA MAT1
       LRS 18
       STA MAT1
       LLS 36
       STD MAT1
       CLA MAT1
       LDQ MAT2
       TRA 1,4
MAFCN  LXA     MAT1,1
       CLA TTB,1              -TT ENTRY-
       LRS 18                 MODE RESULT+0+MODE RESULT+ADDRESS
       ARS 15
       LRS 32
       CLA OP
       SUB AQFCN
       TZE MA5
       LDQ AQ3
MA5    CLA TTB,1
       ANA MQA
       TRA 1,4
MAT1   PZE
MAT2   PZE
MAT3   PZE
MAT4   PZE
       REM
       REM ADDRESS DECODING ROUTINE (AD)
       REM   (ARGUMENT IN AC)
       REM   (RESULT IN AC)
ADEC   ANA MQA                     -ADDRESS TYPES-
       LRS     12
       TNZ     ADLIT
       LLS     4
       PAX     0,1
       PXD     0,0
       LLS     8
       STO     ADT3
       TRA *+16,1
       TRA ADC                15 COMPLEMENT CONSTANT
       NOP
       TSX     MADERR,4
       TRA     ADFF
       TRA     ADLM
       TRA     ADDT
       TRA ADZTA              9 ZTA
       TRA ADTV               8 TV
       TRA ADCF               7 CON F
       TRA ADC1               6 CON 1
       TRA ADBTA              5 BTA
       TRA     ADLP
       TRA ADT                3 T
       TRA ADB                2 B
       TRA ADA                1 A
ADC    CLA AQ0
       TRA 2,4
ADLIT  LLS     12
       SUB     UPBRK
       TPL     *+2
       SUB     PLC
       ADD     UPBRK
       TRA     1,4
ADA    CLA     ABASE
       STO     ADT2
       CAL     AA
AD13   SLW     ADT1
       ARS 15
       PDX 0,1
       TRA *+8,1
       TSX     MADERR,4
       TRA AD1                PARAMETER
       TSX     MADERR,4
       TRA AD19
       TRA AD2                TEMPORARY
       TRA AD3                FLOATING ADDRESS
       TRA AD22               EXTERNAL PARAMETER
AD3    CLA     ADT3
       ADD     ADT1
       TNZ AD17
       TRA 1,4
AD17   TRA 2,4
AD2    CLA     ADT3
       ALS     15
       ORS     ADT1
       LXD     TEM,1
       CLA*    TEM
       LAS     ADT1
       TRA     *+2
       TRA     AD4
       TIX     *-4,1,1
       SXA     AD18,4
       CLA     ADT1
       ANA     MQA
       STO     ADT1
       TSX     CADDR,4
       SLW     COM16+2
       CLA     I
       ANA     MQA
       SUB     ADT1
       STO     ADT1
       ALS     1
       ADD     ADT1
       ALS     18
       ADD     INST
       PDX     0,4
       CLA     INST+4,4
       TSX     CADDR,4
       SLW     COM16+10
       TSX     WRFLX,4
       MZE     COM16,,15
       TSX     WRFLX,4
       PZE     BCD-15,,15
       CLA     ER3
       STO     PRENDS
AD18   AXT     **,4
       TRA     AD6
AD4    SSM
AD14   STO*    TEM
AD6    PXA     0,1
       ADD     PQ3
       ADD     ADFNR
       STO     ADT1
       TRA     2,4
AD1    CLA ADT1
       ANA MQDA
       ORA     TQ1
       SUB     ADT3
       LXD LINK,1
       TXI *+1,1,2
       TXH AD8,1,PPLINK-2
       SXD     LINK,1
       STO LINK,1
       STQ LINK+1,1
       CLA LOC
       ORS LINK+1,1
       CLA     STT1
       ANA     MQPDT
       SUB     IQTXH
       TNZ     *+3
       CLA     AQPAR
       TRA     *+2
       CLA OP
       ALS 18
       STD LINK+1,1
       CAL     PQ4
       STP     ADT1
       CLS     IN1
AD23   STO     ADT2
       CLA     ADT1
       TRA 2,4
AD22   CLA ADT1
       ANA MQDA
       ORA     TQ1
       SUB     ADT3
       LXD ELINK,1
       TXI *+1,1,2
       TXH     AD8,1,PPELNK-2
       SXD     ELINK,1
       STO ELINK,1
       STQ ELINK+1,1
       CLA LOC
       ORS ELINK+1,1
       CLA     STT1
       ANA     MQPDT
       SUB     IQTXH
       TNZ     *+3
       CLA     AQPAR
       TRA     *+2
       CLA OP
       ALS 18
       STD ELINK+1,1
       CLS     IN2
       TRA AD23
AD8    TSX ERROR,4            LINK TABLE EXCEEDED
       PZE COM23,,5
ADB    CLA     BBASE
       STO     ADT2
       CAL AB
       TRA     AD13
ADT    LXD     TEM,1
       NZT*    TEM
       TRA     AD5
       TIX     *-2,1,1
       LXD     TEM,1
       TXI     *+1,1,1
       SXD     TEM,1
       TXH     AD7,1,PPTEM
AD5    CLA     TOPND
       TRA     AD14
       PXA     0,1
       ADD     PQ3
       ADD     ADFNR
       TRA     2,4
AD7    TSX     ERROR,4
       PZE     COM32,,4
ADDT   CLA     ADT3
       ALS     15
       ADD     TOPND
       STO     ADT1
       LXD     TEM,1
       CLA*    TEM
       LAS     ADT1
       TRA     *+2
       TRA     AD6
       TIX     *-4,1,1
       LXD     TEM,1
       NZT*    TEM
       TRA     AD9
AD12   TIX     *-2,1,1
AD11   LXD     TEM,1
       ZET*    TEM
       TXI     *+1,1,1
       TXI     *+1,1,1
       SXD     TEM,1
       TXI     *+1,1,-1
AD10   CLA     TOPND
       STO*    TEM
       TXI     *+1,1,1
       TXH     AD7,1,PPTEM
       ADD     TQ1
       STO*    TEM
       ZET     ADT3
       TRA     AD6
       TXI     AD6,1,-1
AD9    TXL     AD11,1,1
       TXI     *+1,1,-1
       ZET*    TEM
       TRA     AD12
       TRA     AD10
ADLP   CLA     ADT3
       TRA     *+2
ADLM   CLS     ADT3
       ADD     LOC
       TRA     2,4
ADBTA  CLA AA
       ALS 15
       ANA MQT
       ORS CODE+1,2
       TRA ADB
ADZTA  CLA AA
       ALS 15
       ANA MQT
       ORS CODE+1,2
       PXD
       TRA 1,4
AD19   LGL 1
       CLA ADT1
       TQP AD20
       ALS 18
       STD STT1
       TRA AD21
AD20   STA CODE+1,2
AD21   PXD
       TRA 2,4
ADTV   LXD     OPMD,1
       CLA     PGCERS        CHECK FOR ODD MTX SUBSCRIPTION
       TPL     AD24
       CLA     OPMD,1
       SUB     OPM1
       TZE     *+3
       SUB     DQ8
       TNZ     AD24
       CLA     ADT4
       TRA     AD25
AD24   LDQ     OPMD,1
       CLA     AQ330
       LGL     3
       ALS 3
       LGL     3
       ALS 3
       LGL     3
       ALS 3
       LGL     3
       ALS     3
       LGL     3
       SLW ADT1
       CLA ADT1
AD25   LXD     ADTT,1
       CAS ADTT,1
       TRA *+2
       TRA AD15
       TIX *-3,1,1
       LXD ADTT,1
       TXI *+1,1,2
       TXH AD16,1,PPADTT
       SXD ADTT,1
       STO ADTT,1
AD15   PXD 0,1
       ARS 19
       SSM
       ADD TVSTRT
       ANA MQA
       STO ADTT+1,1
       TRA 2,4
AD16   TSX ERROR,4            ADTT TABLE EXCEEDED
       PZE COM24,,6
ADC1   CLA CON1
       ANA MQA
       TRA 2,4
ADCF   CLA CONF
       TRA ADC1+1
ADFF   SXA     ADFFX4,4
       LXA     FNFNRS,4
       TXI     *+1,4,1
       CLA     FNFLAD
       TXL     *+2,4,0
       CLA     FNFLAD+1
ADFFX4 AXT     **,4
       TRA     2,4
TEM    PZE     PTEM,1
ADT1   PZE
ADT2   PZE
ADT3   PZE
ADT4   BCI     1,.SUBS
ADFNR  PZE
       REM
       REM EXITS FROM CODE GENERATION(OP)
       REM
ZXT    CLA AQ16
       TRA OP1
MQXT   CLA AQ17
       TRA OP1
ACMQXT CLA     AQ19
       TRA     OP1
ACXT   CLA AQ18
OP1    STO CON
       LXD OPMD,1
       CLA     OPMD,1
       ANA MQT
       ALS     3             STORE MODE OF RESULT
       LXD INST,1             IN OP
       STD     INST+1,1
       STO MT
       TRA MAD3A
SQXT   LXD INST,1
       CLA INST-2,1
       STD     INST+1,1
       TRA MAD3A
LACXT  CLA     AQ48
       TRA     OP1
AACXT  CLA PQ3
       STO AA
       LDQ AQ26
OP2    STO TOPND
       STQ CON
       LXD OPMD,1
       CAL     OPMD,1
       ANA     MQMDT
       STD OPT1
       ANA MQT
       ARS 15
       STO MA
       CLA OPT1
       ARS 18
       ANA MQA
       STO MB
       TRA ST14
BACXT  CLA PQ3
       LDQ AQ22
       STO AB
       TRA OP2
STRXT  LXD     CODE,2
       CLS     CODE-2,2
       STO     CODE-2,2
       TRA     ZXT
DICTS  CLA AB
       STO DICLOC
       CLA LOC
       STO DICETY
OP11   STZ     IR1
       STZ     IR2
       STZ     IR1A
       STZ     IR2A
       TRA MAD3A
FLDS   LXD FLAD,1
       TXI *+1,1,2
       TXH OP7,1,PPFLAD       TRA IF FLAD TABLE EXCEEDED
       SXD FLAD,1
       CLA AB
       STO FLAD,1
       CLA LOC
       STO FLAD+1,1
       STZ     IR1
       STZ     IR2
       STZ     IR1A
       STZ     IR2A
       TRA SQXT
SAVLCS CLA LOC
       STO SAVLOC
       CLA AB
OP13   STO LOC
       TRA ZXT
RSTLCS CLA SAVLOC
       TRA OP13
OP7    TSX ERROR,4
       PZE COM25,,6
IFBGNS CLA AQ1
       ORS FNSW
       CLA FNR
       ADD     DQ1
       STO FNR
       STO     ADFNR
       CLA     TEM
       STO     WP9
       CLA     OP9
       STO     TEM
       CLA*    CURPL
       ARS     18
       ADD     CURPL
       ADD     AQ1
       STA     MA12
OP3    STO     CURPL
       CLA     FNR
       ORA     PQ2
       ORA     MQT
       LXA     FNFNRS,4
       TXH     *+2,4,0
       STO     FNFLAD
       TXI     *+1,4,-1
       SXA     FNFNRS,4
       TXH     *+2,4,-2
       STO     FNFLAD+1
       TRA     OP11
OP9    PZE     FTEM,1
EFBGNS CAL PQ4
       SLW FNSW
       CLA*    CURPL
       ARS     18
       ADD     CURPL
       ADD     AQ1
       STA     MA11
       TRA     OP3
IFETYS LXD CODE,2
       CLA LOC
       STO     CODE+2,2
       CLA     IQTTR
       STO     CODE+1,2
       CLA AB
       STO CODE,2
       TXI     *+1,2,-3
       SXD     CODE,2
       TRA     OP11
EFETYS LXA     PETY,1
       TXI *+1,1,2
       TXH OP8,1,PPPETY       TRA IF PR ENTRY TABLE EXCEEDED
       SXA     PETY,1
       CLA BOPND
       STO PETY,1
       CLA LOC
       STO PETY+1,1
       TRA     OP11
OPT1   PZE
OP8    TSX ERROR,4
       PZE COM27,,5
FENDS  CLA FNSW
       TNZ OP10
       LXD ELINK,1
       SXD LINK,1
OP14   CLA ELINK,1
       STO LINK,1
       TIX OP14,1,1
OP10   LXD LINK,3             -LINK DOUBLE ENTRIES-
       TRA     OP4           0+PL INDEX+0 OR 1+ADD CON
OP6    CLA     LINK,2        0OR 2 + OP + 0 + LOC
       TLQ OP5
       STO LINK,1
       STQ LINK,2
       CLA LINK+1,1           SORT LINK TABLE
       LDQ LINK+1,2
       STO LINK+1,2
       STQ LINK+1,1
OP4    LDQ LINK,1
OP5    TIX OP6,2,2
       LXA AQ0,2
       TIX OP4,3,2
       REM
       REM DIFFERENCE FCN ABCONS AND SET UP
       REM ADDITIONAL ABCON TABLE(DF)
       LXD LINK,1
       TXL WPCD,1,0
       STZ LINK-2,1
DF6    CLA LINK+1,1
       ARS 18
       SUB AQPAR
       TNZ DF1
       CAL PQ4
       ORS LINK+1,1           PBIT=1 FOR OP=PAR
DF1    CLA LINK,1
       STD DFT2               PAR NO. OF CURRENT LINK
       STT     DFT2
       STA DFT1               CURRENT ADD CON
       CLA LINK-2,1
       STA DFT3               PREV ADD CON
       ANA     MQDT
       SUB DFT2
       TZE DF3                TRA IF SAME PAR NO.
DF9    CAL PQ4
       ORS LINK,1             PBIT=1 FOR FIRST OF PAR GROUP
       CLA DFT1
       TZE DF2
       SUB     MQA
       TZE     DF12
       ADD     MQA
DF8    LXD AABC,2
       TXL DF11,2,0
DF5    CAS AABC,2
       TRA *+2
       TRA DF4
       TIX DF5,2,1
       LXD AABC,2
DF11   TXI *+1,2,1
       TXH DF10,2,PPAABC      TRA IF AABC TABLE EXCEEDED
       SXD AABC,2
DF4    STO AABC,2
       PXA     0,2
       ADD LOC
       SUB AQ1
       ALS 18
       STD LINK+1,1
DF7    TIX DF6,1,2
       TRA WPCD               TRA TO WRITE PROLOGUE CODE
DF10   TSX ERROR,4
       PZE COM26,,6
DF2    CAL MQPTA
       ANS LINK+1,1
       TRA DF7
DF3    CLA LINK-1,1
       ALS 1
       PBT
       TRA *+2
       TRA DF9
       CLA DFT1
       TZE DF2
       SUB DFT3
       TZE DF2
       TRA DF8
DF12   CLS     AQ1
       TRA     DF8
DFT1   PZE
DFT2   PZE
DFT3   PZE
       REM
       REM WRITE CODE INTERLUDE (WI)
       EJECT
WCD    STQ CODE+1,2
       STO CODE+2,2
WI3    CLA LOC
       STO CODE,2
       NZT     TAB7.9
       STO     TAB7.9
       ADD AQ1
       STO LOC
       TXI *+1,2,-3
       SXD CODE,2
       TRA 1,4
WCD1   STQ CODE+1,2
       STA CODE+1,2
       STZ CODE+2,2
       TRA WI3
       REM
       REM WRITE PROLOGUE CODE (WP)
       REM
WPCD   LXD CODE,2             -LINK DOUBLE ENTRIES-
       STZ     TAB7.9
       LXD     AABC,1        0 OR 4 + PAR NO + 0 OR 1 + ADD CON
       TXL WP2,1,0            0 OR 4 OR 6+ CON ADDR + 0 + INST ADDR
       SXD WP1,1
       LXA AQ1,1
WP3    LDQ     AABC,1
       PXD     0,0
       TSX     WCD,4
       TXI *+1,1,1
WP1    TXL WP3,1,**
WP2    LXD FLAD,1
       TXI     *+1,1,4
       TXH OP7,1,PPFLAD       TRA IF FLAD TABLE EXCEEDED
       SXD FLAD,1
       CLA AB
       STO     FLAD+2,1
       CLA LOC
       STO     FLAD+3,1
       LDQ IQTXH
       PXD
       TSX WCD,4
       LDQ     IQSXD1
       CLA     AB
       TSX     WCD,4
       LDQ     IQSXA4
       LXA     FNFNRS,4
       CLA     FNFLAD
       TXH     *+2,4,-2
       CLA     FNFLAD+1
       TSX     WCD,4
       LXD LINK,1
       TXL WP10,1,0           TRA IF NO PARAMETERS SET
WP7    CAL LINK,1
       PBT
       TRA WP4                TRA IF NOT FIRST OF PAR GROUP
       ARS 18
       ANA MQA
       LDQ IQCLA4
       TSX WCD1,4
WP4    CAL LINK+1,1
       ANA MQD
       TZE WP5                TRA IF NO DIFFERENCE
       ARS 18
       LDQ IQSUB
       TSX WCD,4
WP5    CAL LINK+1,1
       ALS 1
       PBT                    TRA IF NOT DECREMENT
       TRA WP6
       LDQ IQALS
       PXD
       TSX WCD,4
       LDQ IQSTD
       CAL LINK+1,1
       ANA MQA
       TSX WCD,4
       TRA WP10
WP6    CAL LINK+1,1
       LDQ IQSTA
       PBT
       TRA WP8                TRA IF NOT PARAMETER
       LDQ IQSTO
WP8    ANA MQA
       TSX WCD,4
WP10   TIX WP7,1,2
       LDQ IQTRA2
       CLA AQ1
       STD LINK
       TSX WCD1,4
       LXD     FLAD,1        RESTOR FLAD POINTER
       LXA     FNFNRS,4
       TXI     *+1,4,1
       CLA     FNFLAD
       TXL     *+2,4,0
       CLA     FNFLAD+1
       SXA     FNFNRS,4
       STO     FLAD,1
       CLA     LOC
       STO     FLAD+1,1
       LDQ     IQAXT4
       PXD     0,0
       TSX     WCD,4
       LDQ     IQLXA2
       CLA     AB
       TSX     WCD,4
       LDQ     IQLXD1
       CLA     AB
       TSX     WCD,4
       LDQ     IQTRA4
       CLA     AQ1
       TSX     WCD1,4
       SXD     CODE,2
       CLA     FNSW
       TNZ     WP11          TRA IF AN INTERNAL FUNCTION
       CLA     LOC
       SUB     TAB7.9        GIVE THE LENGTH OF PROLOG
       ALS     18            THE '1' IS ALREADY TAKEN INTO ACCOUNT
       STD     TAB7.9
       TRA     MAD3A
       REM
WP11   CLA     FNR
       ORA PQ3
       LXD FLAD,1
       TXI *+1,1,2
       TXH OP7,1,PPFLAD       TRA IF FLAD TABLE EXCEEDED
       SXD FLAD,1
       SLW FLAD,1
       CLA LOC
       SUB AQ1
       STO FLAD+1,1
       CLA     TEM
       ARS 18
       ADD LOC                INCREASE LOC COUNTER TO
       STO LOC                ACCOUNT FOR TEMPORARY
       PXD
       STA FNSW
       CLA     WP9
       STO     TEM
       STZ     ADFNR
       TRA MAD3A              BLOCK.
WP9    PZE     PTEM,1
       REM
       REM     EXIT FROM MAD III - PART 1  (PR)
       REM
       REM
PRENDS LXA     CMST,1
       SXD     PR12,1
       SXD     PR14,1
       TXI     *+1,1,12
       SXA     PR10,1
PR10   AXT     **,1
       TXI     *+1,1,-2
PR12   TXL     PR15,1,**
       SXA     PR10,1
PR9    TXL     PR10,1,10
       CLA     PR10
       STA     PR11
       PXA     0,1
       PAX     0,2
       CAL     CMST+10,1
PR8    TXL     PR11,2,10
       LAS     CMST+10,2
       TRA     PR16
       TXI     PR8,2,-10
       TXI     PR8,2,-10
PR16   CLA     CMST+11,1
       LDQ     CMST+11,2
       STQ     CMST+11,1
       STO     CMST+11,2
       CAL     CMST+10,2
       LDQ     CMST+10,1
       STQ     CMST+10,2
       SLW     CMST+10,1
       TXI     PR8,2,-10
PR11   AXT     **,2
       TXI     *+1,2,-2
PR14   TXL     PR13,2,**
       SXA     PR11,2
       TRA     PR8
PR13   TXI     PR9,1,-10
PR15   LXA     SATEST,2
       CLA     MQD
       TXH     PR3,2,1
       PXD
       STD BW5
PR3    STD     BC3
       LXD     CODE,2
       AXT PPINST,1
       CLA AQEOF
       STO CODE,2
       STO CODE+1,2
       STO CODE+2,2
       TXI *+1,2,-3
       SXD CODE,2
       SXD BWT3,2
       CLA PR1
       STO BC28
       CLA BC27
       STO BC27-1
       TRA BC2
PR1    TMI BC15
PR2    CAL     PQ3
       LXD FLAD,1
       TXI *+1,1,2
       TXH OP7,1,PPFLAD       TRA IF FLAD TABLE EXCEEDED
       SXD FLAD,1
       SLW FLAD,1
       CLA LOC
       SUB AQ1
       STO FLAD+1,1
       STA     TAB7.6
       CLA     TEM
       STD     TAB7.6        SAVE FIRST,,N OF TEMPORARY FOR SINGLE PGM.
       ARS 18
       ADD LOC
       STO LOC
       CLA SIMPIO
       LXD FLAD,1
       TXI *+1,1,2
       TXH OP7,1,PPFLAD
       SXD FLAD,1
       PAI
       ANA     MQCT6
       STO FLAD,1
       CLA LOC
       STO SIMPIO
       STO     TAB7.8        SET SYMBOL TABLE STARTING ADDRESS
       RFT     300000
       TRA     PR4A
       TRA     PR4B          TAG=4, NO SYMBOL TABLE WANTED
PR4A   RNT     600000
       TRA     PR4T          TAG=5, PARTIAL SYMBOL TABLE WANTED
       ACL CMST
       ANA MQA
       ADD AQ4
PR4B   STO     FLAD+1,1
       ADD AQ1
       STO LOC
       SUB     TAB7.8        MINUS STARTING ADDRESS
       ALS     18            GIVES LENGTH
       STD     TAB7.8
       LXD     FLAD,3
       TXL MAD3B,1,0
       TRA     PR6
PR5    CLA     FLAD,2
       TLQ     PR7
       STO FLAD,1
       STQ FLAD,2
       CLA FLAD+1,1           SORT FLAD TABLE
       LDQ FLAD+1,2
       STO FLAD+1,2
       STQ FLAD+1,1           -SAVE FOR MAD3B-
PR6    LDQ     FLAD,1
PR7    TIX     PR5,2,2
       LXA AQ0,2                 ADTV
       TIX     PR6,3,2
       TRA     MAD3B
*
PR4T   LXA     CMST,2
       AXT     0,4           IR4 = NR LOCNS IN FINAL PARTIAL TABLE
PR4C   CAL     CMST+1,2
       ANA     PQ1
       TZE     *+2
       TXI     *+1,4,2
       TIX     PR4C,2,2
       PXA     0,4
       ADD     LOC
       TRA     PR4B
       REM
       REM CODE FOR THE ADDITION OF DEFINED OPERATIONS
       REM
INDEF  CLA IQTRAD
       STO ST12
       CLA     BCDS
       STO     BC34
       TRA MAD3A
DEFINE SLW     OP
       ARS 18
       STO DET1
       STZ     DET2
       LXA DEFOP,1
       CAL DEFOP,1
       ANA MQTA
       CAS DET1
       TRA *+2
       TRA DE1               SPECIAL OPERATION
       TIX *-5,1,1
       LXA OPTAB,1
       CAL OPTAB,1
       ANA MQTA
       CAS DET1
       TRA *+2
       TRA DE2               MACHINE OPERATION
       TIX *-5,1,1           MACHINE OPERATION
       CLA DET1
       TSX ERROR,4
       MZE     COM29,,10
DET1   PZE
DE2    LDQ OPTAB,1
       SLQ DET2              STORE MACHINE OP
       CAL     OP
       ANA     MQMTA
       SUB     COM39
       TNZ     DE19
       CLA     MQIND
       ORS     DET2
       TRA     DE18
DE19   CLA     DET2
       ANA     MQPSE
       SUB     IQPSE
       TNZ     DE17
       CLA     DET2
       LRS     22
       ALS     18
       LLS     4
       STO     DET2
       TRA     DE18
DE17   CLA     DET2
       ADD     PQ1
       TNZ     DE18
       CLA     MQT
       ORS     DET2
DE18   LDQ     AOPND
       TSX     OPND,4
       ALS     18
       ORS     DET2
       LDQ     BOPND
       TSX     OPND,4
       ORS     DET2
       CLA     OP
       ALS     9
       ANA     MQT
       ORA     DET2
DE14   SLW     TMTXF
       LXA     DE14,1
       TXI     *+1,1,1
       TXH     DE5,1,TMTX
       SXA     DE14,1
       SXD     ST36,1
       TRA MAD3A
DE5    TSX     ERROR,4
       MZE     COM30,,10
DET2   PZE
DE1    CLA DEFOP,1
       ARS 18
       STA *+1
       TRA **
DEMOD  LXD     INST,2
       TXI     *+1,2,-3
       SXD     INST,2
DECNV  TSX     OPMTL1,4
       TXH     DE22,1,0
       LXD     DE8,1
       TXI     *+1,1,-1
       TXL     DE5,1,0
       SXD     DE8,1
DE16   TXI     *+1,1,1
       CAS     OPMD-1,1
       TRA     DE22
       TSX     MADERR,4
       LDQ     OPMD-1,1
       STQ     OPMD,1
       TXL     DE16,1,PPOPMD-2
DE22   STO     OPMD,1
       ARS     21
       TSX     CADDR,4
       SLW     COM19+9
       TSX     PRLINE,4
       PZE     BCD-15,,15
       TSX     PRLINE,4
       PZE     COM19,,11
       LXD     INST,2
       CLA     INST+2,2
       ALS     3
       ORA     INST,2
       ALS     15
       ORS     OPMD,1
       CLA     DE14
       STA     OPMD,1
       TRA     MAD3A
DESAM  TSX     OPMTL1,4
       TXH     DE7,1,0
       TSX ERROR,4
       PZE COM31,,7
DE7    CLA     DE14
       ANA     MQA
       STO     DET1
       LXD     DE8,2
DE9    TXI     *+1,2,1
       CLA     OPMD,2
       ANA     MQA
       CAS     DET1
       TRA     *+2
       TRA     DE11
       TXL     DE9,2,PPOPMD-1
DEEND  CLA     IQLAS
       STO ST12
       CLA     IQTXH
       STO     BC34
       STZ     OP
       TRA MAD3A
DE11   CLA     OPMD,1
       STA     OPMD,2
       TRA     DE9
DEJMP  CLA OP
       ANA MQA
       LXA JCODE,1
       CAS JCODE,1
       TRA *+2
       TRA DE12
       TIX *-3,1,1
       TSX ERROR,4
       MZE     COM29,,10
DE12   CLA AOPND
       ALS     20
       SLW AOPND
       CLA BOPND
       ALS     20
       SLW BOPND
       CLA     BOPND
       ARS     20
       STO BOPND
       CLA     DE14
       ANA     MQA
       ALS     20
       ADD AOPND
       ARS     2
       STO AOPND
       PXD 0,1
       ARS 3
       ORA     DE14
       ANA     MQTA
       ORA AOPND
       ADD BOPND
       ORA DE13
       TRA DE14
DE13   MON
DEOUT  CAL BOPND
       LRS 18
       STO DET1
       LXA EXCODE,1
       CLA EXCODE,1
       ANA MQTA
       CAS DET1
       TRA *+2
       TRA DE15
       TIX     *-5,1,1
       CLA DET1
       TSX ERROR,4
       MZE     COM29,,10
DE15   CAL EXCODE,1
       ARS 18
       ORA     DE13
       TRA DE14
       REM                   OPERAND DECODING SUBROUTINE
OPND   SLQ     DET3
       STQ     DET5
       XCA
       ANA     MQT
       ALS     3
       ACL     DET3
       SLW     DET3
       LXA     ACODE,1
       CAL     ACODE,1
       ANA     MQPD
       LAS     DET3
       TRA     *+2
       TRA     DE3
       TIX     *-5,1,1
       CAL     MQMDP
       ANS     DET3
       LXA     LCODE,1
       CAL     LCODE,1
       ANA     MQPD
       LAS     DET3
       TRA     *+2
       TRA     DE4
       TIX     *-5,1,1
       CLA     DET3
       TSX     ERROR,4
       MZE     COM29,,10
DE3    CLA     DET5
       ANA     MQCON
       ORA     ACODE,1
       ANA     MQA
       TRA     1,4
DE4    CLA     LCODE,1
       STA     DE20
       LDQ     DET5
       LGL     6
       PXD     0,0
       LGL     12
       PAX     0,1
       LGL     2
       PXD     0,0
       LLS     15
       STO     DET3
DE20   CLA     **,1
       ANA     MQA
       ADD     DET3
       SUB     UPBRK
       TPL     DE6
       ADD     PLC
       TPL     DE10
       ADD     UPBRK
       TRA     1,4
DE6    ADD     UPBRK
       TRA     1,4
DE10   TSX     ERROR,4
       MZE     COM15,,6
DET3   PZE
DET5   PZE
       REM     OPMD BINARY SEARCH SUBROUTINE
OPMTL1 CLA     OP
       ANA     MQA
       ALS     3
       ORA     AOPND
       ALS     3
       ORA     BOPND
OPMTL  ALS     21
       STO     DET4
       SXA     DE24,4
       AXT     16,4
       AXT     PPOPMD-127,1
DE25   TXI     *+1,4,-2
DE8    TXL     DE28,1,OPMD-OPMDF
       CLA     OPMD,1
       ANA     MQMPD
       CAS     DET4
DE28   TRA     DE26,4
       TRA     DE27
       TRA     DE26+1,4
       TXI     DE25,1,64
       TXI     DE25,1,-64
       TXI     DE25,1,32
       TXI     DE25,1,-32
       TXI     DE25,1,16
       TXI     DE25,1,-16
       TXI     DE25,1,8
       TXI     DE25,1,-8
       TXI     DE25,1,4
       TXI     DE25,1,-4
       TXI     DE25,1,2
       TXI     DE25,1,-2
       TXI     DE25,1,1
       TXI     DE25,1,-1
DE26   NOP
       AXT     0,1
DE27   CLA     DET4
DE24   AXT     **,4
       TRA     1,4
DET4   PZE
       REM     SPECIAL DEFINED OPERATIONS TABLE
       VFD     18/DEMOD,H18/MOD
       VFD     18/DESAM,H18/SAM
       VFD     18/DEEND,H18/END
       VFD     18/DEJMP,H18/JMP
       VFD     18/DEOUT,H18/OUT
       VFD     18/DECNV,H18/CNV
DEFOP  PZE     6
       REM ADDRESS CODE TABLE FOR DEFINITIONS
       VFD     O18/216060,18/A
       VFD     O18/226060,18/B
       VFD     O18/636060,18/T
       VFD     O18/434623,18/LP
       VFD     O18/434624,18/LM
       VFD     O18/230160,18/C1
       VFD     O18/232660,18/CF
       VFD     O18/636560,18/TV
       VFD     O18/606060,18/0
       PZE
       VFD     O18/246360,18/DT
       VFD     O18/262660,18/FF
ACODE  PZE     12
       REM     LETTER CODE FOR LITERALS
       VFD     O6/26,30/TTB
       VFD     O6/42,30/CABC
       VFD     O6/65,30/CMST
LCODE  PZE     3
       REM     JUMP CODE FOR DEFINITIONS
       BCD 10000MQ
       BCD 10000AC
       BCD 10000BT
       BCD 10000AT
       BCD 1000000
       BCD 10000LA
JCODE  PZE     6
       REM     EXIT CODE FOR DEFINITIONS
       VFD     18/ACXT,O18/212360
       VFD     18/MQXT,O18/445060
       VFD     18/AACXT,O18/212123
       VFD     18/BACXT,O18/222123
       VFD     18/ZXT,O18/716060
       VFD     18/SQXT,O18/625060
       VFD     18/LACXT,O18/432123
       VFD     18/ACMQXT,O18/212350
EXCODE PZE     8
       EJECT
       REM     BUFFERED I/O ROUTINES
       HEAD    P
       REM
*
(W     SYN     0$(W
AQ0    SYN     0$AQ0
PQ4    SYN     0$PQ4
PQ1    SYN     0$PQ1
MQA    SYN     0$MQA
*
*
OPEN   TIA     0$H5
(BUFFR TIA     0$H16
FWAIT  TIA     0$H15
*
*
*BFOPEN.(STATUS,NAME1,NAME2,BUFF1(N),(BUFF2(N)),(BUFF3(N)),MODE,ERR)
*
*
*
BFOPEN SXA     XR1,1
       SXA     XR2,2
       SXA     XR4,4
       CLA     8,4
       STA     ERRET
       CAL*    7,4
       SLW     MODE
       LXA     TSPACE,1
       AXT     1,2     ERRCOD 1 IF NONE LEFT
       TXL     (ERR0,1,0      ERR=NO MORE ROOM IN TABLE
       CLA*    1,4
       STO     STATUS
       SUB     (W            IF STATUS CORRECT
       STO     READ          =0 FOR WRITE     =NON-0 FOR READ
       TZE     INIT
       CLA AQ0
INIT   STO     TABLE+TABSIZ+3,1  +FOR READ     -FOR WRITE
       STO     TABLE+TABSIZ+4,1
       STO     TABLE+TABSIZ+5,1
       CLA*    2,4
       STO     NAME1
       LDQ*    3,4
       STQ     NAME2
       TSX     GETFIL,4
       TRA     *+1
       STO     TABLE+TABSIZ+1,1
       STQ     TABLE+TABSIZ+2,1
       LXA     XR4,4
       AXT     2,2     ERRCOD 2 IF NOT ENOUGH BUFFERS GIVEN
       CLA*    4,4
       TPL     ARG4
       TZE     (ERR0          ERR=NO BUFFERS SPECIFIED
ARG4   CLA     4,4           BUFF1
       STA     TABLE+TABSIZ+3,1
       CLA*    5,4
       TPL     ARG5
       TNZ     ARG5
       NZT     READ
       TRA     (ERR0          ERR=NEED MORE BUFFS FOR WRITE
       TRA     OPN
ARG5   CLA     5,4
       NZT     READ
       TRA     ARG6
       STA     TABLE+TABSIZ+4,1  READ BUFF2
       TRA     OPN
ARG6   CLA*    6,4
       TPL     ARG7
       TNZ     ARG7
       CLA     5,4
       TRA     OPN-1
ARG7   CLA     5,4
       STA     TABLE+TABSIZ+4,1  WRITE BUFF2
       CLA     6,4
       STA     SYSBUF
OPN    TSX     OPEN,4
       TXH     STATUS
       TXH     NAME1
       TXH     NAME2
       TXH     MODE
       TXH     PQ4
       TXH     (ERR
       NZT     READ
       TRA     ASGNBF
*
FILLBF AXT     (EOF,4
       SXA     EOFRET,4
       STZ     BUFPTR        FILL BUFFER1
FILL1  CLA     TABLE+TABSIZ+3,1
       TSX     SYSIO,4
FILL2  CLA     TABLE+TABSIZ+4,1
       TZE     OPNRET
       STL     BUFPTR        FILL BUFFER 2
       TSX     SYSIO,4
       TRA     OPNRET
*
ASGNBF TSX     (BUFFR,4
       TXH     NAME1
       TXH     NAME2
SYSBUF TXH     **,,BUFSIZ
       TXH     (ERR
OPNRET TXI     *+1,1,-5
CMNXIT SXA     TSPACE,1
XR1    AXT     **,1
XR2    AXT     **,2
XR4    AXT     **,4
       TRA     1,4
*
*
(EOF   CLA     EOFCT
       TNZ     (EOF1
       AXT     BUFSIZ,4
       PXD     0,4
       NZT     BUFPTR
       STD     TABLE+TABSIZ+3,1      LAST WORD IN BUFFER 1
       ARS     18                     AND MARK EOF AT LAST
       TRA     (EOF1+2
(EOF1  ZET     BUFPTR
       ALS     18
       STO     TABLE+TABSIZ+5,1
       TSX     FWAIT,4       WAIT UNTIL FULL
       TXH     NAME1
       TXH     NAME2
       TXH     (ERR
       TRA     OPNRET
STATUS BSS     1
MODE   BSS     1
BSIZ   DEC     432     SIZE OF RECORD AND BUFFER
*
*
*
*BFREAD.ORBFWRIT.(NAME1,NAME2,BLOCK(N)...N,EOF,EOFCT,ERR)
*
*
*
RDFILE TIA     0$H6
WRFILE TIA     0$H7
RDWAIT TIA     0$H8
WRWAIT TIA     0$H9
BFREAD STL     READ
       TRA     SAVXRS
*
BFWRIT STZ     READ
*
SAVXRS SXA     XR1,1
       SXA     XR2,2
       SXA     XR4,4
       CLA     6,4
       STA     ERRET
       CLA*    1,4
       STO     NAME1
       LDQ*    2,4
       STQ     NAME2
       AXT     3,2     ERRCOD 3 IF FILE NOT OPEN
       TSX     GETFIL,4
       TRA     (ERR0          ERR= FILE N1 N2 NOT OPEN
       CLA     TABLE+TABSIZ+4,1  ARE WE IN BUFFER 2
       PDC     0,2
       SXA     BUFPTR,2
       TXH     *+3,2,0
       CLA     TABLE+TABSIZ+3,1
       PDC     0,2
       SCA     BUFFCT,2
       LXA     XR4,4
       AXT     4,2     ERRCOD 4
       NZT     READ
       TRA     TESTWR
TESTRD TMI     (ERR0          ERR=TRYING TO READ A WRITE FILE
       STA     (TAKE          BUFFER ADDR
       CLA     XR4
       STT     (TAKE
       CLA XR2
       STT     GIVE
       CLA     3,4
       STA     GIVE          BLOCK ADDR
       TRA    TESTWC
*
TESTWR TPL     (ERR0          ERR=TRYING TO WRITE A READ FILE
       STA     GIVE          BUFFER ADDR
       CLA     XR4
       STT     GIVE
       CLA XR2               TAKE FROM CALLER NO. WORDS COUNTED IN XR2
       STT     (TAKE
       CLA     3,4
       STA     (TAKE          BLOCK ADDR
*
TESTWC PDC     0,2           WORD COUNT
       ANA     PQ1
       TNZ     *+3           FAP CALL
       CLA     0,2           MAD CALL
       PAC     0,2
       TXL     RSTXRS,2,0    NO TRANSMISSION IF WC=0
       LAC     BUFFCT,4
       STZ     EOFCT
       CLA     TABLE+TABSIZ+5,1
       ZET     BUFPTR
       ARS     18
       ANA     MQA
       TZE     XCHNGE        NO EOFCT.BUFFER FULL.
       SBM     BUFFCT        ARE NO. OF WORDS LEFT IN BUFFER
       XCA
       PCA     0,2           WC
TSTEOF TLQ     MRKEOF
       TRA     XCHNGE        GRTR OR EQL TO NO. ASKED FOR
MRKEOF STQ     EOFCT         LESS THAN NO. ASKED FOR
       LAC     EOFCT,2       NEW WC
       TXH     XCHNGE,2,0
       SXD     EOFCT,1
       TRA     RETURN
XCHNGE SXD     GIVE+3,2
       AXT     0,2
       TXL     *+2,4,0
       TXL     CHNGBF,4,-BUFSIZ
(TAKE  CLA     **,0
GIVE   STO     **,0
       TXI     *+1,4,-1
       TXI     *+1,2,-1
       TXL     RETURN,2,**
       TXH     (TAKE,4,-BUFSIZ
CHNGBF CLA     TABLE+TABSIZ+3,1
       NZT     TABLE+TABSIZ+4,1
       TRA     BUFFIO        1 BUFFER-EMPTY(FILL) IT FOR REUSE
       NZT     BUFPTR        2 BUFFERS-EMPYT(FILL) THIS ONE
       CLA     TABLE+TABSIZ+4,1  WHILE USING OTHER
       ZET     READ
       STA     (TAKE          READ FROM NEW BUFF
       NZT     READ
       STA     GIVE          WRITE INTO NEW BUFF
       CLA     TABLE+TABSIZ+3,1
       NZT     BUFPTR
       TRA     *+3
       CLA     TABLE+TABSIZ+4,1
       AXT     0,4
       SXA     BUFPTR,4      POINT TO OTHER BUFFER
BUFFIO ZET     TABLE+TABSIZ+5,1  IF NO EOF HIT PREVIOUSLY
       TRA     HELP
       AXT     EOF1,4        EMPTY(FILL) BUFFER
       SXA     EOFRET,4
       TSX     SYSIO,4
CHGBCT AXT     0,4
       PXD     0,0
       STD     TABLE+TABSIZ+3,1
       STD     TABLE+TABSIZ+4,1
       TRA     (TAKE
*
HELP   SCA     WORDCT,2       NO. OF WORDS ALREADY TRANSMITTED
       CLA     TABLE+TABSIZ+5,1  EOFCT
       ZET     BUFPTR
       ARS     18
       ANA     MQA
       TZE     CHGBCT        EOF HIT IN OTHER BUFFER
       ADD     WORDCT
       XCA                   NO. OF WORDS WHICH CAN BE TRANSMITTED
       LDC     GIVE+3,4
       PXA     0,4           NO. OF WORDS WHICH SHOULD BE TRANSMITTED
       TLQ     *+2
       TRA     CHGBCT
       STQ     EOFCT
       LXA     EOFCT,4
       SCD     GIVE+3,4
       TRA     CHGBCT
*
*
EOF1   CLA     EOFCT
       TNZ     EOF2
       ZET     TABLE+TABSIZ+4,1
       TRA     EOF01
       SXD     EOFCT,1
       SCA     EOFCT,2
EOF01  AXT     BUFSIZ,4
       PXA     0,4
       PAC     0,4
       ZET     BUFPTR
       ALS     18
       STO     TABLE+TABSIZ+5,1
       NZT     TABLE+TABSIZ+4,1
       TRA     RETURN
       TRA     HELP
EOF2   TSX     FWAIT,4
       TXH     NAME1
       TXH     NAME2
       TXH     (ERR
EOF3   STZ     EOFCT
       NZT     TABLE+TABSIZ+4,1
       TRA     *+3
       NZT     BUFPTR        2 BUFFERS-EOF HIT WHILE TRANSM IN OTHER BUF
       ALS     18
       STO     TABLE+TABSIZ+5,1
       NZT     TABLE+TABSIZ+4,1
       TRA     HELP
       TRA     CHGBCT
*
*
RETURN PCD     0,4
       ZET     BUFPTR
       STD     TABLE+TABSIZ+4,1
       NZT     BUFPTR
       STD     TABLE+TABSIZ+3,1
RSTXRS LXA     XR1,1
       LXA     XR2,2
       LXA     XR4,4
       CLA     EOFCT
       TZE     1,4
       STA*    5,4
       TRA*    4,4
*
BUFFCT PZE     0
WORDCT PZE     0
*
*
*BFCODE.(0)
*
*
BFCODE CLA     ERRCOD
       STZ     ERRCOD
       TRA     1,4
ERRCOD PZE     0
*
*
*BFCLOS.(NAME1,NAME2,ERR)
*
*
*
CLOSE  TIA     0$H10
BFCLOS SXA     XR1,1
       SXA     XR2,2
       SXA     XR4,4
       CLA     3,4
       STA     ERRET
       STZ     READ
       CLA*    1,4
       LDQ*    2,4
       STL     ALL
       CAS     B3ALL
       TRA     *+2
       TRA     CLOSAL
       CAS     0$ALL
       TRA     *+2
       TRA     CLOSAL
CLOS1  TSX     GETFIL,4
       TRA     *+1            ERR=TRYING TO CLOSE A FILE NOT OPENED
N1     STO     NAME1
N2     STQ     NAME2
FLUSH1 AXT     (ERR,4
       SXA     EOFRET,4
       CLA     TABLE+TABSIZ+3,1
       TPL     CLOSIT
       PDX     0,4
       TXL     FLUSH2,4,0
       SXD     BUFADR,4      TRANSMIT ONLY BUFFCT WORDS
       TSX     SYSIO,4       WRITE BUFF 1
FLUSH2 CLA     TABLE+TABSIZ+4,1
       PDX     0,4
       TXL     CLOSIT,4,0
       SXD     BUFADR,4      TRANSMIT ONLY BUFFCT WORDS
       TSX     SYSIO,4       WRITE BUFF 2
CLOSIT TSX     CLOSE,4
       TXH     NAME1
       TXH     NAME2
       TXH     (ERR
       NZT     ALL
       TRA     GETNXT
       LXA     TSPACE,4
       TXI     *+1,4,5
       SXD     *+1,4
MOVEUP TXL     FINI,1,**
       CLA     TABLE+TABSIZ+5+1,1
       STO     TABLE+TABSIZ+1,1
       TXI     MOVEUP,1,-1
FINI   AXT     BUFSIZ,4
       SXD     BUFADR,4
       TRA     CMNXIT
CLOSAL TQP     CLOS1
       ZET*    2,4
       TRA     CLOS1
       STZ     ALL
       LXA     TSPACE,1
GETNXT TXH     FINI,1,TABSIZ-1
       TXI     *+1,1,5
       CLA     TABLE+TABSIZ+1,1
       LDQ     TABLE+TABSIZ+2,1
       TRA     N1
*
ALL    SYN     STATUS
B3ALL  BCI     1,   ALL
       TTL     INTERNAL SUBROUTINES
*
*
*      TSX     SYSIO,4       READ=0 FOR READ NON-0 FOR WRITE
*                            EOFRET MUST BE SET
*                            BUFFER ADDR MUST BE IN ADDR OF AC
*
SYSIO  SXA     IORTN,4
       STA     BUFADR
       AXC     0,4
       NZT     TABLE+TABSIZ+4,1
       AXC     2,4
       CLA     XMTFIL,4
       NZT     READ
       CLA     XMTFIL+1,4
       STA     *+1
IOSUBR TSX     **,4
       TXH     NAME1
       TXH     NAME2
       TXH     AQ0
BUFADR TXH     **,,BUFSIZ
EOFRET TXH     **
       TXH     EOFCT
       TXH     (ERR
IORTN  AXT     **,4
       TRA     1,4
*
(ERR0  SXA     ERRCOD,2
(ERR   LXA     XR1,1
       LXA     XR2,2
       LXA     XR4,4
ERRET  TRA     **
*
*
*      TSX     GETFIL,4      N1 N2 MUST BE IN ARITHM. AC AND MQ
*      TRA     1,4           N1 N2 NOT IN TABLE
*      TRA     2,4           N1 N2 IN TABLE
*                            C(XR1)-TABSIZ=PTR TO N1 N2 OR NEXT EMPTY
*                                   LOC IN TABLE
*
GETFIL LXA     TSPACE,1
       SXD     FINDN1,1
       AXT     TABSIZ,1
FINDN1 TXL     RTN2,1,**     IF MORE ENTRIES IN TABLE
       CAS     TABLE+TABSIZ+1,1  TRY TO FIND N1
       TRA     *+2
       TRA     FINDN2
       TIX     FINDN1,1,5
RTN2   TRA     1,4           N1 N2 NOT FOUND
FINDN2 XCA
       CAS     TABLE+TABSIZ+2,1
       TRA     *+2
       TRA     RTN1
       XCA
       TRA     RTN2-1
RTN1   XCA
       TRA     2,4           N1 N2 FOUND
*
*
TABSIZ EQU     5*10
BUFSIZ EQU     432
*
BUFPTR PZE     0
EOFCT  PZE     0
READ   PZE     0
XMTFIL PZE     RDFILE        1
       PZE     WRFILE        2
       PZE     RDWAIT        3
       PZE     WRWAIT        THESE 4 MUST BE IN ORDER
NAME1  BSS     1
NAME2  BSS     1
*
TSPACE BSS     0
TABLE  PZE     TABSIZ
       BSS     TABSIZ
       EJECT
       REM     TRANSLATED SEQUENCES
       REM
       HEAD    0
       REM
OPMD   BES     PPOPMD        2 ENTRIES, 0+OP+MODE A + MODE B
       PZE     PPOPMD,,OPMD-OPMDF
       REM                   OR  0+MODEB+MODEA+ENTRY
       REM                   OR  TXL+0+0+ENTRY
       REM
       REM
       REM     OPERATION-MODE TABLE
       REM
Z      EQU     0
F      EQU     0
X      EQU     1
BN     EQU     2
FN     EQU     3
ST     EQU     4
M5     EQU     5
M6     EQU     6
M7     EQU     7
       REM
CTXA   MACRO   OPN,MODEA,MODEB,MRB,MRA,ENTRY
       VFD     9/OPN,3/MODEA,3/MODEB,3/MRB,3/MRA,15/ENTRY
CTXA   END
CTXB   MACRO   OPN,MODEA,MODEB,ENTRY
       VFD     9/OPN,3/MODEA,3/MODEB,O6/70,15/ENTRY
CTXB   END
       REM
PLUS   EQU 1
MINUS  EQU 2
MPY    EQU 3
DIV    EQU 4
STOR   EQU 5
EQV    EQU 8
ABS    EQU 10
P      EQU 11
EXOR   EQU 12
E      EQU 13
NE     EQU 14
G      EQU 15
GE     EQU 16
L      EQU 17
LE     EQU 18
SUB    EQU 20
TAG    EQU 21
FCN    EQU 23
PAR    EQU 24
TRA    EQU 25
HALT   EQU 26
MTX2   EQU 27
DICT   EQU 28
DIF    EQU 29
DEF    EQU 30
FLD    EQU 31
PAR2   EQU 32
SAVLC  EQU 33
RSTLC  EQU 34
RTN    EQU 35
ERR    EQU 36
NEG    EQU 37
FEND   EQU 38
CONST  EQU 39
IFBGN  EQU 40
STA    EQU 41
IOPAR  EQU 46
BCDOP  EQU     47
GENZER EQU 48
ORA    EQU     49
EFETY  EQU 50
EFBGN  EQU 52
ANA    EQU     53
COM    EQU     54
LS     EQU     55
RS     EQU     56
GENONE EQU 57
STD    EQU 58
TAGA   EQU 59
IFETY  EQU 60
RTNB   EQU 62
TXL    EQU 63
FMT    EQU 64
NFCN   EQU 65
PREND  EQU     66
ERA    EQU     70
PRLG   BOOL    107
SRRTN  BOOL    110
       REM
       REM
       PMC     ON
       ORG OPMD-PPOPMD
       CTXA    PLUS,F,F,Z,F,FPLUS
       CTXA    PLUS,F,X,F,F,FLBS
       CTXA    PLUS,X,F,F,F,FLAS
       CTXA    PLUS,X,X,Z,X,XPLUS
       CTXA    MINUS,F,F,Z,F,FMINUS
       CTXA    MINUS,F,X,F,F,FLBS
       CTXA    MINUS,X,F,F,F,FLAS
       CTXA    MINUS,X,X,Z,X,XMINUS
       CTXA    MPY,F,F,Z,F,FMPY
       CTXA    MPY,F,X,F,F,FLBS
       CTXA    MPY,X,F,F,F,FLAS
       CTXA    MPY,X,X,Z,X,XMPY
       CTXA    DIV,F,F,Z,F,FDIV
       CTXA    DIV,F,X,F,F,FLBS
       CTXA    DIV,X,F,F,F,FLAS
       CTXA    DIV,X,X,Z,X,XDIV
       CTXA    STOR,F,F,Z,Z,STORS
       CTXA    STOR,F,X,F,F,FLBS
       CTXA    STOR,F,FN,Z,Z,STORS
       CTXA    STOR,X,F,X,X,FXBS
       CTXA    STOR,X,X,Z,Z,STORS
       CTXA    STOR,X,FN,Z,Z,STORS
       CTXA    STOR,BN,BN,Z,Z,STORS
       CTXA    STOR,FN,FN,Z,Z,STORS
       CTXA    STOR,ST,ST,Z,Z,STORS
       CTXA    EQV,BN,BN,Z,BN,BEQV
       CTXA    ABS,Z,F,Z,F,ABSS
       CTXA    ABS,Z,X,Z,X,ABSS
       CTXA    P,F,F,Z,F,EXPS
       CTXA    P,F,X,Z,F,EXPS
       CTXA    P,X,F,F,F,FLAS
       CTXA    P,X,X,Z,X,EXPS
       CTXA    EXOR,BN,BN,Z,BN,EXORS
       CTXA    E,BN,ST,Z,Z,ES
       CTXA    NE,BN,ST,Z,Z,NES
       CTXA    G,BN,ST,Z,Z,GS
       CTXA    GE,BN,ST,Z,Z,GES
       CTXA    L,BN,ST,Z,Z,LSQ
       CTXA    LE,BN,ST,Z,Z,LES
       CTXA    SUB,F,F,X,F,FXBS
       CTXA    SUB,F,X,Z,F,SUBS
       CTXA    SUB,X,F,X,X,FXBS
       CTXA    SUB,X,X,Z,X,SUBS
       CTXA    SUB,BN,X,Z,BN,SUBS
       CTXA    SUB,FN,X,Z,FN,SUBS
       CTXA    SUB,ST,X,Z,ST,SUBS
       CTXA    TAG,Z,F,X,Z,FXBS
       CTXA    TAG,Z,X,Z,Z,TAGS
       CTXA    FCN,Z,F,Z,F,FCNS
       CTXA    FCN,Z,X,Z,X,FCNS
       CTXA    FCN,Z,BN,Z,BN,FCNS
       CTXA    FCN,Z,FN,Z,FN,FCNS
       CTXA    FCN,Z,ST,Z,ST,FCNS
       CTXA    FCN,Z,M5,Z,M5,FCNS
       CTXA    FCN,Z,M6,Z,M6,FCNS
       CTXA    FCN,Z,M7,Z,M7,FCNS
       CTXA    PAR,Z,F,Z,Z,PARS
       CTXA    PAR,Z,X,Z,Z,PARS
       CTXA    PAR,Z,BN,Z,Z,PARS
       CTXA    PAR,Z,FN,Z,Z,PARS
       CTXA    PAR,Z,ST,Z,Z,PARS
       CTXA    PAR,Z,M5,Z,Z,PARS
       CTXA    PAR,Z,M6,Z,Z,PARS
       CTXA    PAR,Z,M7,Z,Z,PARS
       CTXA    TRA,Z,ST,Z,Z,TRAS
       CTXA    HALT,Z,Z,Z,Z,HALTS
       CTXA    HALT,Z,ST,Z,Z,HALTS
       CTXA    MTX2,X,Z,Z,X,MTX2S
       CTXA    MTX2,X,X,Z,X,MTX2S
       CTXB    DICT,Z,ST,DICTS
       CTXA    DIF,F,F,Z,BN,XMINUS
       CTXA    DIF,F,X,F,F,FLBS
       CTXA    DIF,X,F,F,F,FLAS
       CTXA    DIF,X,X,Z,BN,XMINUS
       CTXA    DIF,BN,BN,Z,BN,XMINUS
       CTXA    DIF,FN,FN,Z,BN,XMINUS
       CTXA    DIF,ST,ST,Z,BN,XMINUS
       CTXB    FLD,Z,ST,FLDS
       CTXA    PAR2,F,F,Z,Z,PAR2S
       CTXA    PAR2,Z,X,Z,Z,PAR2S
       CTXA    PAR2,Z,BN,Z,Z,PAR2S
       CTXA    PAR2,X,Z,Z,Z,PAR2S
       CTXA    PAR2,X,X,Z,Z,PAR2S
       CTXA    PAR2,BN,Z,Z,Z,PAR2S
       CTXA    PAR2,BN,BN,Z,Z,PAR2S
       CTXA    PAR2,M5,M5,Z,Z,PAR2S
       CTXA    PAR2,M6,M6,Z,Z,PAR2S
       CTXA    PAR2,M7,M7,Z,Z,PAR2S
       CTXB    SAVLC,Z,F,SAVLCS
       CTXB    SAVLC,Z,X,SAVLCS
       CTXB    SAVLC,Z,BN,SAVLCS
       CTXB    SAVLC,Z,FN,SAVLCS
       CTXB    SAVLC,Z,ST,SAVLCS
       CTXB    RSTLC,Z,Z,RSTLCS
       CTXA    RTN,ST,Z,Z,Z,RTNS
       CTXA    ERR,ST,Z,Z,Z,ERRS
       CTXA    NEG,Z,F,Z,F,NEGS
       CTXA    NEG,Z,X,Z,X,NEGS
       CTXB    FEND,Z,ST,FENDS
       CTXB    IFBGN,Z,Z,IFBGNS
       CTXA    STA,ST,X,Z,Z,STAS
       CTXA    IOPAR,F,F,Z,Z,IOPARS
       CTXA    IOPAR,Z,X,Z,Z,IOPARS
       CTXA    IOPAR,Z,BN,Z,Z,IOPARS
       CTXA    IOPAR,Z,FN,Z,Z,IOPARS
       CTXA    IOPAR,Z,ST,Z,Z,IOPARS
       CTXA    IOPAR,X,Z,Z,Z,IOPARS
       CTXA    IOPAR,X,X,Z,Z,IOPARS
       CTXA    IOPAR,BN,Z,Z,Z,IOPARS
       CTXA    IOPAR,BN,BN,Z,Z,IOPARS
       CTXA    IOPAR,FN,Z,Z,Z,IOPARS
       CTXA    IOPAR,M5,M5,Z,Z,IOPARS
       CTXA    IOPAR,M6,M6,Z,Z,IOPARS
       CTXA    IOPAR,M7,M7,Z,Z,IOPARS
       CTXA    GENZER,Z,Z,Z,BN,GENZS
       CTXA    ORA,X,X,Z,X,ORAS
       CTXB    EFETY,Z,ST,EFETYS
       CTXB    EFBGN,Z,Z,EFBGNS
       CTXA    ANA,X,X,Z,X,ANAS
       CTXA    COM,Z,X,Z,X,COMS
       CTXA    LS,X,X,Z,X,LSS
       CTXA    RS,X,X,Z,X,RSS
       CTXA    GENONE,Z,Z,Z,BN,GENOS
       CTXA    STD,ST,X,Z,Z,STDS
       CTXA    TAGA,F,F,X,F,FXBS
       CTXA    TAGA,F,X,Z,X,TAGAS
       CTXA    TAGA,X,F,X,X,FXBS
       CTXA    TAGA,X,X,Z,X,TAGAS
       CTXA    TAGA,BN,X,Z,X,TAGAS
       CTXA    TAGA,FN,X,Z,X,TAGAS
       CTXA    TAGA,ST,X,Z,X,TAGAS
       CTXB    IFETY,Z,FN,IFETYS
       CTXA    RTNB,ST,F,Z,Z,RTNBS
       CTXA    RTNB,ST,X,Z,Z,RTNBS
       CTXA    RTNB,ST,BN,Z,Z,RTNBS
       CTXA    RTNB,ST,FN,Z,Z,RTNBS
       CTXA    TXL,Z,FN,Z,Z,TXLS
       CTXA    TXL,Z,ST,Z,Z,TXLS
       CTXA    FMT,ST,Z,Z,Z,FMTS
       CTXA    FMT,ST,X,Z,Z,FMTS
       CTXA    NFCN,Z,FN,Z,FN,NFCNS
       CTXA    NFCN,Z,ST,Z,Z,NFCNS
       CTXB    PREND,Z,Z,PRENDS
       CTXA    ERA,X,X,Z,X,ERAS
       CTXA    PRLG,Z,ST,Z,Z,PRLGS
       CTXA    SRRTN,Z,ST,Z,Z,SRRTNS
OPMDF  PZE
       ORG     OPMD+1
OPM1   CTXA    MTX2,X,Z,Z,X,MTX2S
       REM
       REM TRANSLATED SEQUENCES
       REM
TST    OPSYN MON
       REM
LAC    EQU     1
TFR    EQU     2
AT     EQU     3
BT     EQU     4
AC     EQU     5
MQ     EQU     6
       REM
A      EQU     1*256
B      EQU     2*256
T      EQU     3*256
LP     EQU     4*256
BTA    EQU     5*256
C1     EQU     6*256
CF     EQU     7*256
TV     EQU     8*256
ZTA    EQU     9*256
DT     EQU     10*256
LM     EQU     11*256
FF     EQU     12*256
XPLUS  TST     *+1,LAC,*+3
       SLW     T
       TST     *+15,TFR
       TST     *+2,MQ,*+1
       TST     *+3,AC,*+13
       TST     *+4,AT,*+1
       TST     *+5,BT,*+8
       TST     *+11,AT,*+1
       TST     *+4,BT,*+8
       XCA
       TST     *+8,TFR
       XCA
       ADD     A
       TST     ACXT
       STQ     T
       TST     *+2,TFR
       STO     T
       CLA     A
       ADD     B
       TST     ACXT
FPLUS  TST     *+1,LAC,*+3
       SLW   T
       TST     *+15,TFR
       TST   *+1,MQ,*+3
       TST    *+5,AT,*+1
       TST    *+6,BT,*+9
       TST    *+1,AC,*+11
       TST    *+11,AT,*+1
       TST    *+4,BT,*+8
       XCA
       TST    *+8,TFR
       XCA
       FAD    A
       TST    ACXT
       STQ    T
       TST    *+2,TFR
       STO    T
       CLA    A
       FAD    B
       TST    ACXT
XMINUS TST     *+1,LAC,*+3
       SLW     T
       TST     *+16,TFR
       TST     *+2,MQ,*+1
       TST     *+3,AC,*+14
       TST     *+4,AT,*+1
       TST     *+5,BT,*+9
       TST     *+12,AT,*+1
       TST     *+4,BT,*+9
       XCA
       TST     *+9,TFR
       XCA
       CHS
       ADD     A
       TST     ACXT
       STQ     T
       TST     *+2,TFR
       STO     T
       CLA     A
       SUB     B
       TST     ACXT
FMINUS TST     *+1,LAC,*+3
       SLW   T
       TST     *+16,TFR
       TST   *+1,MQ,*+3
       TST    *+5,AT,*+1
       TST    *+6,BT,*+10
       TST    *+1,AC,*+12
       TST    *+12,AT,*+1
       TST    *+4,BT,*+9
       XCA
       TST    *+9,TFR
       XCA
       CHS
       FAD    A
       TST    ACXT
       STQ    T
       TST    *+2,TFR
       STO    T
       CLA    A
       FSB    B
       TST    ACXT
XMPY   TST     *+1,LAC,*+3
       SLW   T
       TST     *+15,TFR
       TST   *+2,AC,*+1
       TST     *+3,MQ,*+13
       TST     *+4,AT,*+1
       TST     *+5,BT,*+8
       TST     *+11,AT,*+1
       TST     *+4,BT,*+8
       XCA
       TST     *+8,TFR
       XCA
       MPY     A
       TST     MQXT
       STO     T
       TST     *+2,TFR
       STQ     T
       LDQ     A
       MPY     B
       TST     MQXT
FMPY   TST     *+1,LAC,*+3
       SLW   T
       TST     *+15,TFR
       TST   *+1,AC,*+3
       TST    *+5,AT,*+1
       TST    *+6,BT,*+9
       TST    *+1,MQ,*+11
       TST    *+11,AT,*+1
       TST    *+4,BT,*+8
       XCA
       TST    *+8,TFR
       XCA
       FMP    A
       TST    ACXT
       STO    T
       TST    *+2,TFR
       STQ    T
       LDQ    A
       FMP    B
       TST    ACXT
XDIV   TST     *+1,LAC,*+3
       SLW     T
       TST     *+10,TFR
       TST     *+3,AT,*+1
       TST     *+7,AC,*+1
       TST     *+4,MQ,*+7
       TST     *+1,AC,*+7
       LRS     35
       TST     *+7,TFR
       STQ     T
       TST     *+2,TFR
       STO     T
       LDQ     A
       PXD
       LLS     0
       DVP     B
       TST     MQXT
FDIV   TST     *+1,LAC,*+3
       SLW   T
       TST     *+10,TFR
       TST   *+1,MQ,*+2
       TST     *+3,AT,*+7
       TST     *+1,AC,*+7
       TST     *+7,AT,*+3
       XCA
       TST     *+5,TFR
       STO     T
       TST     *+2,TFR
       STQ     T
       CLA     A
       FDP     B
       TST     MQXT
ABSS   TST     *+1,LAC,*+3
       SLW   T
       TST     *+10,TFR
       TST   *+1,AC,*+2
       TST     *+9,BT,*+7
       TST     *+1,MQ,*+7
       TST     *+3,BT,*+1
       STQ     T
       TST     *+4,TFR
       XCA
       TST     *+3,TFR
       STO     T
       CLA     B
       SSP
       TST     ACXT
NEGS   TST     *+1,LAC,*+3
       SLW   T
       TST     *+11,TFR
       TST   *+1,AC,*+2
       TST     *+6,BT,*+8
       TST     *+1,MQ,*+8
       TST     *+3,BT,*+1
       STQ     T
       TST     *+5,TFR
       XCA
       CHS
       TST     ACXT
       STO     T
       CLS     B
       TST     ACXT
EXPS   TST    *+1,AC,*+3
       TST    *+7,AT,*+1
       TST    *+9,BT,*+4
       TST    *+1,MQ,*+4
       TST    *+10,AT,*+1
       TST    *+7,BT,*+11
       STO    T
       CLA    A
       LDQ    B
       TSX    TV,4
       TST    ACXT
       XCA
       CLA    A
       TST    *-4,TFR
       XCA
       TST    *-7,TFR
       STQ    T
       TST    *-10,TFR
STORS  TST     *+1,BT,*+3
       TST     *+3,AC,*+1
       TST     *+4,LAC,*+6
       CLA     B
       STO     A
       TST     ZXT
       SLW     A
       TST     ZXT
       STQ     A
       TST     ZXT
BEQV   TST    *+1,AC,*+3
       TST    *+14,AT,*+1
       TST    *+9,BT,*+11
       TST    *+1,MQ,*+11
       TST    *+4,AT,*+1
       TST    *+5,BT,*+1
       STQ    T
       TST    *+7,TFR
       XCA
       TST    *+6,TFR
       XCA
       ADD    A
       TST    *+4,TFR
       STO    T
       CLA    A
       ADD    B
       SUB    C1
       SSP
       TST    ACXT
EXORS  TST    *+1,AC,*+3
       TST    *+14,AT,*+1
       TST    *+9,BT,*+4
       TST    *+1,MQ,*+11
       TST    *+4,AT,*+1
       TST    *+5,BT,*+8
       STO    T
       TST    *+7,TFR
       XCA
       TST    *+6,TFR
       XCA
       ERA    A
       TST    ACXT
       STQ    T
       CLA    A
       ERA    B
       TST    ACXT
GENZS  PXD 0,0
       TST ACXT
GENOS  CLA C1
       TST ACXT
ES     TST    *+1,AT,*+3
       TST    *+6,AC,*+1
       TST    *+4,MQ,*+1
       NZT    A
       TRA    B
       TST    ZXT
       XCA
       TZE    B
       TST    ZXT
NES    TST    *+1,AT,*+6
       TST    *+3,AC,*+1
       TST    *+1,MQ,*+4
       XCA
       TNZ    B
       TST    ZXT
       ZET    A
       TRA    B
       TST    ZXT
GS     TST    *+1,AC,*+2
       TST    *+5,AT,*+3
       TST    *+1,MQ,*+3
       TST    *+6,AT,*+8
       STO    T
       CLA    A
       TZE    LP+2
       TPL    B
       TST    ZXT
       XCA
       TST    *-4,TFR
       STQ    T
       TST    *-7,TFR
GES    TST    *+1,AC,*+2
       TST    *+5,AT,*+3
       TST    *+1,MQ,*+3
       TST    *+6,AT,*+8
       STO    T
       CLA    A
       TZE    B
       TPL    B
       TST    ZXT
       XCA
       TST    *-4,TFR
       STQ    T
       TST    *-7,TFR
LSQ    TST    *+1,AC,*+2
       TST    *+5,AT,*+3
       TST    *+1,MQ,*+3
       TST    *+6,AT,*+8
       STO    T
       CLA    A
       TZE    LP+2
       TMI    B
       TST    ZXT
       XCA
       TST    *-4,TFR
       STQ    T
       TST    *-7,TFR
LES    TST    *+1,AC,*+2
       TST    *+5,AT,*+3
       TST    *+1,MQ,*+3
       TST    *+6,AT,*+8
       STO    T
       CLA    A
       TZE    B
       TMI    B
       TST    ZXT
       XCA
       TST    *-4,TFR
       STQ    T
       TST    *-7,TFR
FLAS   TST     *+1,LAC,*+3
       SLW    T
       TST     *+10,TFR
       TST    *+1,AC,*+2
       TST    *+9,AT,*+7
       TST    *+1,MQ,*+7
       TST    *+3,AT,*+1
       STQ    T
       TST    *+4,TFR
       XCA
       TST    *+3,TFR
       STO    T
       CLA    A
       ORA    CF
       FAD    CF
       TST    AACXT
FLBS   TST     *+1,LAC,*+3
       SLW    T
       TST     *+10,TFR
       TST    *+1,AC,*+2
       TST    *+9,BT,*+7
       TST    *+1,MQ,*+7
       TST    *+3,BT,*+1
       STQ    T
       TST    *+4,TFR
       XCA
       TST    *+3,TFR
       STO    T
       CLA    B
       ORA    CF
       FAD    CF
       TST    BACXT
FXBS   TST     *+1,LAC,*+3
       SLW    T
       TST     *+10,TFR
       TST    *+1,AC,*+2
       TST    *+9,BT,*+7
       TST    *+1,MQ,*+7
       TST    *+3,BT,*+1
       STQ    T
       TST    *+4,TFR
       XCA
       TST    *+3,TFR
       STO    T
       CLA    B
       UFA    CF
       ERA    CF
       LLS    0
       TST    BACXT
TAGS   TST     *+1,BT,*+9
       TST     *+6,AC,*+1
       TST     *+4,MQ,*+1
       TST     *+4,LAC,*+6
       LXA     BTA,0
       TST     ZXT
       XCA
       PAX     ZTA,0
       TST     ZXT
       LXA     BTA,0
       TST     SQXT
SUBS   TST     *+1,LAC,*+2
       TST     *+6,BT,*+14
       TST     *+1,AC,*+2
       TST     *+4,BT,*+10
       TST     *+1,MQ,*+6
       TST     *+1,BT,*+4
       XCA
       PAX     0,1
       TST     *+3,TFR
       STQ     T
       LXA     B,1
       CLA     A,1
       TST     ACXT
       STO     T
       TST     *-4,TFR
       SLW     T
       TST     *-6,TFR
TAGAS  TST     *+1,LAC,*+2
       TST     *+11,BT,*+14
       TST     *+1,AC,*+2
       TST     *+9,BT,*+7
       TST     *+1,MQ,*+7
       TST     *+1,BT,*+3
       XCA
       TST     *+5,TFR
       STQ     T
       TST     *+2,TFR
       STO     T
       CLA     B
       SUB     LP+1
       TXH     A
       TST     ACXT
       SLW     T
       TST     *-5,TFR
STAS   TST    *+1,AC,*+2
       TST    *+9,BT,*+7
       TST    *+1,MQ,*+7
       TST    *+1,BT,*+3
       XCA
       TST    *+5,TFR
       STQ    T
       TST    *+2,TFR
       STO    T
       CLA    B
       STA    A
       TST    ZXT
STDS   TST    *+1,AC,*+2
       TST    *+9,BT,*+7
       TST    *+1,MQ,*+7
       TST    *+1,BT,*+3
       XCA
       TST    *+5,TFR
       STQ    T
       TST    *+2,TFR
       STO    T
       CLA    B
       ALS    18
       STD    A
       TST    ZXT
TRAS   TRA B
       TST ZXT
TXLS   TXL B
       TST ZXT
HALTS  TSX     TV,4
       TXH B
       TST ZXT
FCNS   TST     *+1,LAC,*+2    CHANGED 12/64 (FERREN MCINTYRE) (*)
       SLW     T                            *
       TST     *+1,AC,*+6                   *
       TST     *+1,MQ,*+4                   *
       STO     DT                           *
       STQ     DT+1                         *
       TST     *+4,TFR                      *
       STO     T                            *
       TST     *+1,MQ,*+2                   *
       STQ     T                            *
       TSX     BTA,0
       TST     ACXT
PARS   TXH B,0,A
       TST SQXT
NFCNS  TSX    BTA,0
       TST ZXT
PRLGS  SXA     B,2
       TSX     B,2
       TST     ZXT
PAR2S  TIX B,0,A
       TST SQXT
RTNBS  TST     *+1,LAC,*+2
       SLW     T
       TST     *+1,MQ,*+3
       XCA
       TST     *+3,TFR
       TST     *+2,AC,*+1
       CLA     B
RTNS   TRA     FF
       TST     ACXT
ERRS   LXA     FF,4
       CLA B,4
       STA LP+2
       LDQ LP+2
       TLQ    0
       TXH    0
       TST ZXT
IOPARS TST     *+2,BT,*+1
       TST     *+1,AT,*+7
       TST     *+1,LAC,*+2
       SLW     T
       TST     *+1,AC,*+2
       STO     T
       TST     *+1,MQ,*+2
       STQ     T
       STR     B,7,A
       TST     ZXT
FMTS   TXI    A,1,B
       TST    STRXT
MTX2S  TST     *+1,MQ,*+9     (CHANGED 12/64) FROM ARDEN VERSION
       TST     *+1,AC,*+4
       STO     DT
       STQ     DT+1
       TST     *+15,TFR
       TST     *+1,LAC,*+7
       SLW     DT
       STQ     DT+1
       TST     *+11,TFR
       TST     *+1,LAC,*+3
       SLW     T
       TST     *+8,TFR
       TST     *+1,MQ,*+3
       TST     *+7,BT,*+1
       TST     *+9,AT,*+4
       TST     *+1,AC,*+4
       TST     *+8,AT,*+1
       TST     *+9,BT,*+11
       STQ     T
       LDQ     B
       CLA     A
       TSX     TV,4
       TST     ACXT
       XCA
       LDQ     B
       TST     *-4,TFR
       XCA
       TST     *-7,TFR
       STO     T
       TST     *-10,TFR
ANAS   TST    *+19,AC,*+1
       TST    *+1,LAC,*+3
       TST    *+7,AT,*+1
       TST    *+8,BT,*+10
       TST    *+1,MQ,*+4
       TST    *+10,AT,*+1
       TST    *+11,BT,*+1
       STQ    T
       CAL    A
       ANA    B
       TST    LACXT
       ANA    A
       TST    LACXT
       SLW    T
       TST    *-6,TFR
       XCL
       TST    *-7,TFR
       XCL
       TST    *-7,TFR
       STO    T
       TST    *-12,TFR
ORAS   TST    *+19,AC,*+1
       TST    *+1,LAC,*+3
       TST    *+7,AT,*+1
       TST    *+8,BT,*+10
       TST    *+1,MQ,*+4
       TST    *+10,AT,*+1
       TST    *+11,BT,*+1
       STQ    T
       CAL    A
       ORA    B
       TST    LACXT
       ORA    A
       TST    LACXT
       SLW    T
       TST    *-6,TFR
       XCL
       TST    *-7,TFR
       XCL
       TST    *-7,TFR
       STO    T
       TST    *-12,TFR
COMS   TST    *+11,AC,*+1
       TST    *+1,LAC,*+2
       TST     *+5,BT,*+11
       TST    *+1,MQ,*+3
       TST    *+5,BT,*+1
       STQ    T
       CAL    B
       COM
       TST    LACXT
       XCL
       TST    *-3,TFR
       STO     T
       TST    *-6,TFR
       SLW     T
       TST     *-8,TFR
RSS    TST     *+1,BT,*+7
       TST     *+1,MQ,*+2
       XCA
       STA     LP+2
       CAL     A
       LGR     0
       TST     LACXT
       TST     *+1,AC,*+2
       STO     T
       TST     *+1,MQ,*+2
       STQ     T
       TST     *+1,LAC,*+2
       SLW     T
       CAL     B
       STA     LP+2
       CAL     A
       LGR     0
       TST     LACXT
LSS    TST     *+1,BT,*+7
       TST     *+1,MQ,*+2
       XCA
       STA     LP+2
       CAL     A
       ALS     0
       TST     LACXT
       TST     *+1,AC,*+2
       STO     T
       TST     *+1,MQ,*+2
       STQ     T
       TST     *+1,LAC,*+2
       SLW     T
       CAL     B
       STA     LP+2
       CAL     A
       ALS     0
       TST     LACXT
ERAS   TST     *+7,AC,*+1
       TST     *+1,MQ,*+3
       TST     *+9,AT,*+1
       TST     *+10,BT,*+13
       TST     *+1,LAC,*+4
       TST     *+4,AT,*+1
       TST     *+8,BT,*+12
       STO     T
       CAL     A
       ERA     B
       TST     LACXT
       XCL
       TST     *-3,TFR
       XCL
       ERA     A
       TST     LACXT
       STQ     T
       TST     *-9,TFR
       SLW     T
       TST     *-11,TFR
SRRTNS STR     FF,7
       TST     ZXT
TMTXF  PZE
EOF    SYN     EOF.
BOT    SYN     *
       END     PASS1
