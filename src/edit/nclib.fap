*
       TTL     NCLIB --- SUBROUTINE ENTRIES TO AVOID LOADING LIBRARY
       REM
       LINK    ON
       PCC     ON
       PMC     ON
       REM
       ENTRY   CHNCOM        EXIT TO CTSS
       ENTRY   COMARG        GET PARAMETER FROM COMMAND LIST
       ENTRY   SETBCD        SET CONSOLE IN 6-BIT MODE
       ENTRY   SETFUL        SET CONSOLE IN 12-BIT MODE
       ENTRY   SETBRK        SET CONSOLE INTERRUPT PROCEDURE
       ENTRY   WHOAMI        DETERMINE THE USER
       ENTRY   RED           CHANGE CONSOLE TO RED
       ENTRY   BLACK         CHANGE CONSOLE TO BLACK
       REM
TSSX   MACRO   A             CALL CORE A
       AXC     *+1,4         .
       TIA     =H'A          ..
TSSX   END                   ..






EXIT   CAL*    1,4           EXIT TO CTSS
       STA     CHNARG
       AXC     *+1,4         EXIT TO NEXT COMMAND
       TIA     =HCHNCOM      .. OR GO DEAD OR DORMANT
CHNARG PZE     **            ..
       REM
CHNCOM SYN     EXIT          CHNCOM SAME AS EXIT
       REM
SETBCD TIA     =HSETBCD      SET CONSOLE IN 6-BIT MODE
       REM
SETFUL TIA     =HSETFUL      SET CONSOLE IN 12-BIT MODE
       REM
SETBRK TIA     =HSETBRK      SET CONSOLE INTERRUPT PROCEDURE
       REM
WHOAMI TIA     =HWHOAMI      DETERMINE THE USER
       REM
RED    SXA     X4,4          COLOR SHIFT TO RED
       TSX     SETFUL,4      SET CONSOLE IN 12-BIT MODE
       TSSX    WRFLXA        GIVE RED COLOR SHIFT
               REDS,,1       ..
       TSX     SETBCD,4      RETURN CONSOLE TO 6-BIT MODE
       TRA     X4            RETURN
       REM
BLACK  SXA     X4,4          COLOR SHIFT TO BLACK
       TSX     SETFUL,4      SET CONSOLE IN 12-BIT MODE
       TSSX    WRFLXA        GIVE BLACK COLOR SHIFT
               BLACKS,,1     ..
       TSX     SETBCD,4      RETURN CONSOLE TO 6-BIT MODE
       TRA     X4            RETURN
       REM
COMARG SXA     X4,4          GET PARAMETER FROM COMMAND LIST
       CAL*    1,4           NUMBER OF DESIRED PARAMETER
       SLW     CARGNO        ..
       TSSX    GETCOM        PICK UP ARGUMENT
CARGNO PZE     **            ..
       XCL                   LOGICAL AC TO MQ
       XCA                   RETURN TO ARITH. AC
X4     AXT     **,4          RETURN TO PROGRAM
       TRA     1,4           ..
       REM
REDS   OCT     013300570057  COLOR SHIFT(RED), NULL, NULL
BLACKS OCT     013200570057  COLOR SHIFT(BLACK), NULL, NULL
       TTL     LITERALS AND CROSS-REFERENCE LISTING FOR NCLIB
       REM
       END
