          R..  MAIN PROGRAM TO CALL RUNOFF COMMAND.
          R
          R..  J. H. SALTZER, DECEMBER, 1965.
          R
          R..  DECLARATIONS.
          R
           NORMAL MODE IS INTEGER
           INSERT FILE RCOM
           VECTOR VALUES FENCE = 777777777777K
          R
          R..  DEFAULT ARGUMENT VALUES
          R
           VECTOR VALUES NOSTOP = 1B
           VECTOR VALUES NOWAIT = 0B
           VECTOR VALUES FSTPAG = 0
           VECTOR VALUES OUTFIL = 0B
          R
          R..  COLLECT COMMAND ARGUMENTS.  FIRST ARGUMENT IS
          R..  NAME OF FILE TO BE READ.
          R
           SETBRK.(CHNCOM.)
           PRINAM = COMARG.(1)
           WHENEVER PRINAM .E. FENCE
BADARG          PRINT COMMENT $0U1S1A1G1E0:$
                PRINT COMMENT $0 0 0 0 0 0R0U0N0O0F0F0 1N1A1M1E0 0-0P0A0
          1G0E0 1N0-0 0-0S0T0O0P0-$
                CHNCOM.(0)
           END OF CONDITIONAL
          R
           THROUGH GETARG, FOR I = 2, 1, COMARG.(I) .E. FENCE
          1                              .OR. I .G. 20
                ARG = COMARG.(I)
          R
                WHENEVER ARG .E. $  STOP$
                     NOSTOP = 0B
          R
                OR WHENEVER ARG .E. $NOWAIT$
                     NOWAIT = 1B
                     NOSTOP = 1B
          R
                OR WHENEVER ARG .E. $  PAGE$
                     ARG = COMARG.(I+1)
                     WHENEVER ARG .E. FENCE, TRANSFER TO BADARG
                     I = I + 1
                     FSTPAG = BIN.(ARG)
          R
                OR WHENEVER ARG .E. $ PRINT$
                     OUTFIL = 1B
                     NOWAIT = 1B
          R
                OR WHENEVER ARG .E. $  BALL$
                     ARG = COMARG.(I+1)
                     WHENEVER ARG .E. FENCE, TRANSFER TO BADARG
                     I = I + 1
                     CHBALL.(ARG,2,BADARG)
          R
                OTHERWISE
                     TRANSFER TO BADARG
          R
GETARG          END OF CONDITIONAL
          R
           RUNOFF.
           CHNCOM.(0)
          R
           END OF PROGRAM
