*      SUBROUTINE TO CONVERT BCD NUMBER TO BINARY.
*
*        J. H. SALTZER, MAY, 1966.
       LINK    ON
       ENTRY   BIN           CONVERTER.
 BIN   SXA     IRR1,1
       STZ     INT           CLEAR RESULT.
       AXT     6,1
       LDQ*    1,4           GET ARGUMENT.
LOP    ZAC
       LGL     6             GET NEXT CHARACTER.
       LAS     =10           SKIP OVER NON-DIGITS.
       TRA     TIX
       TRA     TIX
       STO     DIG           NUMBER, SAVE DIGIT.
       STQ     TEM           SAVE MQ FOR MULTIPLIACATION.
       LDQ     INT           GET OLD NUMBER.
       MPY     =10           SHIFT.
       XCA
       ADD     DIG           ADD THIS DIGIT TO RESULT.
       STO     INT           SAVE, FOR NEXT PASS.
       LDQ     TEM           RESTORE MQ.
TIX    TIX     LOP,1,1
 IRR1  AXT     --,1
       CLA     INT           RESULT IN AC.
       TRA     2,4
*
 INT
 DIG
 TEM
*
       END
