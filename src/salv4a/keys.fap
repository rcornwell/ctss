*                  
       ENTRY   KEYS          KEYS=KEYS.   
       ENTRY   SW            1B=SW.(I)    
       ENTRY   EVSW          ..     
KEYS   ENK  
       XCA  
       TRA     1,4
*     
SW     SCA     XR,4          SAVE   
       CAL*    1,4           GET SWITCH NUMBER  
       PAC     ,4 
       ZAC  
       TXL     XR,4,-7       TRA IF BAD NUMBER  
       XEC     TEST,4        EXECUTE SWT  
       TRA     *+2           TRA IF DOWN  
       CAL     =1 
       SLW     TEMP     
       LAS     LSW,4         CHECK FOR CHANGE   
       TRA     *+2           CHANGE 
       TRA     OK 
       SLW     LSW,4    
       AXT     6,4           PRINT SWITCHES     
       ZAC  
       ALS     6             PACK TO ONE WORD   
       ORA     LSW+7,4  
       TIX     *-2,4,1  
       SLW     PSW
       TSX     $MPRINT,4
       TXH     =1 
       TXH     =H0SWS=  
       TXH     PSW
       CAL     TEMP     
OK     ALS     0,4           SET EVER (ALL SWITCHES   
       ORS     EVER          WHICH HAVE EVER BEEN DOWN)     
       CAL     TEMP     
XR     AXC     ,4 
       TRA     1,4
TEMP  
PSW   
LSW    BSS     7  
*     
EVSW   SCA     XRE,4         SAVE   
       CAL*    1,4           GET SWITCH NUMBER  
       PAC     ,4 
       CAL     EVER          GET EVER WORD
       ARS     0,4
       ANA     =1 
XRE    AXC     ,4 
       TRA     1,4
EVER  
TEST   TRA     XR 
       DUP     1,6
       SWT     *-TEST   
       END  
