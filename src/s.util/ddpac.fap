* M1416 786 ... R. DALEY ... DDPAC ... USED AS SUBROUTINE PACK FOR DDIOA   
       TITLE   
       REM
       ENTRY   EXIT          EXIT TO FMS MONITOR
       ENTRY   PRINT         ON LINE PRINT ROUTINE    
       ENTRY   EPRINT        PRINT ERROR CONDITION ON TRAP  
       ENTRY   WRTOPR        .. SAME AS EPRINT  
       ENTRY   RTDINP        READ MONITOR INPUT TAPE (BCD MODE)   
       ENTRY   RTBINP        READ MONITOR INPUT TAPE (BINARY MODE)
       ENTRY   WTDOUT        WRITE MONITOR OUTPUT TAPE (BCD MODE) 
       ENTRY   CMEXIT        COMMON EXIT ROUTINE FROM ALL TRAPS   
       ENTRY   ALLSAV        SAVE BASIC MACHINE CONDITIONS ON TRAP
       ENTRY   ALLRST        RESTORE BASIC MACHINE CONDITIONS     
       ENTRY   ENABLE        POINTER TO COMMON ENABLE WORD  
       REM
       REM
U      TAPENO  A2            MONITOR INPUT TAPE UNIT  
O      TAPENO  A3            MONITOR OUTPUT TAPE UNIT 
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... EXIT ... EXIT TO FMS MONITOR SYSTEM .......... 
       REM
EXIT   ENB     =0            DISABLE ALL TRAPS  
       TCOA    *             WAIT UNTIL ALL CHANNEL ACTIVITY CEASES     
       TCOB    *             ..     
       RDCA                  AND RESET CHANNELS A,    
       RDCB                  .. AND B     
       REM
       ENK                   IF KEYS 0-2 DOWN, STOP.  
       XCL  
       LAS     =O700000000000 
       NOP  
       HTR     0  
       REM
       AXT     10,4          RELOAD FMS FROM A1 
EXIT1  REWA    1             ..     
       RTBA    1             ..     
       RCHA    LDFIOP        ..     
       TCOA    *             ..     
       TRCA    A1BAD         ..     
       TEFA    A1BAD         ..     
       RTBA    1             .. SKIP CARD TO TAPE RECORD    
       RTBA    1             .. SKIP DUMP RECORD
       TRA     1             EXIT TO FIOP TO LOAD SIGN-ON RECORD  
       REM
A1BAD  TIX     EXIT1,4,1     COUNT ERRORS 
       HTR     EXIT1         STOP ON TOO MANY ERRORS  
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... PRINT ... NORMAL ON LINE PRINT ROUTINE   
       EJECT
PRINT  SXA     PRINX4,4 
       ENB     =0            DISABLE ALL TRAPS FOR CALL TO EPRINT 
       CAL     1,4           PZE FIRST,,N 
       SLW     *+2           PASS CALL TO EPRINT
       TSX     EPRINT,4      CALL EPRINT TO PRINT THIS LLNE 
               **,,**        ..     
       ENB     ENABLE        REENABLE AFTER CALL TO EPRINT  
PRINX4 AXT     **,4          AND RETURN   
       TRA     2,4           ..     
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... EPRINT ... ON LINE PRINT ROUTINE FOR I/O ERRORS .....
       REM
WRTOPR SYN     *             ..     
EPRINT SXA     NPIR1,1  
       SXA     NPIR2,2  
       SXA     NPIR4,4  
       TCOA    *             SAVE STATUS OF CHANNEL 'A'     
       STZ     SVIOCK        ..     
       IOT                   ..     
       STL     SVIOCK        ..     
       SCHA    SVCHAN        ..     
       CAL     1,4           PZE FIRST,,N 
       ARS     18 
       ACL     1,4
       PDX     0,4           WORD COUNT TO IR4  
       TXL     NPEND,4,0     SKIP IF WORD COUNT .E. 0 
       STA     NPRC3         WORD COUNT + STARTING ADDRESS  
       CAL     NOP           SET UP FOR FIRST PASS    
       SLW     PSPR          ..     
       CAL     SPR9          ..     
       SLW     PSPR1         ..     
       REM
1PASS  AXT     24,2          CLEAR WORKING STORAGE    
       STZ     CIBUF+24,2    ..     
       TIX     *-1,2,1       .. C(IR2)=1 AFTER LOOP   
NPRC1  CAL     =-0           INITIALIZE COLUMN MARKER 
NPRC2  SLW     PRCOL         ..     
       SXA     NPSV4,4       SAVE WORD COUNT    
NPRC3  LDQ     **,4          PICK UP FIRST OR NEXT BCD WORD 
       AXT     6,4           SET CHARACTER COUNT
NPRC4  ZAC  
       LGL     6             GET A CHARACTER    
       ALS     1             DOUBLE IT    
       PAX     0,1           INTO IR1     
       CAL     PRCOL    
       ARS     6,4           POSITION COLUMN MARKER   
       TXL     PDIGIT,1,24   SKIP IF DIGIT ONLY 
       TXL     PNZONE,1,95    
       TXL     NPRC5,1,96    SKIP IF BLANK
       REM
PNZONE TXH     PNMIN,1,62    SKIP IF 11 OR 0 ZONE     
       ORS     CIBUF+23,2    OR IN THE 12 ZONE  
       TIX     PDIGIT,1,32   REMOVE 12 PUNCH    
       TRA     NPRC5         SKIP IF + ONLY (NO DIGIT)
PNMIN  TXH     PNZER,1,94    SKIP IF 0 ZONE     
       ORS     CIBUF+21,2    OR IN THE 11 ZONE  
       TIX     PDIGIT,1,64   REMOVE 11 ZONE     
       TRA     NPRC5         SKIP IN - ONLY (NO DIGIT)
PNZER  ORS     CIBUF+19,2    OR IN THE 0 ZONE   
       TXI     PDIGIT,1,-96  REMOVE 0 ZONE
       REM
PDIGIT TXL     PNDIG,1,18    SKIP IF NORMAL DIGIT     
       ORS     CIBUF+3,2     HERE FOR 8-3, 8-4, OR IN THE 8 PUNCH 
       TXI     *+1,1,-16     REMOVE THE 8 PUNCH 
PNDIG  TXL     *+2,2,0       SKIP IF IN RIGHT HALF OF CARD IMAGE  
       ORS     CIBUF+18,1    OTHERWISE 'OR' DIGIT TO LEFT HALF OF IMAGE 
       TXH     *+2,2,0       SKIP IF IN LEFT HALF OF CARD IMAGE   
       ORS     CIBUF+19,1    OTHERWISE 'OR' DIGIT TO RIGHT HALF   
NPRC5  TIX     NPRC4,4,1     COUNTS CHARACTERS  
       ARS     1             SET COLUMN MARKER FOR NEXT WORD
NPSV4  AXT     **,4          RESTORE BCD WORD COUNT   
       TXI     *+1,4,-1      REDUCE WORD COUNT BY ONE 
       TXL     PNOW,4,0      SKIP TO END WHEN WORD COUNT EXHAUSTED
       TNZ     NPRC2         GET NEXT WORD UNTIL COLUMN MARKER MOVES OUT
       TXL     PNOW,2,0      SKIP TO END WHEN CARD IMAGE COMPLETE 
       AXT     0,2           OTHERWIZE SET UP FOR RIGHT HALF
       TRA     NPRC1    
       REM
PNOW   WPRA                  PRINT THIS CARD IMAGE ON LINE  
PSPR   ***                   .. NOP OR SPRA 9   
       RCHA    NPIOC         ..     
       TXL     NPEND,4,0     SKIP TO END WHEN WORD COUNT EXHAUSTED
       CAL     PSPR          OTHERWISE SWITCH NOP AND SPR 9 
       LDQ     PSPR1         ..     
       STQ     PSPR          ..     
       SLW     PSPR1         ..     
       TCOA    *             WAIT UNTIL CHANNEL FINISHED    
       TRA     1PASS         AND RETURN TO CONVERT REST OF LINE   
       REM
NPEND  TCOA    *             RESTORE STATUS OF CHANNEL 'A'  
       CAL     SVCHAN        ..     
       PDC     0,4           ..     
       ANA     =O700000377777  ..   
       LDQ     -1,4          ..     
       SLW     -1,4          ..     
       RCHA    -1,4          ..     
       STQ     -1,4          ..     
       TCOA    *             ..     
       NZT     SVIOCK        ..     
       IOT                   ..     
       NOP  
NPIR1  AXT     **,1     
NPIR2  AXT     **,2     
NPIR4  AXT     **,4     
       TRA     2,4           RETURN TO CALLER   
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       EJECT
       REM     ... RTDINP/RTBINP ... READ CARD FROM MONITOR INPUT TAPE  
       REM
RTDINP CAL     RTDU          ENTRY TO READ IN BCD MODE
       TRA     READ1         ..     
       REM
RTBINP CAL     RTBU          ENTRY TO READ IN BINARY MODE   
       REM
READ1  SLW     READU         SET UP BCD OR BINARY READ INSTRUCTION
       SXA     RTIR4,4  
       CAL     1,4           SET UP I/O COMMAND FROM 1,4    
       STA     RDCOM         .. ADDRESS OF USER'S INPUT BUFFER    
       STD     RDCOM         .. WORD COUNT
       REM
FRSTSW TRA     *+1           FIRST PASS SWITCH  
       TCOU    *             INITIALIZE ON FIRST PASS ONLY  
       TRCU    *+1           .. RESET TAPE CHECK TRIGGER    
       TEFU    *+1           .. RESET EOF TRIGGER     
       STL     FRSTSW        SET TO SKIP INITIALIZATION SECTION   
       REM
RETRY  AXT     20,4          TAPE ERROR RETRY COUNT   
READU  ***                   RTDU OR RTBU, READ NEXT CARD FROM TAPE     
       RCHU    RDCOM         ..     
       TCOU    *             ..     
       TRCU    ERRU          .. SKIP ON TAPE ERROR    
RTIR4  AXT     **,4          RESTORE CALLER'S IR4     
       TEFU*   2,4           TAKE EOF EXIT ON END OF FILE   
       TRA     3,4           OTHERWISE TAKE NORMAL RETURN (3,4)   
       REM
ERRU   BSRU                  HERE FOR TAPE ERROR, BACKSPACE 
       TIX     READU,4,1     AND RETRY UP TO 20 TIMES 
       WPRA                  HERE FOR TOO MANY ERRORS 
       SPRA    1             SPACE UP PAGE ON LINE    
       TSX     PRINT,4       AND PRINT ERROR COMMENT  
               BADU,,12      ..     
       WPRA                  SPACE UP PAGE AGAIN
       SPRA    1             ..     
       HTR     RETRY         AND STOP, RETRY 20 MORE TIMES ON START     
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... WTDOUT ... WRITE MONITOR OUTPUT TAPE (BCD MODE)
       REM
WTDOUT TRA     *+1           FIRST PASS SWITCH  
       TCOO    *             WAIT AND TURN OFF ALL TAPE TRIGGERS  
       TRCO    *+1           ..     
       TEFO    *+1           ..     
       STL     WTDOUT        CLOSE THIS PATH    
       REM
       CAL     1,4           PZE LINE,,COUNT    
       STA     WRCOM         SET UP USER WRITE COMMAND
       STD     WRCOM         ..     
       SXA     WRIR4,4       SAVE USER'S IR4    
       REM
REWRIT AXT     20,4          TAPE ERROR RETRY COUNT   
WRITEO WTDO                  TRY TO WRITE THIS RECORD 
       RCHO    WRCOM         ..     
       TCOO    *             ..     
       TRCO    ERRO          SKIP ON TAPE CHECK 
WRIR4  AXT     **,4          OTHERWISE    
       TRA     2,4           RETURN 
       REM
ERRO   BSRO                  HERE FOR TAPE ERROR, BACKSPACE 
       TIX     WRITEO,4,1    AND RETRY UP TO 20 TIMES 
       WPRA                  SPACE UP PAGE ON LINE    
       SPRA    1             ..     
       TSX     PRINT,4       AND PRINT ERROR COMMENT  
               BADO,,12      ..     
       WEFO                  END THIS TAPE
       WEFO                  ..     
       WPRA                  SPACE UP PAGE AGAIN
       SPRA    1             ..     
       HTR     REWRIT        AND STOP, WAIT FOR OPERATOR    
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... CMEXIT ... COMMON EXIT ROUTINE FROM ALL TRAPS .......
       REM
CMEXIT ENB     =0            INSURE ALL TRAPS DISABLED
       STI     CMXSI         SAVE INDICATORS    
       LDI*    2,4           PICK UP RETURN FLAGS     
       RIR     700000        INSURE TAG ZERO    
       STI     CMXRTN        SAVE RETURN LOCATION     
       XEC     1,4           RESTORE USER'S IR4 FROM 1,4    
       LFT     20000         WAS ECC SET FOR MEMORY 'A'     
       TRA     CMRTNB        NO, SET ECC TO MEMORY 'B'
       LDI     CMXSI         YES, RELOAD INDICATORS   
       ENB     ENABLE        AND RETURN TO INTERRUPTED PROGRAM    
       TRA*    CMXRTN        ..     
       REM
CMRTNB LDI     CMXSI         HERE TO RETURN WITH ECC SET FOR MEMORY 'B' 
       ENB     ENABLE        RETURN TO INTERRUPTED PROGRAM  
       LRI     =0            .. (DUMMY TO PROTECT THIS SEQUENCE FROM TRA
       SEB                   .. SET ECC FOR MEMORY 'B'
       TRA*    CMXRTN        ..     
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... ALLSAV ... SAVE BASIC MACHINE CONDITIONS ............
       REM
ALLSAV SXA     TRPIR1,1      SAVE BASIC MACHINE CONDITIONS  
       SXA     TRPIR2,2      ..     
       SXA     TRPIR3,3      ..     
       AXT     0,3           .. SAVE TAG MODE   
       AXT     1,1           ..     
       STZ     TRPMTM        ..     
       TXL     7TAG,3,0      .. SKIP IF IN 7-TAG (7094) MODE
       STL     TRPMTM        .. OTHERWISE SET 3-TAG (7090) MODE   
       LMTM                  ..     
       SXA     TRPIR3,3      .. RESAVE IR3
7TAG   SXA     TRPIR5,5      ..     
       SXA     TRPIR6,6      ..     
       SXA     TRPIR7,7      ..     
       EMTM                  .. RESTORE 3-TAG (7090) MODE   
       STI     TRPSI         .. SAVE SENSE INDICATORS 
       STQ     TRPMQ         .. MQ  
       SLW     TRPLAC        .. LOGICAL AC
       ARS     2             ..     
       STO     TRPSQ         .. S AND Q BITS    
       STZ     TRPOV         .. AC OVERFLOW CONDITION 
       TNO     *+2           ..     
       STL     TRPOV         ..     
       TRA     1,4           AND RETURN   
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... ALLRST ... RESTORE BASIC MACHINE CONDITIONS .........
       REM
ALLRST LMTM                  INSURE 7-TAG (9094) MODE 
TRPIR1 AXT     **,1          ..     
TRPIR2 AXT     **,2          ..     
TRPIR3 AXT     **,3          ..     
TRPIR5 AXT     **,5          ..     
TRPIR6 AXT     **,6          ..     
TRPIR7 AXT     **,7          ..     
       ZET     TRPMTM        .. RESTORE TAG MODE
       EMTM                  ..     
       LDQ     TRPSQ         .. RESTORE S AND Q BITS  
       CLA     *             .. AND INSURE OVERFLOW LIGHT IS ON   
       LLS     1             ..     
       ALS     36            ..     
       ORA     TRPLAC        .. RESTORE LOGICAL AC    
       LDQ     TRPMQ         .. RESTORE MQ
       LDI     TRPSI         .. RESTORE SI
       NZT     TRPOV         .. RESTORE CORRECT OVERFLOW STATUS   
       TOV     *+1           ..     
       TRA     1,4           AND RETURN   
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ... ENABLE ... COMMON ENABLE WORD FOR ALL TRAPS .......  
       REM
ENABLE OCT     0             COMMON ENABLE WORD 
       REM
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  
       REM
       REM     ..... CONSTANT AND VARIABLE STORAGE FOR 'DDPAC' ........ 
       REM
       REM
CMXRTN PZE     0             TEMP FOR RETURN FROM 'CMEXIT'  
CMXSI  PZE     0             SENSE INDICATORS SAVED BY 'CMEXIT'   
       REM
TRPIR4 PZE     0             MACHINE CONDITIONS SAVED ON TRAP     
TRPMTM PZE     **            ..     
TRPSI  PZE     0             ..     
TRPMQ  PZE     0             ..     
TRPLAC PZE     0             ..     
TRPSQ  PZE     0             ..     
TRPOV  PZE     0             ..     
PRCOL  PZE     0             TEMPS FOR EPRINT   
PSPR1  PZE     0             ..     
SVCHAN PZE     0             ..     
SVIOCK PZE     0             ..     
       REM
SPR9   SPRA    9             CONSTANT FOR EPRINT
NOP    NOP                   CONSTANT FOR EPRINT
RTDU   RTDU                  CONSTANT FOR RTDINP
RTBU   RTBU                  CONSTANT FOR RTBINP
       REM
CIBUF  BSS     24            CARD IMAGE BUFFER FOR 'EPRINT' 
       REM
LDFIOP IOCP    0,,3          LOAD FIOP INTO MEMORY 'A'
       TCH     0             ..     
NPIOC  IOCD    CIBUF,,24     I/O COMMAND TO WRITE OUT CARD IMAGE  
RDCOM  IORT    **,,**        COMMAND TO READ FROM MONITOR INPUT TAPE    
WRCOM  IOCD    **,,**        COMMAND TO WRITE ON MONITOR OUTPUT TAPE    
       REM
BADU   BCI     7,******* MONITOR INPUT TAPE WILL NOT READ,  
       BCI     5,PRESS START TO RETRY. ******** 
BADO   BCI     7,***** MONITOR OUTPUT TAPE WILL NOT WRITE,  
       BCI     5,CHANGE AND PRESS START. ****** 
       REM
       DETAIL
       END  
