*M1416 3845  PETER R. BOS ......... SYSTEM TRANSFER COMMANDS .........  SAVC0001
       PCC     ON                                                       SAVC0002    
       NOLNK                                                            SAVC0003    
       ENTRY   ENDLG.        TSS ENDLOG COMMAND                         SAVC0004    
       ENTRY   SBSYS.        TSS SUBSYS COMMAND                         SAVC0005    
       ENTRY   SAVE.         TSS SAVE COMMAND                           SAVC0006    
       ENTRY   MSAVE.        TSS MYSAVE COMMAND                         SAVC0007    
       ENTRY   DO.           TSS DO COMMAND                             SAVC0008    
       ENTRY   RSTOR.        TSS RESTOR COMMAND                         SAVC0009    
       ENTRY   RSUME.        TSS RESUME COMMAND                         SAVC0010    
       ENTRY   RCALL.        TSS RECALL COMMAND                         SAVC0011    
       ENTRY   CNTIN.        TSS CONTIN COMMAND                         SAVC0012    
       ENTRY   RSTRT.        TSS RSTART COMMAND                         SAVC0013    
       ENTRY   START.        TSS START COMMAND                          SAVC0014    
       ENTRY   RSETF.        TSS RESETF COMMAND                         SAVC0015    
       LBL     SAVC0000                                                 SAVC0016    
       PCC     OFF                                                      SAVC0017    
       REM                                                              SAVC0018    
       EXTERN  ATTFIL,CHNCOM,CHNERR,CHNGUS,CMPROC                       SAVC0019    
       EXTERN  DORMNT,GETUFD,ILLCOM,NEWCOM,OTOLOG                       SAVC0020    
       EXTERN  PRDIAG,RESETF,RSTCPU,SETUSR,UDUMP                        SAVC0021    
       EXTERN  ULOAD,WRFLX,ZBL                                          SAVC0022    
       REM                                                              SAVC0023    
       UNLIST                                                           SAVC0024    
       INSERT  MACRO                                                    SAVC0025    
       INSERT  EQU                                                      SAVC0026    
       INSERT  COMMON                                                   SAVC0027    
       LIST                                                             SAVC0028    
       TITLE                                                            SAVC0029    
       REM                                                              SAVC0030    
       REM                                                              SAVC0031    
ENDLG. TYPE    (/ AUTOMATIC LOGOUT./)  TELL USER THIS IS AUTOLOGOUT     SAVC0032    
       LAC     USER,1        GET USER NO.                               SAVC0033    
       NZT     MEMBND        CREATE SAVED FILE ONLY IF CORE IMAGE       SAVC0034    
       ZET     WATCOM        .. OR WAITING COMMAND IN CHAIN             SAVC0035    
       TRA     *+2           .. BY POPULAR DEMAND                       SAVC0036    
       TRA     ENDLG1        ..                                         SAVC0037    
       CAL     PROGN,1       FUDGE UP A SAVED FILE NAME                 SAVC0038    
       ALS     6             .. 'PROG'L                                 SAVC0039    
       ORA     =H00000L      ..                                         SAVC0040    
       SLW     SN1           SAVE FILE NAME                             SAVC0041    
       STL     MSW           INDICATE SAVE IN USER UFD                  SAVC0042    
       TSX     SAVE,4        CREATE SAVED FILE                          SAVC0043    
       PZE     SN1,,1        .. IN TEMP. MODE                           SAVC0044    
       NOP                   .. IGNORE ERROR                            SAVC0045    
ENDLG1 CAL*    OTOLOG        SET UP CHAIN TO OTOLOG                     SAVC0046    
       SLW     UCOMND        ..                                         SAVC0047    
       TRA     CMPROC        .. FINISH UP AUTOLOGOUT                    SAVC0048    
       REM                                                              SAVC0049    
SBSYS. LAC     USER,1        HERE FOR 'SUBSYS' COMMAND                  SAVC0050    
       LDI     USROPT,1      PICK UP USER STANDARD OPTIONS              SAVC0051    
       RFT     SAVEBT        DOES USER SUBSYSTEM WISH SAVED FILE        SAVC0052    
       RFT     SYSBIT        AND IS THIS NON-SUBSYSTEM CORE IMAGE       SAVC0053    
       TRA     SBSYS1        .. NO                                      SAVC0054    
       NZT     MEMBND        CREATE SAVED FILE ONLY IF CORE IMAGE LEFT  SAVC0055    
       ZET     COMCTR        .. OR INCOMPLETE COMMAND CHAIN             SAVC0056    
       TRA     *+2           ..                                         SAVC0057    
       TRA     SBSYS1        .. ELSE, SKIP SAVE                         SAVC0058    
       STL     MSW           SIMULATE 'MYSAVE PROGN T'                  SAVC0059    
       TSX     SAVE,4        CREATE SAVED FILE                          SAVC0060    
       PZE     PROGN,1,1     .. IN TEMPORARY MODE                       SAVC0061    
       NOP                   IGNORE POSSIBLE ERROR                      SAVC0062    
       REM                                                              SAVC0063    
SBSYS1 LDI     UCOMSW        MAIN BODY OF SUBSYSTEM PROCESSOR           SAVC0064    
       LNT     UCOMBT        WAS SUBSYS ISSUED BY USER                  SAVC0065    
       TRA     *+4           .. NO, SKIP                                SAVC0066    
       LDQ     Q0            .. YES, SET CONDITION CODE TO 0            SAVC0067    
       SLQ     SUBSWS,1      ..                                         SAVC0068    
       TRA     SBSYS4        LOAD SUBSYSTEM UNCONDITIONALLY             SAVC0069    
       REM                                                              SAVC0070    
       NZT     SUBNAM,1      CHECK IF USER HAS SUBSYSTEM                SAVC0071    
       TRA     SBSYS2        SKIP IF NO SUBSYSTEM SET UP                SAVC0072    
       LDI     SUBSWS,1      LOAD SUBSYSTEM CONDITION SWITCHES          SAVC0073    
       CAL     SUBSWS,1      ..                                         SAVC0074    
       ARS     9             SHIFT TO POSITION                          SAVC0075    
       NZT     MEMBND        ..                                         SAVC0076    
       ARS     9             ..                                         SAVC0077    
       TIO     SBSYS3        IF CONDITION MET, LOAD IT                  SAVC0078    
       REM                                                              SAVC0079    
SBSYS2 LDI     UCOMSW        HERE IF NO SUBSYSTEM LOADING               SAVC0080    
       LNT     UCOMBT        WAS SUBSYS USER-ISSUED                     SAVC0081    
       LNT     UCMLBT        .. NO, DID USER ISSUE A COMMAND            SAVC0082    
       TRA     $ENDSYS       .. NO, EXIT                                SAVC0083    
       CAL*    COMPTR        .. YES, GET COMMAND TYPED                  SAVC0084    
       SIL     UCOMBT        INDICATE COMMAND ISSUED BY USER            SAVC0085    
       STI     UCOMSW        ..                                         SAVC0086    
       TRA     SBSYS5        GO PROCESS NEW COMMAND                     SAVC0087    
       REM                                                              SAVC0088    
SBSYS3 LDI     USROPT,1      HERE TO START UP SUBSYSTEM                 SAVC0089    
       RNT     SYSBIT        IS CURRENT USER PROGRAM SUBSYSTEM          SAVC0090    
       TRA     SBSYS4        .. NO, GO LOAD SUBSYSTEM                   SAVC0091    
       CAL*    $START        .. YES, RESTART IT                         SAVC0092    
       TRA     SBSYS5        GO PROCESS NEW COMMAND                     SAVC0093    
       REM                                                              SAVC0094    
SBSYS4 LDI     USROPT,1      HERE TO LOAD SUBSYSTEM FOR USER            SAVC0095    
       SIR     SYSBIT        INDICATE USER PROGRAM WILL BE SUBSYSTEM    SAVC0096    
       STI     USROPT,1      ..                                         SAVC0097    
       LDI     UCOMSW        INDICATE NEXT COMMAND                      SAVC0098    
       RIL     UCOMBT        .. INITIATED BY SYSTEM                     SAVC0099    
       STI     UCOMSW        ..                                         SAVC0100    
       CAL     M0            SET CORE IMAGE BIT                         SAVC0101    
       ZET     MEMBND        ..                                         SAVC0102    
       ORS     SUBSWS,1      .. IF USER HAS CORE IMAGE                  SAVC0103    
       CAL     SUBNAM,1      GET NAME OF USER SUBSYSTEM COMMAND         SAVC0104    
       REM                                                              SAVC0105 
SBSYS5 SLW     UCOMND        SET UP NEW COMMAND TO RUN                  SAVC0106    
       TRA     CMPROC        EXIT TO COMMAND PROCESSOR                  SAVC0107    
       REM                                                              SAVC0108    
SAVE.  STZ     MSW           SAVE COMMAND, CURRENT UFD                  SAVC0109    
       TRA     MSAVE.+1      ..                                         SAVC0110    
       REM                                                              SAVC0111    
MSAVE. STL     MSW           MYSAVE COMMAND, RESET TO OWN UFD           SAVC0112    
       LAC     USER,1        GET USER NO.                               SAVC0113    
       CAL     PROGN,1       THIS IS FILE NAME IF NOT GIVEN             SAVC0114    
       SLW     SN1           ..                                         SAVC0115    
       ZSD     MODE          MODE IS '0' IF NOT SPECIFIED               SAVC0116    
       LAC     COMPTR,2      LOAD POINTER TO COMMAND BUFFER             SAVC0117    
       CAL     1,2           GET FIRST ARGUMENT                         SAVC0118    
       LAS     FENCE         CHECK FOR FENCE                            SAVC0119    
       TRA     *+2           ..                                         SAVC0120    
       TRA     SAVE.1        YES, SKIP                                  SAVC0121    
       SLW     SN1           NO, SAVE FILE NAME                         SAVC0122    
       CAL     2,2           CHECK FOR 'T' ARGUMENT                     SAVC0123    
       ERA     =H     T      ..                                         SAVC0124    
       TNZ     SAVE.1        NOT T, SKIP                                SAVC0125    
       AXT     1,4           'T' SPECIFIED, USE TEMP. MODE              SAVC0126    
       SXD     MODE,4        ..                                         SAVC0127    
SAVE.1 TSX     SAVE,4        CREATE SAVED FILE                          SAVC0128    
MODE   PZE     SN1,,-        ..                                         SAVC0129    
       TRA     LER1          .. POSSIBLE ERROR                          SAVC0130    
       REM                                                              SAVC0131    
SVRET  NZTBIT  UCOMSW,,UCHNBT DID COMMAND COME VIA CHNCOM               SAVC0132    
       TRA     SVRET1        .. NO, SKIP                                SAVC0133    
       LDI     Q1            .. YES, CONTINUE CHAIN                     SAVC0134    
       TRA     CHNCOM        ..                                         SAVC0135    
SVRET1 TSX     DORMNT,4      GO DORMANT                                 SAVC0136    
       VFD     O36/SB.TSX    ..                                         SAVC0137    
       REM                                                              SAVC0138    
       REM                                                              SAVC0139    
SAVE   SXA     SVX4,4        ROUTINE TO CREATE SAVED FILE               SAVC0140    
       CAL*    1,4           PICK UP SAVED FILE NAME                    SAVC0141    
       SLW     SN1           .. SAVE                                    SAVC0142    
       CAL     1,4           PICK UP DESIRED FILE MODE                  SAVC0143    
       STD     SVMD          ..                                         SAVC0144    
       TSX     GETUFD,4      PICK UP CURRENT USRFIL NAME                SAVC0145    
       NZT     MSW           IS FILE TO BE CREATED IN CURRENT UFD       SAVC0146    
       TRA     *+3           .. YES, SKIP                               SAVC0147    
       CAL     PROBN,1       .. NO, GET USER OWN UFD NAME               SAVC0148    
       LDQ     UFDNM,1       ..                                         SAVC0149    
       TSX     ATTFIL,4      ATTACH HIM THERE                           SAVC0150    
       TRA     SVERR         .. POSSIBLE ERROR                          SAVC0151    
       CALL    SETUSR(Q2,(RCODE,1),M0,PRIND,RLIND,M0)  ..               SAVC0152    
       TSX     UDUMP,4       DUMP USER ON DISK                          SAVC0153    
SVMD   PAR     SVOPT,,-      ..                                         SAVC0154    
       PAR     SN1           ..                                         SAVC0155    
       PAR     SAVED         ..                                         SAVC0156    
       PAR     *+1,,SVERR    .. IGNORE NON-FATAL ERROR                  SAVC0157    
SVX4   AXT     -,4           RESTORE LINKAGE                            SAVC0158    
       TRA     3,4           RETURN                                     SAVC0159    
       REM                                                              SAVC0160    
SVERR  LXA     SVX4,4        RESTORE LINKAGE                            SAVC0161    
       TRA     2,4           GIVE ERROR RETURN                          SAVC0162    
       EJECT                                                            SAVC0163
       REM                                                              SAVC0164    
DO.    STL     RUNSW         TSS DO COMMAND, INDICATE WANT TO RUN       SAVC0165    
       STL     CHNSW         ALLOW CHNCOM IF NOT RUNNABLE               SAVC0166    
       CAL     COMPTR        GET COMMAND POINTER                        SAVC0167    
       PAC     ,2            ..                                         SAVC0168    
       STA     *+2           ..                                         SAVC0169    
       AXC     2,4           COPY COMMAND, MOVED DOWN TWO WORDS         SAVC0170    
       CAL     -,4           ..                                         SAVC0171    
       SLW     UCOMBF-2,4    ..                                         SAVC0172    
       TXI     *+1,4,-1      ..                                         SAVC0173    
       TXH     *-3,4,-LCBUF  ..                                         SAVC0174    
       LAC     USER,1        GET USER NO.                               SAVC0175    
       LDQ     LDOPT0        PICK UP LOAD OPTIONS FOR DO                SAVC0176    
       CAL     1,2           LOOK AT COMFIL NUMBER                      SAVC0177    
       LAS     FENCE         CHECK FOR NOT SPECIFIED                    SAVC0178    
       TRA     *+2           ..                                         SAVC0179    
       TRA     LOAD2         ASSUME 'DO * PROGN'                        SAVC0180    
       LAS     =H     *      DOES USER WANT LOAD FROM CURRENT UFD       SAVC0181    
       TRA     *+2           .. NO                                      SAVC0182    
       TRA     DO.1          .. YES                                     SAVC0183    
       TSX     ZBL,4         STRIP LEADING BLANKS                       SAVC0184    
       ANA     =O7777        SAVE ONLY TWO CHARACTERS                   SAVC0185    
       TZE     DO.2          DO 0 MEANS OWN DIRECTORY                   SAVC0186    
       LDI     RCODE,1       CHECK FOR DISK-RESTRICTED USER             SAVC0187    
       RFT     RDSKBT        ..                                         SAVC0188    
       TRA     NODO          .. MAY ONLY USE 'DO 0' OR 'DO *'           SAVC0189    
       LAS     =H00000P      IS THIS COMFIL P                           SAVC0190    
       TRA     *+2           ..                                         SAVC0191    
       TRA     DO.3          .. YES, GO PICK UP TSSFID                  SAVC0192    
       RNT     RCOMBT        SEE IF USER ALLOWED IN COMMON FILES        SAVC0193    
       TRA     NODO          .. NO, ERROR                               SAVC0194    
       ORA     CMFL00        FORM FILE DIRECTORY NAME2                  SAVC0195    
       XCL                   ..                                         SAVC0196    
       CAL     PROBN,1       .. AND NAME1                               SAVC0197    
DO.0   TSX     ATTFIL,4      ATTACH TO SPECIFIED COMFIL                 SAVC0198    
       TRA     CHNERR        .. POSSIBLE ERROR RETURN                   SAVC0199    
       LDQ     LDOPT0        RELOAD OPTION FLAGS FOR ULOAD              SAVC0200    
DO.1   CAL     2,2           PICK UP SAVED FILE NAME                    SAVC0201    
       TRA     LOAD1         .. SKIP TO COMMON CODE                     SAVC0202    
       REM                                                              SAVC0203    
DO.2   LDQ     UFDNM,1       HERE FOR DO 0 ...                          SAVC0204    
       TRA     DO.0-1        REJOIN COMMON CODE                         SAVC0205    
       REM                                                              SAVC0206    
DO.3   CAL     TSSFID        HERE FOR 'DO P ...'                        SAVC0207    
       LDQ     TSSFID+1      PICK UP TSSFIL NAME                        SAVC0208    
       TRA     DO.0          GO TO ATTFIL                               SAVC0209    
       REM                                                              SAVC0210    
NODO   TYPE    (/ USER MAY NOT USE COMMON FILES./)                      SAVC0211    
       TRA     CHNERR        GO PUT HIM IN DORMANT STATUS               SAVC0212    
       REM                                                              SAVC0213    
RSTOR. STZ     RUNSW         RESTOR COMMAND, INDICATE DORMANT AFTER LOADSAVC0214    
       TRA     RSUME.+1      .. SKIP                                    SAVC0215    
       REM                                                              SAVC0216    
RSUME. STL     RUNSW         RESUME COMMAND, INDICATE DESIRE TO RUN     SAVC0217    
       STL     CHNSW         .. GO TO CHNCOM AT END                     SAVC0218    
       CAL     COMPTR        COPY COMMAND BUFFER FOR USER               SAVC0219    
       STA     *+2           ..                                         SAVC0220    
       AXC     1,4           .. MOVED DOWN ONE WORD                     SAVC0221    
       CAL     -,4           ..                                         SAVC0222    
       SLW     UCOMBF-1,4    ..                                         SAVC0223    
       TXI     *+1,4,-1      ..                                         SAVC0224    
       TXH     *-3,4,-LCBUF  ..                                         SAVC0225    
       LDQ     LDOPT1        PICK UP ULOAD OPTIONS FOR RESTOR/RESUME    SAVC0226    
       TRA     LOAD0         AND GO LOOK FOR FILE NAME                  SAVC0227    
       REM                                                              SAVC0228    
RCALL. STZ     RUNSW         RECALL COMMAND, NO START                   SAVC0229    
       TRA     CNTIN.+1      .. SKIP                                    SAVC0230    
       REM                                                              SAVC0231    
CNTIN. STL     RUNSW         CONTIN COMMAND WANTS TO START UP           SAVC0232    
       STZ     CHNSW         .. GO TO DORMNT IF NOT STARTING            SAVC0233    
       LDQ     LDOPT2        PICK UP ULOAD OPTIONS FOR RECALL/CONTIN    SAVC0234    
       REM                                                              SAVC0235    
LOAD0  LAC     USER,1        GET USER NO.                               SAVC0236    
       LAC     COMPTR,2      .. AND COMMAND BUFFER POINTER              SAVC0237    
       CAL     1,2           GET SAVED FILE NAME IF SPECIFIED           SAVC0238    
LOAD1  LAS     FENCE         CHECK FOR FENCE                            SAVC0239    
       TRA     *+2           .. NO                                      SAVC0240    
LOAD2  SLW     UCOMBF+1      .. YES, MAKE SURE USER COMMAND HAS A FENCE SAVC0241    
       LAS     FENCE         CHECK IT AGAIN TO MAKE SURE                SAVC0242    
       TRA     *+2           .. STILL NO FENCE                          SAVC0243    
       CAL     PROGN,1       FILE NAME NOT GIVEN, USE PROGNO.           SAVC0244    
       SLW     SN1           .. FOR NAME OF SAVED FILE                  SAVC0245    
       SLW     UCOMBF        .. PROGRAM MAY WANT TO KNOW TOO            SAVC0246    
       SLW     UCOMND        .. AND SO MAY ROGER                        SAVC0247    
       STQ     ULOPT         SAVE ULOAD OPTIONS                         SAVC0248    
       CALL    CHNGUS(Q2)    SET USER TO BE DISK USER                   SAVC0249    
       TSX     ULOAD,4       LOAD SAVED FILE                            SAVC0250    
       PAR     ULOPT         ..                                         SAVC0251    
       PAR     SN1           ..                                         SAVC0252    
       PAR     SAVED         ..                                         SAVC0253    
       PAR     LERR,,CHNERR  ..                                         SAVC0254    
       ZET     RUNSW         DO WE WANT TO START UP PROGRAM             SAVC0255    
       TRA     RSTRT.        .. YES, SIMULATE RSTART COMMAND            SAVC0256    
       LDI     Q1            .. NO, GO DORMANT                          SAVC0257    
       ZET     CHNSW         IS THIS TO GO TO CHNCOM                    SAVC0258    
       TRA     CHNCOM        .. YES, DO SO                              SAVC0259    
       TSX     DORMNT,4      .. NO, GO DORMANT (RECALL ONLY)            SAVC0260    
       VFD     O36/SB.TSX    ..                                         SAVC0261    
       REM                                                              SAVC0262    
LERR   NZT     RUNSW         IS USER EXPECTING TO RUN                   SAVC0263    
       TRA     LER1          .. NO, SKIP                                SAVC0264    
       TYPE    (/ TYPE RSTART TO PROCEED./)  ELSE, TELL HIM             SAVC0265    
LER1   TSX     DORMNT,4      .. AND THEN GO DORMANT                     SAVC0266    
       VFD     O36/SB.TSX    ..                                         SAVC0267    
       EJECT                                                            SAVC0268
       REM                                                              SAVC0269    
RSTRT. LXA     WATCOM,4      RSTART COMMAND .. IS USER BETWEEN COMMANDS SAVC0270    
       TXL     START.,4,0    .. NO, START UP USER PROGRAM               SAVC0271    
       LDI     Q1            .. YES, ARE WE TO GO TO CHNCOM             SAVC0272    
       TXL     CHNCOM,4,1    ..                                         SAVC0273    
       TXL     NEWCOM,4,2    .. OR NEWCOM (IF USER QUIT IN CHAIN)       SAVC0274    
       REM                                                              SAVC0275    
START. STZ     WATCOM        START COMMAND, RESET WAITING COMMAND SWITCHSAVC0276    
       NZT     MEMBND        DOES USER HAVE CORE IMAGE                  SAVC0277    
       TRA     ILLCOM        .. IF NOT, RETURN TO MAIN CONTROL          SAVC0278    
       TSX     $CKQUIT,4     INSURE USER STILL RUNNING                  SAVC0279    
       CAL     UCOMND        SET UP CURRENT COMMAND NAME                SAVC0280    
       SLW     COMMND,1      .. FOR THOSE OF US WHO ARE NOSY            SAVC0281    
       TRA     RSTCPU        GO RUN USER PROGRAM                        SAVC0282    
       REM                                                              SAVC0283    
       REM                                                              SAVC0284    
RSETF. TSX     CHNGUS,4      SET USER TO BE DISK USER                   SAVC0285    
       PAR     Q2            ..                                         SAVC0286    
       TSX     RESETF,4      RESET ACTIVE FILES                         SAVC0287    
       PAR     PRDIAG        .. POSSIBLE ERROR RETURN                   SAVC0288    
       TRA     SVRET         .. EXIT                                    SAVC0289    
       REM                                                              SAVC0290    
       REM                                                              SAVC0291    
       REM                                                              SAVC0292    
RUNSW  PZE                   INDICATES LOAD, START SEQUENCE             SAVC0293    
CHNSW  PZE                   INDICATES COMMAND MAY GO TO CHNCOM         SAVC0294    
SN1    PZE                   SAVED FILE NAME TO DUMP OR LOAD            SAVC0295    
MSW    PZE                   MYSAVE SWITCH (OWN UFD)                    SAVC0296    
SVOPT  VFD     O18/0,O18/D.DSK+D.OPT+D.DIR+D.TSF+D.COM+D.SAV            SAVC0297    
LDOPT0 VFD     O18/0,O18/L.MEM                                          SAVC0298    
LDOPT1 VFD     O18/0,O18/L.DSK+L.OPT+L.CPU+L.MEM                        SAVC0299    
LDOPT2 VFD     O18/0,O18/L.DSK+L.OPT+L.DIR+L.TSF+L.COM+L.CPU+L.MEM      SAVC0300    
ULOPT  PZE                   ..                                         SAVC0301    
       REM                                                              SAVC0302    
       REF     1                                                        SAVC0303
       TITLE                                                            SAVC0304
       RMT     *                                                        SAVC0305    
       DETAIL                                                           SAVC0306
       REM                                                              SAVC0307    
       END                                                              SAVC0308    
