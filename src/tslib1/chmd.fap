*      FAP
* FOR TIME SHARING MODULAR VERSION.     LP   FEB 64
       REM     ALTERED TO CALL NEW FILE SYSTEM.     EMK     AUG 65
       REM     CALLING SEQUENCE
       REM     EXECUTE CHMODE.(FILNAM, (0, 1, 2, OR 3))
       REM     X = CHMODE.(...),  X=0 WHEN SUCCESS, X=ERROR CODE IF FAIL
       REM     NAMES SET BY VECTOR VALUES, OR BY 'SETNAM'.
       REM     VECTOR VALUES NAMES=$  ALFA  BETA$
       REM     CALL SETNAM (NAMES, 12H  ALFA  BETA)
       LINK    ON
       LBL     CHMOD,X
       REM
       ENTRY   CHMODE
       REM
X4     SYN     *-2
       REM
       REM
CHMODE SYN     *
       SXD     X4,4
       CAL*    2,4
       SLW     MODE
       TSX     $GNAM,4
       PZE     X4
       PZE     FILNAM
       LDQ     MODE
       SUB     =2
       TZE     *+2           SKIP IF FORTRAN
       RQL     18
       LGL     18
       PAX     0,4
       CLA     MODES,4       GET MODE CORRESPONDING TO OLD ONE.
       STO     MODE
       TSX     $CHFILE,4     CHANGE MODE WITH CALL TO SYSTEM.
       PTH     FILNAM
       PTH     FILNAM+1
       PTH     MODE
       PTH     =-0
       PTH     =-0
       PTH     ZZZ
       ZAC
A      SYN     *
       LXD     X4,4
       TRA     3,4
       EJECT
ZZZ    XCL
       XCA
       TRA     A
       REM
       OCT     104           OLD R2 MODE
       OCT     104           OLD R1 MODE
       PZE     0             OLD  P MODE
MODES  PZE     1             OLD  T MODE
       REM                   TABLE OF OLD-NEW MODE EQUIVALENCES.
MODE   PZE
FILNAM BSS     2
       END
