*     FAP
*GLAIDE
*      AUGUST 22,1962
*   M.I.T.VERSION OF GLAIDE, AUTOMATIC INTEGRATION OF DIFFERENTIAL
*   EQUATIONS SD 413 AND 827
*   AIDE IS A LIBRARY FUNCTION OF FORTRAN
*   X = INDVF ( X,H) INCREMENTS THE INDEPENDENT VARIABLE
*   Y = DPNVF (Y,DY) INCREMENTS Y USING ADAMS FOUR POINT FORMULA
       LBL     DPNV,X
       LINK    ON
       ENTRY   INDV
       ENTRY   DPNV
NUMBER EQU     50
       PZE     INDV-3
INDV   SXD     INDV-3,4      FOR ERROR SUBPROGRAM
       SXD     ENTRT,4
       SXD     IR1,1
       SXD     IR2,2
       STO     TEMP1
       CLA     INDV-1
       TRA     DPNV+5
       PZE     DPNV-2
       PZE                   ORDER OF FORTRAN FUNCTION, SO ENTRY POINT
       BCI     1,DPNV        CAN BE FOUND BY (FPT)
DPNV   SXD     DPNV-2,4      FOR ERROR SUBPROGRAM
       SXD     IR1,1
       SXD     IR2,2
       STO     TEMP1
       CLA     DPNV-3
       STA     ERPRN+3
       STA     REPT
       STA     OUT
       STQ     TEMP1+1
ENTRT  TXL     INDV1,4,**
       LXD     COUNT,1
       LDQ     TEMP2+5+4*NUMBER,1
       FMP     TEMP2+3
       STO     TEMP1+2
       LDQ     TEMP2+5+3*NUMBER,1
TXH1   TXH     *+2,4,**
       STQ     TEMP2+5+4*NUMBER,1
       FMP     TEMP2+2
       FAD     TEMP1+2
       STO     TEMP1+2
       LDQ     TEMP2+5+2*NUMBER,1
TXH2   TXH     *+2,4,**
       STQ     TEMP2+5+3*NUMBER,1
       FMP     TEMP2+1
       FAD     TEMP1+2
       STO     TEMP1+2
       LDQ     TEMP1+1
TXH3   TXH     *+2,4,**
       STQ     TEMP2+5+2*NUMBER,1
       FMP     TEMP2
       FAD     TEMP1+2
       LRS     35
       FMP     TEMP1+3
       FAD     TEMP2+4+NUMBER,1
       FAD     TEMP1
       STQ     TEMP2+4+NUMBER,1
       TXI     *+1,1,1
       TXH     ERR2,1,NUMBER
OUT1   SXD     COUNT,1
       LXD     IR1,1
       LXD     IR2,2
       TRA     1,4
INDV1  STQ     TEMP1+3
       SLT     1
       TRA     ERR1
       LXD     TXLS+7,2
       STZ     TEMP2+5+NUMBER,2
       TIX     *-1,2,1
       TSX     COUNT+1,2
       LDQ     TXLS
       CLA     DECD+1
       STO     TEMP2
       STQ     INDV1+2
TXL8   CLA     TEMP2+4+NUMBER
       FAD     TEMP1+3
       FAD     TEMP1
       STQ     TEMP2+4+NUMBER
       AXT     1,1
       SXD     ENTRT,1
COUNT  TXL     OUT1,,**
       AXT     -1,1
       SXD     TXH1,1
       SXD     TXH2,1
       SXD     TXH3,1
       TRA     1,2
TXL1   LDQ     TXLS+1
       CLA     DECD+2
       STO     TEMP2
       CHS
       STO     TEMP2+1
       STQ     INDV1+2
       CLA     TEMP1
IR1    TXL     COUNT-2,,**
TXL2   LDQ     TXLS+2
       CLA     DECD+3
IR2    TXL    COUNT+8,,**
TXL3   LXD     OUT1-1,2
       CLA     TEMP2+5+4*NUMBER,2
       STO     TEMP2+5+2*NUMBER,2
       TIX     *-2,2,1
       CLA     DECD
       STO     TEMP2
       CLS     DECD+2
       STO     TEMP2+1
       LDQ     TXLS+3
       TRA     COUNT-7
TXL4   AXT     3,2
       CLA     TXLS,2
       STO     TEMP2+3,2
       TIX     *-2,2,1
       LDQ     TXLS+4
TR1    AXT     0,1
       TSX     COUNT+2,2
       TRA     IR1-2
TXL5   AXT     3,2
       CLA     DECD+14,2
       STO     TEMP2+3,2
       TIX     *-2,2,1
       TSX     COUNT+1,2
       LDQ     TXLS+5
       TRA     COUNT-7
TXL6   AXT     4,2
       CLS     DECD+11,2
       STO     TEMP2+4,2
       TIX     *-2,2,1
       LDQ     TXLS+6
       TRA     TR1
TXL7   AXT     4,2
       CLA     DECD+8,2
       STO     TEMP2+4,2
       TIX     *-2,2,1
       TSX     COUNT+1,2
       LDQ     TXLS+7
       TRA     COUNT-7
ERR1   CLA     LARG1
       STA     ERPRN+1
       TRA     ERPRN
ERR2   CLA     LARG2
       STA     ERPRN+1
ERPRN  TSX     $ERROR,4
       PZE     **
       TXI     *+2,,0
       PZE     **,,0
       CLA     *-1
       ADD     =1
       STA     LDMP+1        ADDRESS OF BCI NAME
       CLA     TEMP1         ARGUMENTS IN AC AND MQ
       LDQ     TEMP1+1
LDMP   TSX     $LDUMP,4
       PZE     **
       TRA     REPT
       TRA     OUT
REPT   LXD     **,4
       TRA     0,4
OUT    LXD     **,4
       TRA     1,4
LARG1  PZE     ARG1
LARG2  PZE     ARG2
ARG1   BCI     7, SENSE LIGHT 1 NOT ON UPON FIRST ENTRY
       OCT     777777777777
ARG2   BCI     9, NUMBER OF DEPENDENT VARIABLE STATEMENTS EXCEEDED 50
       OCT     777777777777
DECD   DEC     1.5
       DEC     1.
       DEC     .5
       DEC     .333333333
       DEC     2.291666667
       DEC     -2.458333333
       DEC     1.541666667
       DEC     -.375
       DEC     1.125
       DEC     -1.125
       DEC     .375
       DEC     1.916666667
       DEC     -1.333333333
       DEC     .416666667
       DEC     -.833333333
       DEC     .416666667
TXLS   TXL     TXL1
       TXL     TXL2
       TXL     TXL3
       TXL     TXL4
       TXL     TXL5
       TXL     TXL6
       TXL     TXL7
       TXL     TXL8,,NUMBER+4
TEMP1  BSS     4
TEMP2  BSS     4*NUMBER+6
       END
