*       FAP
*BST      FOR TIME-SHARING MOD XI FOREGROUND LIBRARY   JDH   DEC 62
       REM     MODIFIED FOR NEW FILE SYSTEM JUNE 65  EMK
       LBL     BST,X
       LINK    ON
       ENTRY   (RWT)
       ENTRY   (EFT)
       ENTRY   (BST)
X4     SYN     *-2
       REM
(BST)  SXD     X4,4
       TSX     $WRFLX,4
               NOTE,,4
       LXD     X4,4
       TRA     1,4
       REM
NOTE   BCI     4,BACKSPACE TAPE IGNORED.
       REM
(EFT)  SXD     X4,4          SAVE RETURN INDEX
       LGR     18
       STO     FILNAM+1
       TSX     $DEFBC,4
       PZE     FILNAM+1
       STO     FILNAM+1
       TSX     $BZEL,4
       PZE     FILNAM+1
       STO     FILNAM+1
WR     TSX     $FILE,4       GO CLOSE OUT
               FILNAM        FILE.
       LXD     X4,4
       TRA     1,4           RETURN.
       REM
FILNAM BCI     1,.TAPE.
       PZE
       EJECT
(RWT)  SXD     X4,4
       SXA     IR1,1
       LGR     18
       STO     FILNAM+1
       TSX     $DEFBC,4
       PZE     FILNAM+1
       STO     FILNAM+1
       TSX     $BZEL,4
       PZE     FILNAM+1
       STO     FILNAM+1
       TSX     $SRCH,4
       PZE     FILNAM
       TRA     RWTX
       TZE     RWTX
       PAX     ,1
       TXL     WR,1,1        STATUS 2 FOR READ.
       TSX     $ENDRD,4
       PZE     FILNAM
RWTX   LXD     X4,4
IR1    AXT     **,1
       TRA     1,4           RETURN.
       END
