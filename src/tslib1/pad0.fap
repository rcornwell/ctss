* M1416 3845  PETER R. BOS ..... PAD, BZL, NZL, ZBL, NBL ......
       PCC     ON
       NOLNK
       ENTRY   PAD           ARBITRARY PADDING
       ENTRY   BZL           BLANK LEADING ZEROS
       ENTRY   NZL           NULL LEADING ZEROS
       ENTRY   ZBL           ZERO LEADING BLANKS
       ENTRY   NBL           NULL LEADING BLANKS
       LBL     PAD00000
       PCC     OFF
       REM
PAD    SXA     PADX1,1       SAVE INDEX
       ERA*    1,4           MAKE ALL A'S ZERO
       AXT     36,1          SIX CHARACTERS
       LGR     6             SHIFT RIGHT
       TZE     *+2           .. UNTIL ALL GONE
       TIX     *-2,1,6       COUNT CHARACTERS SHIFTED
       CAL*    2,4           GET B'S TO AC
       ERA*    1,4           INVERT FOR FINAL ERA
       LGL     42,1          SHIFT BACK INTO AC
       ERA*    1,4           RESTORE PROPER CHARACTER CODES
PADX1  AXT     -,1           RESTORE INDEX
       TRA     3,4           RETURN
       REM
BZL    SXA     BZLX4,4       SAVE LINKAGE
       TSX     PAD,4         CALL CONVERTER
       PAR     =O000000000000 ..
       PAR     =O606060606060 ..
BZLX4  AXT     -,4           RESTORE LINKAGE
       TRA     1,4           RETURN
       REM
NZL    SXA     NZLX4,4       SAVE LINKAGE
       TSX     PAD,4         CALL CONVERTER
       PAR     =O000000000000 ..
       PAR     =O575757575757 ..
NZLX4  AXT     -,4           RESTORE LINKAGE
       TRA     1,4           RETURN
       REM
ZBL    SXA     ZBLX4,4       SAVE LINKAGE
       TSX     PAD,4         CALL CONVERTER
       PAR     =O606060606060 ..
       PAR     =O000000000000 ..
ZBLX4  AXT     -,4           RESTORE LINKAGE
       TRA     1,4           RETURN
       REM
NBL    SXA     NBLX4,4       SAVE LINKAGE
       TSX     PAD,4         CALL CONVERTER
       PAR     =O606060606060 ..
       PAR     =O575757575757 ..
NBLX4  AXT     -,4           RESTORE LINKAGE
       TRA     1,4           RETURN
       REM
       END

