*      FAP
*      32K 709/7090 FORTRAN LIBRARY      9XP2
*      32K 709/7090 FORTRAN LIBRARY
       TTL     EXPONENTIAL ROUTINE - FLPT BASE AND FXPT EXPONENT / 9XP2
*      AUGUST 22,1962
       LBL     EXP2,X
       LINK    ON
       DETAIL
       ENTRY   EXP(2
       REM  . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
 EXP(2 TZE  1,4                    EXIT FOR ZERO BASE
       STZ     DCTL                                                   MI
       DCT                                                            MI
       STL     DCTL                                                   MI
       SXD     EXP(2-2,4     SAVE XR4                                 MI
       SXD  SAVEX,2
       TQP  MQPOS
       LXD  NEGONE,2               SET NEGATIVE MQ INDICATOR
NEGONE TXL  MQPOS+1,0,-1
 MQPOS LXD  TXL,2                  SET POSITIVE MQ INDICATOR
       STO  POWER                  INITIALIZATION
       STO     ARGAC          SAVE ARG IN AC FOR LDUMP                MI
       STQ     ARGMQ                                                  MI
       ZAC
       LLS  17                     X
       STA  EXP                    X
       CLA  FLOONE
       NZT  EXP
       TRA  EXIT
       STO  FACTOR                 X
 CLA1  CLA  EXP                    IS POWER
       LBT                         A FACTOR
 SAVEX TXL  CLA2,0,0               NO
       LDQ  POWER                  YES
       FMP  FACTOR
       STO  FACTOR
 CLA2  CLA  EXP
       ARS  1
       STO  EXP
       TZE  TXL-1
       LDQ  POWER
       FMP  POWER
       STO  POWER
       TRA  CLA1
       CLA  FACTOR
 TXL   TXL  EXIT,2,0
       CLA  FLOONE
       FDP     FACTOR                                                 MI
       DCT                                                            MI
       TRA     ERROR1                                                 MI
       ZAC
       LLS  35
EXT    STO     ANS           KEEP THE ANSWER                         0MI
       CLA     FLOONE         TURN ON DIVIDE CHECK LIGHT              MI
       FDP     =0                                                     MI
       NZT     DCTL                                                   MI
       DCT                                                            MI
       CLA     ANS                                                    MI
 EXIT  LXD  SAVEX,2
       TRA  1,4
OUT    LXD     EXP(2-2,4                                              MI
       TRA     EXT                                                    MI
       REM  . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
ERROR1 TSX     $ERROR,4                                               MI
       PZE     A                                                      MI
       TXI     *+2,,0                                                 MI
       PZE     EXP(2-2,,0                                             MI
       CLA     FLOONE        TURN ON DIVIDE CHECK LIGHT BEFORE GOING 1MI
       FDP     =0            TO LDUMP                                2MI
       NZT     DCTL                                                  3MI
       DCT                                                           4MI
       CLA     ARGAC                                                  MI
       LDQ     ARGMQ                                                  MI
       TSX     $LDUMP,4                                               MI
       PZE     EXP(2-1                                                MI
       TRA     REPT          REPEAT      TRA 2,4                      MI
       TRA     OUT           3,4 TO GO OUT                           1MI
REPT   LXD     EXP(2-2,4                                             2MI
       LXD     SAVEX,2                                               4MI
       TRA     0,4                                                   3MI
FLOONE DEC  1.0
DCTL   PZE                                                            MI
 A     BCI ,0DIVIDE CHECK IN EXP(2 DUE TO SMALL BASE AND LARGE NEGATIVE
       BCI     3,EXPONENT.
       OCT     777777777777                                           MI
ANS                                                                  0MI
 EXP   PZE  0
       REM  WORKING STORAGE
       COMMON  -206
 POWER COMMON  1
FACTOR COMMON  1
ARGAC  COMMON  1             BASE                                     MI
ARGMQ  COMMON  1               POWER                                  MI
       END
