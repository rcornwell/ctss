*      FAP
*      GETCF, GETCFN     GET COMMON FILE NUMBER.
* FAP  TSX     GETCF,4
*      PZE     L             RETURNS INTO L AND SAC THE COMMON FILE
*      STO     K             NO. CURRENTLY SWITCHED. (0, 1, 2, 3, 4) BCD
* MAD  K=GETCF.(L)
*FORTRAN  K=GETCFN(L)
*      AUGUST 65             M. CHILD
*
       LBL     GTCF,X
       LINK    ON
TSS    MACRO   A,X,B
       TSX     B,X
       RMT
B      TIA     *+1
       BCI     1,A
       RMT
TSS    END
       ENTRY   GETCF
       ENTRY   GETCFN
X4     SYN     *-2
GETCF  SYN     *
GETCFN SYN     *
       SXD     X4,4
       TSS     GETCF,4
       STO     A
       TZE     B             SKIP IF USER'S FILES
       TSX     $GNAM,4       CHECK TYPE OF CALLING SEQUENCE
       PZE     X4
       SUB     =2
       LDQ     A
       TNZ     *+2
       RQL     18            HERE IF FORTRAN
       XCA
B      LXD     X4,4
       STO*    1,4
       TRA     2,4
*
A      BSS     1             TEMP FOR COMFIL NUMBER
       END
