*     FAP
*EXP(3   MIT LIBRARY VERSION FLOATING POINT EXPONENTIAL ROUTINE
*    DECEMBER 6, 1962
* 709 FORTRAN LIBRARY / EXPONENTIAL ROUTINE - FLPT BASE-FLPT EXP / 9XP3.
* IBM / APPLIED PROGRAMMING / JANUARY 1960.
*
       LBL     EXP3,X
       LINK    ON
       DETAIL
       ENTRY   EXP(3
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
 EXP(3 TZE     1,4           BASE ZERO
       SXD     EXP(3-2,4
       STQ     EXP1
       TPL     HER
       STO     BASE
       CLA     EXP1
       UFA     =O233000000000
       FAD     =O233000000000
*
*              THE EXPONENT HAS BEEN TRUNCATED
*              IF = , RESULT IS A FLOATING POINT INTEGRAL
       CAS     EXP1
       TRA     ERR
       TRA     CHECK
ERR    TSX     $ERROR,4
       PZE     CM
       TXI     *+2,,0
       PZE     EXP(3-2,,0
       LDQ     EXP1
       CLA     BASE
       TSX     $LDUMP,4
       PZE     EXP(3-1
       TRA     REPEAT
       TRA     LXD
REPEAT LXD     EXP(3-2,4
       TRA     0,4
CM     BCI , EXPONENTIATION ERROR, NEGATIVE BASE, NON-INTEGRAL EXPONENT
       OCT     777777777777
*
*              THE FLOATING POINT INTEGRAL IS NOW FIXED IN AC ADDRESS
*
CHECK  UFA     =O233000000000
       STL     FLAG
       LBT
       STZ     FLAG          =0  EVEN NUMBER
*                            =1  OFF NUMBER
       TRA     NHER
NHER   CLA     BASE
       LDQ     EXP1
HER    SYN     *
       TSX     LOG,4
       LRS     35
       FMP     EXP1
       TSX     EXP,4
LXD    LXD     EXP(3-2,4
       ZET     FLAG
       SSM
       STZ     FLAG
       TRA     1,4
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
       REM     LAS816 EXPONENTIAL SUBROUTINE
S816EX STO     COMMON
 EXP   SYN     S816EX
       SSP
       SUB     S816EX+55
       LRS     0
       PXD
       TQP     S816EX+51
       ADM     COMMON
       LDQ     S816EX+62
       SUB     S816EX+61
       TMI     S816EX+17
       LRS     27
       STA     S816EX+13
       PXD
       LLS     0
       DVH     S816EX+56
       ARS     8
       RQL     27
       ADD     S816EX+61
       STQ     COMMON+1
       FAD     S816EX+61
       STO     COMMON+2
       LDQ     COMMON+2
       FMP     COMMON+2
       STO     COMMON+3
       FAD     S816EX+57
       STO     COMMON+4
       CLA     S816EX+58
       FDH     COMMON+4
       STQ     COMMON+4
       CLA     S816EX+59
       FAD     COMMON+4
       STO     COMMON+4
       LDQ     COMMON+4
       FMP     COMMON+3
       FAD     S816EX+60
       STO     COMMON+3
       FSB     COMMON+2
       STO     COMMON+4
       CLA     COMMON+3
       FAD     COMMON+2
       ADD     COMMON+1
       STO     COMMON+3
       LDQ     COMMON
       TQP     S816EX+47
       CLA     COMMON+4
       LDQ     COMMON+3
       STQ     COMMON+4
       FDH     COMMON+4
       STQ     COMMON
       CLA     COMMON
       TRA     1,4
       LDQ     COMMON
       TLQ     1,4
       COM
       TRA     1,4
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
       DEC     8.73E1
       OCT     261344137676
       DEC     4.2E1
       DEC     4.9
       DEC     5.0E-2
       DEC     2.0
       OCT     200000000000
       OCT     0
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
       REM     LA S820 NATURAL LOGARITHM SUBROUTINE
LAS820 TZE     1,4
 LOG   SYN     LAS820
       SSP
       LRS     27
       STQ     COMMON
       ALS     19
       ORA     LAS820+30
       FSB     LAS820+31
       STO     COMMON+1
       CLA     COMMON
       LRS     8
       ORA     LAS820+32
       FAD     LAS820+33
       STO     COMMON
       FSB     LAS820+34
       FDH     COMMON
       STQ     COMMON
       FMP     COMMON
       STO     COMMON+2
       LDQ     LAS820+37
       FMP     COMMON+2
       FAD     LAS820+36
       LRS     35
       FMP     COMMON+2
       FAD     LAS820+35
       LRS     35
       FMP     COMMON
       FAD     COMMON+1
       LRS     35
       FMP     LAS820+38
       TRA     1,4
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
       OCT     210000000000
       DEC     1.285E2
       OCT     200000000000
       DEC     7.07106781187E-1
       DEC     1.41421356237
       DEC     2.88539129030
       DEC     9.614706323E-1
       DEC     5.989786496E-1
       DEC     6.9314718056E-1
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
       REM     WORKING STORAGE
       COMMON  -206+4
COMMON COMMON  1
 EXP1  COMMON  1
 BASE  COMMON  1
 TXL   COMMON  1
       REM     . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
FLAG   PZE     0
       END
