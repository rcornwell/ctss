*      LABEL
*      FAP
*TRANSP        TRANSPOSED MATRIX SUBSCRIPTION
       LINK    ON
       ENTRY   TRANSP
       ENTRY   (SUBS         CHECKING PROCEEDURE FOR SPECIAL SUBSCRIPTS
       ENTRY   .SUBS
       ENTRY   .03310        SECONDARY ENTRIES TO PREVENT
       ENTRY   .03311        DUPLICATION IN CORE.
(SUBS  STO     I
.03310 SYN     (SUBS
.03311 SYN     (SUBS
.SUBS  SYN     (SUBS
       STQ     J
       SXA     IR1,1
       CLA     1,4
       STO     L1
       ARS     18
       STA     L4
       STA     L5
       STA     *+1
       CLA     **            PICK UP FIRST D.V. ELEMENT
       TPL     L2            TRANSFER FOR (03310 OR (03311
       SXD     IR4,4
       STA     *+1           SET CALL FOR ROUTINE DESIRED
       TSX     **,4          PERFORM SUBSCRIPTION CALCULATION.
L1     PZE
       TXH     I,,0
       TXH     J,,0
       LXD     IR4,4
       LXA     IR1,1
       TRA     2,4
I      PZE
J      PZE
AE1    PZE     1
IR     PZE     3,,2
SAVE   PZE
TRANSP CLA     J             TRANSPOSED MATRIX SUBSCRIPTION-ENTER HERE
       LDQ     I
       STQ     J
       STO     I
       SXA     IR1,1
       LXA     IR,1
IR4    TXL     L3,,**
L2     LXD     IR,1
       CLA     I
L3     SUB     AE1
       STO     SAVE
       LDQ     SAVE
L4     MPY     **,1
       STQ     SAVE
       CLA     SAVE
       TXI     *+1,1,-1
L5     ADD     **,1
       ADD     J
       SUB     AE1           (I-1)N+B+(J-1) LINEAR SUBSCRIPT.
       LXA     IR1,1
       TRA     2,4
IR1    PZE
       END
