*
*.01301  EXPONENTIATION SUBROUTINE  FLOATING TO FIXED
*      AUGUST 22,1962
       LBL     1301,X
       LINK    ON
       REM
       REM     AC=BASE (FLOATING)
       REM     MQ=EXPONENT (INTEGER)
       REM
       REM
       REM
       ENTRY   .01301
.01301 XCA                   GET EXPONENT INTO AC
       TNZ     *+3
       CLA     =1.
       TRA     1,4
       XCA                   GET BASE INTO AC
       TZE     1,4
       STO     BASE
       STQ     EXP
       XCA
       TPL     EF3
       CLA     =1.           IF NEGATIVE EXPONENT
       FDP     BASE          TAKE RECIPROCAL OF BASE.
       STQ     BASE
EF3    CLA     =1.
       STO     PX
EF1    CLA     EXP
       LBT
       TXL     ZERO
       LDQ     PX
       FMP     BASE
       STO     PX
       CLA     EXP
ZERO   ARS     1
       STO     EXP
       TZE     EXIT
       LDQ     BASE
       FMP     BASE
       STO     BASE
       TXL     EF1
EXIT   CLA     PX
       TRA     1,4
EXP    BSS     1
PX     BSS     1
BASE   BSS     1
END    SYN     *
       END
