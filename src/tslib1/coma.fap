*      FAP
*      COMARG
*      GET SPECIFIED ARGUMENT FROM COMMAND BUFFER, RIGHT JUSTIFIED
* CALLING SEQUENCE
*FAP   TSX     COMARG,4
*      PZE     =N            N.TH ARG.
*      (PZE    ARG           ARGUMENT)
*      STO     ARG           ARGUMENT
* MAD  EXECUTE COMARG.(N, ARG)
*      ARG=COMARG.(N, ARG1)  ARG1 OPTIONAL
*FORTR CALL COMARG(N, NARG)
*      K=COMARG(N, L)        L OPTIONAL
       REM
* COMPUTATION CENTER        LOUIS POUZIN
*      JUNE 64
       LBL     COMARG,X
       LINK    ON
       REM
       REM
       ENTRY   COMARG
       REM
       REM
X4     SYN     *-2
COMARG SYN     *
       SXD     X4,4
       TXI     *+1,4,-1
       SXD     C,4
       TSX     $GNAM,4
       PZE     X4
       LXD     X4,4
       LDQ*    1,4
       SUB     =2
       TNZ     A             SKIP IF NOT FORTRAN CALL
       RQL     18            N IN ADDRESS
A      STQ     B
       TSX     $GETCOM,4
B      PZE     **            N.TH ARGUMENT
       SLW     B             TEMPORARY
       TSX     $GNAM,4
C      PZE     *,,**         IX4-1 ON CALLING
       LDQ     B
       LXD     X4,4
       TZE     D             SKIP IF SINGLE ARGUMENT
       STQ*    2,4
       TXI     *+1,4,-1      FOR RETURN IN 3,4
D      XCA
       TRA     2,4
       END
