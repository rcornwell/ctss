*      FAP
* FOR TIME SHARING MODULAR VERSION.     LP   FEB 64
       REM     X = FSTAT.(FILNAM)
       REM     RETURNS 0 IF FILE NOT THERE OR OPEN FOR WRITING ONLY
       REM     RETURNS MODE, MODULE NO., EST. WORD COUNT, IN AC(S-35)
       REM     NAMES SET BY VECTOR VALUES, OR BY 'SETNAM'.
       REM     VECTOR VALUES NAMES=$  ALFA  BETA$
       REM     CALL SETNAM (NAMES, 12H  ALFA  BETA)
       REM
       REM
       LBL     FSTAT,X
       ENTRY   FSTAT
       REM
X4     SYN     *-2
       REM
FSTAT  SYN     *
       SXD     X4,4
       STI     INDIC         SAVE INDICATORS
       TSX     $GNAM,4
       PZE     X4
       PZE     FILNAM
       TSX     $FSTATE,4
       PTH     FILNAM
       PTH     FILNAM+1
       PTH     E,,3
       PTH     ERR
       CLA     E+2
       SUB     =3            TRA IF OPEN FOR WRITING ONLY
       TZE     ERR
       CAL     =O100000000000
       LDI     E+1
       RFT     1
       PXD     ,0            ZERO IF TEMPORARY
       RFT     104
       CAL     =O200000000000 -VE IF PROT AND/OR READ ONLY
       STP     E
       CLA     E
H      LXD     X4,4
       LDI     INDIC         RESTORE INDICATORS
       TRA     2,4
ERR    ZAC
       TRA     H
FILNAM BSS     2
E      BSS     3
INDIC  PZE
       END
