*      FAP
*      AUGUST 22,1962
       REM MERWIN  ET AL
       REM     REVISED TO ACCEPT MAD CALLING SEQUENCES
       REM     MAY, 1961
       REM
       REM GENERATION))   U(0) =ANY ODD NUMBER
       REM U(N)=U(N-1)*(2**18+3)  (MOD 2**35)
       REM     THE CALLING SEQUENCE FOR SETU IS
       REM
       REM           CALL  SETU            TSX  $SETU,4
       REM           ARG=N IN AC           TXH  N
       REM        FOR FORTRAN           FOR MAD
       REM
       REM     FOR RANNO IT IS SIMPLY
       REM
       REM           CALL  RANNO,  WITH N IN THE AC
       REM
       LBL     RANN,X
       LINK    ON
       REM
       ENTRY   RANNO
       ENTRY   SETU
RANNO  TNO  *+4              SAVE THE STATUS OF THE
       CLA  FIX               .  OVERFLOW INDICATOR
       STO  COMMON            .
       TRA  *+2               .
       STZ  COMMON            .
       CLA U
       ALS 18
       ADD U
       ADD U
       ADD U
       STO U
       CLA U
       LRS 8
       ORA FIX
       FAD FIX
       STO COMMON+1
       TOV  *+1              RESTORE OVERFLOW STATUS
       CLA  COMMON            .
       ALS  3                 .
       CLA COMMON+1
       TRA 1,4
       EJECT
SETU   ARS     18
       STO     U
       CLA     TXH           MODE TEST--MAD OR FORTRAN
       LDQ     1,4            .
       TLQ     1,4            .  FORTRAN--RETURN
       CLA*    1,4            .  GET MAD ARGUMENT
       STO     U              .
       TRA     2,4            .  RETURN
       REM
       REM
       REM
       REM
       REM
       REM     CONSTANTS
       REM
       REM
 U     OCT 1
 FIX   DEC 128B8
TXH    TXH                   FOR MAD-FORTRAN TEST
       REM
       COMMON  -205
COMMON COMMON  1
       END
