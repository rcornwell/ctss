*      FAP
* PCMT   PRINT COMMENT   FROM MAD                  DAGGETT  FEB. 18,1964
       REM     CORRECTED TO ALLOW SINGLE LINE OF 132 CHARCS  EMK 4/12/65
       REM     SUPERVISOR WILL ADD NECESSARY CARRIAGE RETURNS.
       PCC
       LBL     PCM,X                                                  MI
************************************************************************
*                                                                      *
*                                                                      *
*              PRINT COMMENT SUBROUTINE FOR MAD SIMPLIFIED OUTPUT      *
*                                                                      *
*              CALLING SEQUENCE--                                      *
*                                                                      *
*                                 TSX     .PCOMT,4                     *
*                                 PAR     N                            *
*                                 BCI     N,XXXXXXXXXX....             *
*                                                                      *
*              WHERE N IS THE NUMBER OF BCD WORDS TO BE PRINTED.       *
*                                                                      *
*                                                                      *
************************************************************************
       REM
       REM
       REM
       ENTRY   .PCOMT
BLK    OPSYN   PZE
       REM
.PCOMT SXD     IR4,4
 IR4   SYN     .PCOMT-2
       CAL     1,4                PICK UP COUNT
       PAX     0,4
       SXD     BLK,4              STORE COUNT
       SXD     INCR,4
WMAX   EQU     14
       LDC     IR4,4         GET LOCATION OF CALL
       TXI     *+1,4,2            SET TO BEGINNING OF BCD
       SXA     BLK,4
INCR   TXI     *+1,4,**           SET EXIT
       SXA     EXIT,4
       LXD     INCR,4
       TIX     OVER,4,WMAX     HAVE WE MORE THAN 14 WORDS
       TSX     $WRFLX,4
BLK    BLK     **,,**
       LXD     IR4,4
EXIT   TRA     **                 RETURN
*                            HERE IF MORE THAN 14 WORDS
OVER   SXD     BLK,4
       LXA     BLK,4           GET STARTING LOC. FOR PRINTING
       SXA     W1,4
       TXI     *+1,4,WMAX
       SXA     BLK,4
       TSX     $WRFLXA,4
W1     PZE     **,,WMAX
       TRA     BLK-1
       END
