*      FAP
*      TSSENTRIES * FOREGROUND LIB * L.P. - JULY 63 *
       REM     SUBROUTINIZED ENTRIES TO TSS ROUTINES
       LINK    ON
       ENTRY   RDFLX
       EXTERN  RDFLXA
RDFLX  SXA     IR4,4
       SXA     IR2,2
       TSX     RDFLXA,4
               BUF,,14
       REM     RDFLX RETURNS 14 WORDS WITH BREAK CHARACTER. AC CONTAINS
       REM     N, WHERE BREAK CHARACTER IS NTH CHARACTER
       REM     REMAINING CHARACTERS ARE BLANKS
       REM
       STZ     OVF
       ALS     1
       STT     OVF
       ANA     =O77777
       ARS     1
       ZET     OVF
       TRA     IR4
       SUB     =1
       XCA
       PXD
       DVP     =6
       XCA
       PAC     ,2
       MPY     =6
       XCA
       PAX     ,4
       CAL     BUF,2
       ARS     36,4
       LDQ     =H
       LGL     36,4
       SLW     BUF,2
IR4    AXT     **,4
       CLA     1,4
       ARS     18
       ADD     1,4
       PDX     ,2
       PDX     ,4
       STA     MOVE+1
       TXI     *+1,4,BUF
       SXA     MOVE,4
MOVE   CAL     **,2
       SLW     **,2
       TIX     MOVE,2,1
       LXA     IR4,4
IR2    AXT     **,2
       TRA     2,4
OVF    PZE                   1 IN TAG IF MORE THAN 14 WORDS
BUF    BSS     14
       END
