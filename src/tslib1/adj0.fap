*M1416 3845  PETER R. BOS .... ADJ, LJ, RJ .. LEFT/RIGHT JUSTIFY ....
       PCC     ON
       ENTRY   ADJ           ARBITRARY JUSTIFICATION
       ENTRY   LJ            LEFT JUSTIFY WITHIN BLANKS
       ENTRY   RJ            RIGHT JUSTIFY WITHIN BLANKS
       LBL     ADJ00000
       PCC     OFF
       REM
ADJ    ERA*    1,4           MAKE LEADING FIELD ZERO
       LDQ     =0            CLEAR MQ
ADJ0   NZT*    2,4           LEFT OR RIGHT.Q
       TRA     ADJ1          LEFT, SKIP
       XCL                   ..
       LGL     6             SHIFT ONE CHAR
       XCL                   ..
       TRA     *+2           SKIP
ADJ1   LGR     6             SHIFT ONE CHAR
       TNZ     ADJ0          REPEAT IF NOT DONE
       XCL                   RESULT TO LAC
       ERA*    1,4           RESTORE PROER CODES
       TRA     3,4           RETURN
       REM
LJ     SXA     LJX4,4        LEFT ADJUST, SAVE LINKAGE
       TSX     ADJ,4         CALL ADJUSTER
       PAR     =H            ..
       PAR     =0            ..
LJX4   AXT     -,4           RESTORE LINKAGE
       TRA     1,4           RETURN
       REM
RJ     SXA     RJX4,4        RIGHT ADJUST, SAVE LINKAGE
       TSX     ADJ,4         CALL ADJUSTER
       PAR     =H            ..
       PAR     *             ..
RJX4   AXT     -,4           RESTORE LINKAGE
       TRA     1,4           RETURN
       REM
       END
