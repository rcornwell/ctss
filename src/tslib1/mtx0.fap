*      FAP
*      AUGUST 21,1962
*MTX       N-DIMENSIONAL MATRIX SUBSCRIPTION SUBROUTINE JULY 13, 1961
       LBL     MTX,X
       LINK    ON
       ENTRY   .MTX
       REM     E.G.,A(I1,I2,I3),N1XN2XN3,A(1,1,1)=A(B)
       REM     THEN LINEAR SUBSCRIPT IS
       REM     ((I1-1)*N2+(I2-1))N3+(I3-1)+B
       REM                   CALLING SEQUENCE..
       REM                   TSX .MTX,4
       REM                   TXHA,0,D
       REM                   TXH I1
       REM                   ...
       REM                   TXH IN
       REM                   A IS ADDR OF A(0)
       REM                   D IS ADDR OF DIM. VEC.
 .MTX  CLA     1,4           GET ADD. OF DIM. VECTOR
       ARS     18
       STA     IAT2
       SUB     =1
       STA     FPL1          SET ADDRESS OF ARRAY BASE
 IAT2  CLS     **            CHECK FOR SPECIAL SUBSCRIPT FUNCTION
       TPL*    IAT2          IF YES--GO TO IT
       PAC     ,1            (-N) TO IR 1
       ADD     IAT2          COMPUTE
       SUB     =1            AND SET END OF DIM
       STA     IAT1          VECTOR+ONE
       PXD                   ZERO INITIAL SUM
 PL1   ADD*    2,4           SUM+I(J),J=1,...,N
       SUB     =1            SUM+I(J)-1
       TXI     *+1,1,1
       TXL     FPL1,1,0      TEST FOR (N-1) MULTIPLIES,K=K+1
       STO     TS1
IAT1   LDQ     **,1
       VLM     TS1,,15       (SUM+I(J)-1)*N(K),K=2,...,N
       LLS     15            SUM=(SUM+I(J)-1)*N(K))
       TXI     PL1,4,-1      J=J+1
 FPL1  ADD     **            SUM+I(J)-1+B
       TRA     3,4           RETURN
 TS1   BSS     1
 END   SYN     *
       END
