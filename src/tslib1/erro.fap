*      FAP
       LBL     ERRO,X
       LINK    ON
       ENTRY   ERROR
ERROR  SXA     IR1,1
       SXA     IR2,2
       SXA     IR4,4
       SXD     SX4,4
       SCA     ER2,4
       STZ     PR2
       TSX     $COLT,4
               SX4
       PAX     ,1
       TXL     ER50,1,0
       AXT     -1,2
ER2    CAL     **,2
       SLW     PR
       TSX     $PRNTP,4      PRINT ARGUMENT MESSAGE
PR     PZE
       CAL     =HERROR
       SLW     PR1
ER6    CAL*    $MOVIE)
       ARS     18
       ACL*    $MOVIE)
       STA     ER41
       SBM     =2
       STA     ER10
       ACL     =1
       STA     ER30
       STA     ER70
       SBM     =3
       STA     ER20
ER7    AXT     2,2
       LXD     SX4,4         STORE VALUE OF X4 TO PARAM
       TXI     *+1,2,2
ER10   CAL     **,2          T.V. (INIT MAIN)
       PAX     ,1
       TIX     *-3,1,0
ER20   CAL     **,2          BCD NAME
       TMI     ER50
       PCA     ,4
ER30   LAS     **,2          ORIGIN
       TXI     ER10,2,4
       TXH
ER40   TXL     ER50,2,4
ER41   CAL     **,2          NAME OF SUBPR
       TZE     ER60
       SXA     SX4,2
       CAL     PR2
       TZE     ER70
       CAL     PR1
       SLW     DER1
       CAL     PR2
       SLW     DER2
       TSX     $WRFLX,4      PRINT ERROR CALL MESSAGE
               DER,,5
       CAL     PR2
       SLW     PR1
       TRA     ER70
ER50   CAL     PR2
       TZE     ER51
       LXA     SX4,2
       XEC     ER41
       TRA     ER52
ER51   CAL     =HERROR       TRACE FAILURE
       SLW     DER1
       TSX     $WRFLX,4
               DER,,4
       CAL     =HCALL
ER52   SLW     TF+3
       TSX     $WRFLX,4
               TF,,4
       TRA     FIN
ER60   CAL     PR2
       TZE     ER61
       CAL     PR1
       SLW     DER1
       CAL     PR2
       SLW     DER2
       TSX     $WRFLX,4
               DER,,5
ER61   CAL     =H(MAIN)
       SLW     DER2          END OF TRACE
       CAL     PR2
       TNZ     ER62
       CAL     =HERROR
ER62   SLW     DER1
       TSX     $WRFLX,4
               DER,,5
       TRA     FIN
ER70   CAL     **,2
       SLW     END
ER100  TXI     *+1,2,-2
       XEC     ER10
       PAX     ,1
       TIX     ER100,1,0
       XEC     ER70
       SLW     ORG           STORE PROG ORIGIN
       TSX     $COLT,4
               SX4
       PAX     ,2            LANGUAGE CODE
       XEC     ER110,2
       TRA     MAD
       TRA     FAP
       TRA     FAP
ER110  TRA     ER50

FAP    SYN     *
       CLA     END
       SUB     ORG
       LAS     =20
       CLA     =20
       TXH
       PAX     ,1            SET X1 = COUNT
       ADD     ORG
       STA     *+1           STORE ADDT
ER120  CLA     **,1
       STA     INST
       LAS     INST
       TIX     ER120,1,1
       TRA     ER121
       TIX     ER120,1,1
       TRA     ER50
ER121  PAX     ,2
       TMI     *+4
       SXA     LXA,2
LXA    LXA     **,4
       TRA     *+3
       SXA     LXD,2
LXD    LXD     **,4
       SXD     SX4,4
ER135  LXA     SX4,2         RESTORE X2 TO MOVIE MOD
       CAL     0,4
       STA     *+1
       CAL     **
       PAX     ,1
       PXA     ,1            SET ENTRY ADDT IN AC
       TXI     *+1,2,-2      SET X2 TO PICK UP BCD
       XEC     ER30
       TRA     *-2
       TXI     *+2,2,-2
       TRA     *-4
       XEC     ER20
ER136  SLW     PR2
       TNZ     ER7
       CAL     =1
       SLW     PR2
       TRA     ER50
MAD    SYN     *
       CAL     ORG
       PAC     ,1
ER140  CAL     0,1
       STA     TSX2
       LAS     TSX2
       TRA     *+2
       TRA     ER150         LOCATE   TSX  PROLOG,2
       TIX     ER140,1,1
       TRA     ER50
ER150  ADD     =2
       STA     *+1
       CAL     **
       TRA     ER121
FIN    SYN     *
       TSX     $WRFLX,4
               EXER,,3
IR1    AXT     **,1
IR2    AXT     **,2
IR4    AXT     **,4
       TRA     2,4
DER    BCI     5,ENTRY  NUTS   CALLED BY
TF     BCI     4,TRACE FAILURE IN
EXER   BCI     3,EXIT FROM ERROR
SX4    PZE
ORG    PZE
END    PZE
INST   SXA     0,4
TSX2   TSX     0,2
PR1    PZE
PR2    PZE
DER1   SYN     DER+1
DER2   SYN     DER+4
       END
