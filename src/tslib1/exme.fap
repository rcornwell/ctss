*      FAP
       LINK    ON
       ENTRY   EXMEM
       EXTERN  GETMEM,SETMEM
N      SYN     *-2
EXMEM  SYN     *
       STZ     NA
       SXA     N,4
       SXD     N,2
       CAL     1,4
       ANA     =O777777700000
       AXT     4,2
       LAS     TYPE,2
       TIX     *-1,2,1        SELECT COMPILER LANGUAGE
       TRA     *+2
       TIX     *-3,2,1
       TXL     NX,2,1       EXIT FOR INVALIDITY
       CAL*    1,4
       PDX     ,4             PLACE 'INCR' IN XR4-FORTRAN
       TXL     *+2,2,2
       PAX     ,4
       SXA     W,4            PLACE 'INCR' IN XR4-FAP,MAD
       TSX     GETMEM,4
       ADD     W
       STA     LOOP
       ADM     =O777777700000
       PBT                    TEST IF NEW MEMORY BOUND
       TRA     NX4            WITHIN CORE LIMITS
       TRA     NX
NX4    PAX     ,4
       TXL     NX2,2,2
       SXA     NA,4
NX3    TSX     SETMEM,4
       LXA     W,4
LOOP   STZ     **,4
       TIX     LOOP,4,1
NX     CLA     NA
       LXA     N,4
       LXD     N,2
       TRA     2,4
NX2    SXD     NA,4
       TRA     NX3
       PZE
       TXH
       TSX
       PZE
TYPE   SYN     *
W      PZE                   'INCR' IN ADDR
NA     PZE                   'NMB' IN INTEGER FORM
       END
