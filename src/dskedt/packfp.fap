*      FAP CODE TO REPLACE MAD PACKING ROUTINE                
       LINK    ON
*      IN HOPES OF SPEEDING UP THE PRINT ROUTINE
*      MOVE SINGLE CHARACTERS FROM CARD TO FULL WORDS AT L. 
*      CALL PACKFP.(CARD,L,END,GW2) 
       ENTRY   PACKFP   
PACKFP SXA     X1,1     
       SXA     X1+1,2   
       SXA     X1+2,4   
       CAL     1,4
       STA     CARD     
       CLA     2,4
       STA     L  
       CLA*    3,4           END OC CODES IN CARD     
       PAX     0,1
       SXD     ENDTST,1 
       CLA*    4,4
       PAX     0,1           PRESET IR1 TO GW2, BOTTOM OF L 
       AXT     0,4           FIRST CARD   
CRDLP  AXT     6,2           6 CHAR PER WORD    
LLP    ALS     6  
       TXI     *+1,4,1  
ENDTST TXL     *+3,4,**      END,BOTTOM OF CARD 
       LDQ     BLANK    
       STQ*    CARD     
CARD   ORA     **,4     
       TIX     LLP,2,1  
L      SLW     **,1     
       TIX     CRDLP,1,1
X1     AXT     **,1     
       AXT     **,2     
       AXT     **,4     
       TRA     5,4
BLANK  OCT     60 
       END  
