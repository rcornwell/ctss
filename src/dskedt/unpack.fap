*      NCHAR = UNPACK.(INPUT,LNGTH,OUTPUT,MAXCHR)     
       TITLE
       LINK    OFF
*      UNPACK WORDS INTO CHARACTERS AND IGNORE NULLS  
       ENTRY   UNPACK   
UNPACK SXA     X4,4     
       SXA     X2,2     
       SXA     X1,1     
       CAL     1,4           INPUT  
       STA     GET
       CAL*    2,4           NUMBER OF WORDS IN INPUT 
       PAX     0,1
       SXD     LTH,1    
       CAL     3,4           OUTPUT 
       STA     PUT
       CAL*    4,4           MAX OUTPUT CHRS.   
       PAX     0,1
       TXI     *+1,1,1  
       SXD     MXCHRS,1 
       AXT     1,1           INPUT INDEX  
       AXT     1,4           OUTPUT ONDEX 
GET    LDQ     **,1          GET WORD     
       AXT     36,2     
CHLP   ZAC  
       LGL     6             SHIFT CHAR TO AC   
       LAS     =O57          CHECK FOR NULL     
       TRA     *+2
       TRA     TIX
MXCHRS TXH     FULL,4,**     CHECK FOR FULL OUTPUT    
PUT    SLW     **,4          STORE CHAR   
       TXI     *+1,4,1  
TIX    TIX     CHLP,2,6 
       TXI     *+1,1,1  
LTH    TXL     GET,1,** 
FULL   PXA     0,4           CHAR COUNT IN AC   
X4     AXT     **,4     
X2     AXT     **,2     
X1     AXT     **,1     
       TRA     5,4
       END  
