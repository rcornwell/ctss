*      DEFBC   CONVERTS BINARY INTEGER TO BCD FORM                    
*COMPUTATION CENTER          LOUIS POUZIN 
*      JUNE    64 
*      CALLING SEQUENCE 
*FAP   TSX     DEFBC,4       DEFBC USES FULL WORD     
*      PZE     DATA          DERBC USES RIGHT HALF ONLY     
*      STO     RESULT        DELBC USES LEFT HALF ONLY
*MAD   R=DERBC.(DATA)   
*FORTR R=DELBC(DATA)    
       LINK    ON
       LBL     DEBC,X
       REM
       REM
       ENTRY   DEFBC
       ENTRY   DELBC
       ENTRY   DERBC
       REM
       REM
X4     SYN     *-2
       REM
DELBC  SYN     *  
       CAL*    1,4
       ARS     18 
       TRA     A  
       REM
DERBC  SYN     *  
       CAL*    1,4
A      ANA     =O777777 
       TRA     B  
       REM
DEFBC  SYN     *  
       CAL*    1,4
B      XCA  
       SXD     X4,4     
       AXT     36,4     
       STZ     D  
C      ZAC  
       DVP     =10           SUCCESSIVE DIGITS FROM RIGHT TO LEFT 
       ALS     36,4          SHIFT TO PROPER RANK     
       ORS     D  
       TIX     C,4,6    
       LXD     X4,4     
       CLA     D  
       TRA     2,4
       EJECT
D      PZE                   TEMPORARY RESULT   
       END  
