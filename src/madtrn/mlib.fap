*
       ENTRY   BCDBN    
       ENTRY   BNBCD    
       ENTRY   COMPZ    
       ENTRY   DCOMPZ   
       ENTRY   ZERO     
       ENTRY   ZCOMPZ   
*      ROUTINES USED BY MADTRN3     
*     
*     BCDBN 
BCDBN  LDQ*    1,4
       SXD     COMMON,1 
       STZ     COMMON-2 
       LXD     SIX,1    
LOOPA  PXD  
       LGL     6  
       STO     COMMON-1 
       CLA     COMMON-2 
       ALS     2  
       ADD     COMMON-2 
       ALS     1  
       ADD     COMMON-1 
       STO     COMMON-2 
       TIX     LOOPA,1,1
       LXD     COMMON,1 
HEREA  TRA     2,4
*     
*      BNBCD
*     
BNBCD  LDQ*    1,4
       SXD     COMMON,1 
       STZ     COMMON-1 
       LXA     SIX,1    
LOOP   PXD  
       DVP     TEN
       ALS     36,1     
       ORS     COMMON-1 
       TIX     LOOP,1,6 
       CLA     COMMON-1 
       LXD     COMMON,1 
       TRA     2,4
SIX    PZE     36,,6    
TEN    PZE     10 
*     
*     COMPZ 
*     
COMPZ  STZ     COMMON-3 
       TRA     *+2
ZCOMPZ SXD     COMMON-3,4     
       STZ     COMMON-1 
       CAL     3,4
       ANA     P3 
       CAS     P3 
       TRA     *+2
       SXD     COMMON-1,4     
       SXA     SV1,1    
       SXA     SV2,2    
       SXA     SV4,4    
       CLA*    1,4
       TZE     3,4
       TMI     3,4
       SUB     ONE
       ALS     18 
       TZE     TEST     
IN     STD     THAT     
       STD     THIS     
       CLA     2,4
       STA     HERE     
       CLA     3,4
       STA     THERE    
       STA     ACA
       LXD     SIXA,6   
       LXA     SIXA,1   
       PXD  
HERE   LDQ     **,4     
       LGL     6  
       TIX     OUT,1,6  
       NZT     COMMON-1 
       TRA     EXIT     
THERE  SLW     **,2     
       TXI     *+1,2,1  
       TXI     *+1,4,1  
THIS   TXL     HERE-2,4,0     
       TRA     EXIT     
OUT    TXI     *+1,4,1  
THAT   TXL     HERE,4,0 
       ZET     COMMON-3 
       TRA     ACA-2    
       SLW     COMMON-4 
       PXD     0,1
       ARS     18 
       STA     HOP
       CAL     COMMON-4 
       LDQ     SPACE    
HOP    LGL     ** 
       NZT     COMMON-1 
       TRA     EXIT     
ACA    SLW     **,2     
EXIT   SLW     COMMON-4 
       CLA     COMMON-4 
SV1    AXT     0,1
SV2    AXT     0,2
SV4    AXT     0,4
       TRA     3,4
OUTER  CAL*    2,4
       NZT     COMMON-1 
       TRA     EXIT     
       SLW*    3,4
       TRA     EXIT     
TEST   NZT     COMMON-3 
       TRA     OUTER    
       TRA     IN 
SIXA   PZE     36 
ONE    PZE     1  
SPACE  OCT     606060606060   
TEMP   PZE  
P3     PTH  
       BSS     4  
COMMON PZE     0  
*     
*     DCOMPZ
*     
DCOMPZ CLA*    1,4
       TZE     4,4
       TMI     4,4
       SUB     ONE
       ALS     18 
       STD     DCOM4    
       SXA     DCOM5,1  
       SXA     DCOM5+1,2
       SXA     DCOM5+2,4
       CLA     2,4           INITIALIZE ADRESSES
       STA     DCOM1    
       CLA     3,4
       STA     DCOM3    
       AXT     0,1
       AXT     0,2
DCOM1  LDQ     **,1     
       AXT     6,4
DCOM2  PXD     0,0
       LGL     6  
       ALS     30 
       ORA     =H0
DCOM3  SLW     **,2     
       TXI     *+1,2,1  
       TIX     DCOM2,4,1
       TXI     *+1,1,1  
DCOM4  TXL     DCOM1,1,**     
DCOM5  AXT     **,1     
       AXT     **,2     
       AXT     **,4     
       TRA     4,4
*     
*    ZERO   
*     
ZERO   SXA     RX1,1    
       SXA     RX2,2    
       SXA     RX4,4    
       CLA     1,4
       STA     SMAL     
       ARS     18 
       STA     BIG
       CLA     BIG
       LDQ     SMAL     
       TLQ     *+2
       XCA  
       STQ     SMAL     
       SUB     SMAL     
       ADD     ONE
       PAX     0,1
       AXT     0,2
       LXA     SMAL,4   
       SXA     *+1,4    
       STZ     **,2     
       TXI     *+1,2,-1 
       TIX     *-2,1,1  
RX1    AXT     **,1     
RX2    AXT     **,2     
RX4    AXT     **,4     
       TRA     2,4
SMAL   PZE  
BIG    PZE  
       END  
